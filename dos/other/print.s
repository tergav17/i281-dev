; PRINTER TEST PROGRAM
; GAVIN TERSTEEG, 2024

AY0_ADR	= 0XB0
AY1_ADR	= 0XB2
AY0_WR	= 0XB1
AY1_WR	= 0XB3
AY0_RD	= 0XB0
AY1_RD	= 0XB2

.TEXT

	; SET UP AY CHIPS
	LOADI	A,7
	STORE	[AY1_ADR],A
	STORE	[AY0_ADR],A
	LOADI	A,0XBF
	STORE	[AY1_WR],A
	LOADI	A,0XFF
	STORE	[AY0_WR],A

	; SET D REGISTER TO STRING
	LOADI	D,HELLO

	; SET DEVICE CONTROL STATE
	LOADI	A,15
	STORE	[AY1_ADR],A
	LOADI	A,0XFF
	STORE	[AY1_WR],A
	
	; WAIT BUSY SIGNAL TO CLEAR
LOOP:	LOADI	A,14
	STORE	[AY1_ADR],A
0:	LOAD	A,[AY1_RD]
	SHIFTR	A
	SHIFTR	A
	SHIFTR	A
	BRC	0B
	
	; OUTPUT CHARACTER
	LOADF	B,[D]
	ADDI	B,0
	BRZ	@
	LOADI	A,15
	STORE	[AY0_ADR],A
	STORE	[AY0_WR],B
	ADDI	D,1

	; STROBE PRINTER
	LOADI	A,15
	STORE	[AY1_ADR],A
	LOADI	A,0XF7
	STORE	[AY1_WR],A
	LOADI	A,0XFF
	STORE	[AY1_WR],A
	JUMP	LOOP

.DATA 

.DEFL BYTE HELLO	"HELLO FROM THE PRINTER!",0X0A,0X0D,0