; FS.S
; FILE SYSTEM HANDLING ROUTINES

.BANK BI
FS0_B	= BI
.TEXT
	; SET SEARCH PARAMETERS
	; USING A STRING IN USER SPACE, THE FILE SEARCH PATTERN WILL
	; BE UPDATED
	; THE FILE SEARCH POINTER WILL ALSO BE RESET
	; ASSUMES WORK BANK IS SELECTED
	; A = ADDRESS OF STRING
	; [SRC_BNK] = DATA BANK OF STRING
	; RETURNS A=0X00 IF PATTERN IS VALID, OTHERWISE 0XFF
	; USES: A, B, C, M0, M1, M2
SSPARAM:STORE	[M0],A

	; STORE PATTERN COUNTER
	LOADI	A,6
	STORE	[M1],A
	
	; STORE PATTERN POINTER
	LOADI	A,PATTERN
	STORE	[M2],A
	
	; RESET THE PATTERN
	LOADI	A,PATTERN
	LOADI	B,8
	LOADI	C,0X20
0:	STOREF	[A],C
	ADDI	A,1
	SUBI	B,1
	BRNZ	0B
	
	; DO AN INITAL CHECKOUT OF THE PATTERN
	LOAD	A,[SRC_BNK]
	LOAD	B,[M0]
	STORE	[DBANK],A
	
	; MAKE SURE THERE IS ACTUALLY A PATTERN
	LOADF	C,[B]
	LOADI	A,0X20
	CMP	A,C
	BRAE	9F
	
	; SEE IF THERE IS A USER AREA PREFIX
	; AND CHANGE BANK TO KERNEL WORK AREA
	LOADF	A,[B+1]
	SUBI	A,':'
	LOADI	A,WORK_B
	STORE	[DBANK],A
	BRNZ	1F
	
	; MAKE SURE THAT THE USER AREA PREFIX IS VALID
	LOADI	A,'0'
	CMP	C,A
	BRB	9F
	LOADI	A,'9'
	CMP	C,A
	BRA	9F

	; SET THE WORKING USER AREA
	STORE	[WRK_USR],C
	
	; AND SKIP THE PREFIX
	ADDI	B,2
	STORE	[M0],B

	; GRAB THE NEXT CHARACTER
	; C = NEXT CHARACTER
1:	LOAD	B,[M0]
	LOAD	A,[SRC_BNK]
	STORE	[DBANK],A
	LOADF	C,[B]
	LOADI	A,WORK_B
	STORE	[DBANK],A
	
	; INCREMENT AND SAVE POINTER INTO M0
	ADDI	B,1
	STORE	[M0],B
	
	; GRAB PATTERN POINTER
	LOAD	A,[M2]
	
	; SEE IF THE PATTERN IS COMPLETE
	LOADI	B,0X20
	CMP	B,C
	BRAE	6F
	
	; GRAB PATTERN COUNTER
	LOAD	B,[M1]
	
	; SEE IF IT IS A '.'
	SUBI	C,'.'
	BRZ	5F
	
	; THERE MUST BE SPACE IN THE PATTERN BUFFER FOR ANY
	; OF THE NEXT VALUES TO BE VALID
	; LETS CHECK IT
	SUBI	B,1
	BRN	9F
	
	; SEE IF IT IS A '*'
	SUBI	C,'*'-'.'
	BRZ	3F
	
	; OTHERWISE, IT'S A NORMAL CHARACTER
	; PLACE IT IN THE BUFFER AND MOVE ON
	ADDI	C,'*'
	STOREF	[A],C
	ADDI	A,1
2:	STORE	[M2],A
	STORE	[M1],B
	JUMP	1B

	; HANDLE '*'
	; FILL REST OF PATTERN SECTION WITH '?'
3:	ADDI	B,1
	LOADI	C,'?'
4:	STOREF	[A],C
	ADDI	A,1
	SUBI	B,1
	BRNZ	4B
	JUMP	2B
	
	; HANDLE '.'
	; MAKE SURE THE LAST 2 BYTES OF THE PATTERN BUFFER ARE EMPTY
	; AND THERE HASN'T BEEN A '.' ALREADY
5:	LOADI	C,PATTERN+6
	ADD	A,B
	CMP	A,C
	BRA	9F
	
	; SET THE NEW PATTERN POINTER
	LOADI	A,PATTERN+6
	LOADI	B,2
	JUMP	2B

	; PATTERN IS COMPLETE
	; MAKE SURE ALL FIELDS HAVE BEEN FILLED IN
6:	LOADI	C,PATTERN+6
	CMP	A,C
	BRBE	9F
	LOADI	A,0
	JUMP	FS0DONE
	
	; PATTERN IS INVALID
9:	LOADI	A,0XFF
	JUMP	FS0DONE
	



	; INTERNAL FS0 FUNCTION TO RESET TO WORK BANK AND RETURN
FS0DONE:LOADI	C,WORK_B
	STORE	[DBANK],C
	JUMP	IRET

	; BANK IS DONE, MOVE ON TO THE NEXT
BI	= BI-1