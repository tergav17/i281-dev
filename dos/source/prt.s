; PRT.S
; PRINTER DRIVER
; GAVIN TERSTEEG, 2024
; SDMAY24-14

; AY-3-8910 ADDRESSES
MEGAIO	= 0XB0
AY0_ADR	= MEGAIO+0
AY1_ADR	= MEGAIO+2
AY0_WR	= MEGAIO+1
AY1_WR	= MEGAIO+3
AY0_RD	= MEGAIO+0
AY1_RD	= MEGAIO+2

.BANK BI
PRT0_B	= BI
.TEXT

	; PRINTER INIT
	; NOT USED ATM
	; ASSUMES WORK BANK IS SELECTED
	; USES: A, B, C
PRTINIT:JUMP	PRTDONE

	; PRINTER PUT CHARACTER
	; WAITS FOR PRINTER TO NOT BE BUSY BEFORE SENDING CHARACTER
	; A = CHARACTER TO PRINT
	; ASSUMES WORK BANK IS SELECTED
	; USES: A, B, C
	; SET UP AY CHIPS
PRTPUTC:LOADI	B,7
	STORE	[AY1_ADR],B
	STORE	[AY0_ADR],B
	LOAD	B,[AY1_RD]
	SHIFTL	B
	SHIFTL	B
	SHIFTR	B
	SHIFTR	B
	ADDI	B,0X80
	STORE	[AY1_WR],B
	LOAD	B,[AY0_RD]
	SHIFTL	B
	SHIFTL	B
	SHIFTR	B
	SHIFTR	B
	ADDI	B,0XC0
	STORE	[AY0_WR],B
	
	; SET DEVICE CONTROL STATE
	LOADI	B,15
	STORE	[AY1_ADR],B
	LOADI	B,0XFF
	STORE	[AY1_WR],B
	
	; WAIT FOR BUSY TO CLEAR
	LOADI	B,14
	STORE	[AY1_ADR],B
0:	LOAD	B,[AY1_RD]
	SHIFTR	B
	SHIFTR	B
	SHIFTR	B
	BRC	0B
	
	; SEND OUT CHARACTER
	LOADI	B,14
	STORE	[AY0_ADR],B
	STORE	[AY0_WR],A

	; STROBE PRINTER
	LOADI	A,15
	STORE	[AY1_ADR],A
	LOADI	A,0XF7
	STORE	[AY1_WR],A
	LOADI	A,0XFF
	STORE	[AY1_WR],A
	
	; DONE
	JUMP	PRTDONE

	; PRINTER STATUS
	; RETURNS A=0XFF IF THE PRINTER IS READY
	; ASSUMES WORK BANK IS SELECTED
	; USES: A, B, C
PRTSTAT:LOADI	A,7
	STORE	[AY1_ADR],A
	STORE	[AY0_ADR],A
	LOAD	A,[AY1_RD]
	SHIFTL	A
	SHIFTL	A
	SHIFTR	A
	SHIFTR	A
	ADDI	A,0X80
	STORE	[AY1_WR],A
	LOAD	A,[AY0_RD]
	SHIFTL	A
	SHIFTL	A
	SHIFTR	A
	SHIFTR	A
	ADDI	A,0XC0
	STORE	[AY0_WR],A
	
	; SET DEVICE CONTROL STATE
	LOADI	A,15
	STORE	[AY1_ADR],A
	LOADI	A,0XFF
	STORE	[AY1_WR],A
	
	; GET BUSY FLAG
	LOADI	A,0XFF
	LOADI	B,14
	STORE	[AY1_ADR],B
	LOAD	B,[AY1_RD]
	SHIFTR	B
	SHIFTR	B
	SHIFTR	B
	BRNC	PRTDONE
	LOADI	A,0X00
	
	; FALL TO PRTDONE

	; INTERNAL PRT FUNCTION TO RESET TO WORK BANK AND RETURN
PRTDONE:LOADI	C,WORK_B
	STORE	[DBANK],C
	JUMP	IRET

	; BANK IS DONE, MOVE ON TO THE NEXT
BI	= BI-1