; CORE.S
; MAIN 'GUTS' OF DOS/281

.BANK BI
CORE0_B	= BI
.TEXT

	; SET UP SYSTEM FOR OPERATION
	; BRING UP THE WORK BANK
INIT:	LOADI	A,WORK_B
	STORE	[DBANK],A
	
	; SET THE STACK POINTER TO THE TOP OF THE STACK
	LOADI	D,0X80-2

	; INITALIZE THE TTY DRIVER
	LOADI	C,CORE0_B
	STOREF	[D+1],C
	LOADI	C,@+5
	STOREF	[D],C
	
	LOADI	B,TTY0_B
	LOADI	C,TTYINIT
	JUMP	INDIR
	
	; SEND OUT THE 'HELLO' SPLASH
	LOADI	C,WORK_B
	STORE	[SRC_BNK],C
	LOADI	A,S_HELLO
	
	LOADI	C,@+5
	STOREF	[D],C
	
	LOADI	B,TTY0_B
	LOADI	C,TTYPUTS
	JUMP	INDIR
	
	; DOT PROMPT
PROMPT: LOADI	C,WORK_B
	STORE	[SRC_BNK],C
	LOADI	A,S_PRMT

	LOADI	C,CORE0_B
	STOREF	[D+1],C
	LOADI	C,@+5
	STOREF	[D],C
	
	LOADI	B,TTY0_B
	LOADI	C,TTYPUTS
	JUMP	INDIR
	
	; GET AN INPUT FROM THE TERMINAL
	LOADI	C,CMDL_B
	STORE	[SRC_BNK],C
	
	LOADI	C,@+5
	STOREF	[D],C
	
	LOADI	B,TTY0_B
	LOADI	C,TTYINPT
	JUMP	INDIR
	
	JUMP	PROMPT

	; BANK IS DONE, MOVE ON TO THE NEXT
BI	= BI-1

	; ALLOCATE A BANK FOR THE KERNEL WORK AREA
	; 0X60-7F = KERNEL STACK SPACE
.BANK	BD
WORK_B	= BD
.DATA

	; SOURCE BANK OF WHATEVER OPERATION IS BEING DONE
	; MAINLY USED TO KEEP TRACK OF THE BANK ADDRESS
	; OF STUFF BEING WORKED ON IN USER SPACE
.DEFL BYTE SRC_BNK	0

	; COMMAND LINE PROMPT
.DEFL BYTE S_PRMT	0X0A,0X0D,'.',0

	; PLACE 'HELLO' AT BOTTOM  OF STACK
	; WE DON'T CARE IF IT GETS OVERWRITTEN LATER
.ORG 0X60
.DEFL BYTE S_HELLO	"DOS/281 V1.0",0X0A,0X0D,0


	; BANK IS DONE, MOVE ON TO THE NEXT
BD	= BD-1

	; COMMAND LINE BANK
CMDL_B	= BD

BD	= BD-1
