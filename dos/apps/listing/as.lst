00/00:          ; DOSDEF.S
01/80:          ; THIS FILE CONTAINS IMPORTANT DEFINES USED IN DOS/281
01/80:          ; GAVIN TERSTEEG, 2024
01/80:          ; SDMAY24-14
01/80:          
01/80:          ; BIOS CALLS
01/80:          BOOT	= 0
01/80:          ALTBOOT = 1
01/80:          INDIR	= 2
01/80:          IRET	= 3
01/80:          SYSCALL	= 4
01/80:          SYSJUMP	= 5
01/80:          PRGM	= 6
01/80:          IWRITE	= 7
01/80:          ZSTART	= 8
01/80:          
01/80:          HALT	= 0X7F
01/80:          
01/80:          ; SYSTEM CALLS
01/80:          S_EXIT	= 0
01/80:          S_PUTC	= 1
01/80:          S_GETC	= 2
01/80:          S_STAT	= 3
01/80:          S_PUTS	= 4
01/80:          S_INPUT	= 5
01/80:          S_OPEN	= 6
01/80:          S_CLOSE	= 7
01/80:          S_READ	= 8
01/80:          S_WRITE	= 9
01/80:          S_FSRCH	= 10
01/80:          S_NEXT	= 11
01/80:          S_DELET	= 12
01/80:          S_CREAT	= 13
01/80:          S_FREE	= 14
01/80:          S_EXEC	= 15
01/80:          
01/80:          ; MEMORY AREAS
01/80:          CF_NAME	= 0X60
01/80:          CF_SIZE	= 0X68
01/80:          CF_USR	= 0X6A
01/80:          DFT_USR	= 0X6B
01/80:          ARG_BNK	= 0X6C
01/80:          BD_FREE	= 0X6E
01/80:          MAX_IB	= 0X70
01/80:          MAX_DB	= 0X71
01/80:          CMDL_B	= 0X72
01/80:          AUTO_B	= 0X73
01/80:          KERNMEM	= 0X78
01/80:          BIOSMEM	= 0X7C
01/80:          AU_RUN	= 0X00
01/80:          AU_PNTR	= 0X01
01/80:          AU_BANK	= 0X02
01/80:          AU_BLK	= 0X03
01/80:          AU_UA	= 0X04
01/80:          AU_FILE	= 0X06
01/80:          AU_MISC	= 0X18
01/80:          
01/80:          ; DATA BANK ADDRESS
01/80:          DBANK	= 0X80		
01/80:          
01/80:          
01/80:          ; AS0.S
01/80:          ; ASSEMBLER BACKBONE
01/80:          ; GAVIN TERSTEEG, 2024
01/80:          ; SDMAY24-14
01/80:          
01/80:          ; BANK ALLOCATION STUFF
01/80:          BI	= 1
01/80:          BD	= 1
01/80:          
01/80:          ; MAXIMUM ARGUMENTS
01/80:          MAXARGS	= 13
01/80:          
01/80:          .TEXT
01/80:          .BANK	BI
01/80:          CORE0_B	= BI
01/80:          
01/80:          	; START BY PROCESSING THE ARGUMENTS
01/80: 3000     START:	LOADI	A,0
01/81: A080     	STORE	[DBANK],A
01/83:          	
01/82:          	; RESET ARG STATE
01/82: A000     	STORE	[ARGC],A
01/83: A00F     	STORE	[LFLAG],A
01/85:          	
01/84:          	; SET UP STACK
01/84: 3C5E     	LOADI	D,0X60-2
01/86:          	
01/85:          	; SYSCALL RETURN BANK
01/85: 3801     	LOADI	C,BI
01/86: BB01     	STOREF	[D+1],C
01/88:          	
01/87:          	; SET UP POINTER
01/87: 3800     	LOADI	C,0
01/89:          	
01/88:          	; GO TO ARGUMENT BANK
01/88: 8072     0:	LOAD	A,[CMDL_B]
01/89: A06C     	STORE	[ARG_BNK],A
01/8A: A080     	STORE	[DBANK],A
01/8C:          
01/8B:          	; SKIP THE CURRENT ARGUMENT
01/8B: 9200     1:	LOADF	A,[C]
01/8C: 3420     	LOADI	B,0X20
01/8D: D100     	CMP	A,B
01/8E: F902     	BRBE	2F
01/8F: 5801     	ADDI	C,1
01/90: FFFA     	JUMP	1B
01/92:          
01/91:          	; LOOK FOR AN ARGUMENT
01/91: 9200     2:	LOADF	A,[C]
01/92: 5000     	ADDI	A,0
01/93: F630     	BRZ	ARGDONE
01/94: D100     	CMP	A,B
01/95: F802     	BRA	3F
01/96: 5801     	ADDI	C,1
01/97: FFF9     	JUMP	2B
01/99:          
01/98:          	; IS IT A FLAG?
01/98: 342D     3:	LOADI	B,'-'
01/99: D100     	CMP	A,B
01/9A: F714     	BRNZ	5F
01/9C:          	
01/9B:          	; HANDLE FLAGS HERE	
01/9B: 5801     4:	ADDI	C,1
01/9C: 9200     	LOADF	A,[C]
01/9D: 5000     	ADDI	A,0
01/9E: F625     	BRZ	ARGDONE
01/9F: 3420     	LOADI	B,0X20
01/A0: D100     	CMP	A,B
01/A1: F9EF     	BRBE	2B
01/A3:          	
01/A2:          	; REGISTER THE FLAG
01/A2: 3400     	LOADI	B,0
01/A3: A480     	STORE	[DBANK],B
01/A5:          	
01/A4: 704C     	SUBI	A,'L'
01/A5: F701     	BRNZ	@+2
01/A6: 340F     	LOADI	B,LFLAG
01/A8:          	
01/A7:          	; IS IT A RECOGNIZED FLAG?
01/A7: 5400     	ADDI	B,0
01/A8: F60F     	BRZ	ARGBAD
01/A9: 3001     	LOADI	A,1
01/AA: B100     	STOREF	[B],A
01/AC:          
01/AB:          	; THERE MAY BE ANOTHER FLAG
01/AB: 8472     	LOAD	B,[CMDL_B]
01/AC: A46C     	STORE	[ARG_BNK],B
01/AD: A480     	STORE	[DBANK],B
01/AE: FFEC     	JUMP	4B
01/B0:          
01/AF:          	; SAVE THE ARGUMENT
01/AF: 3400     5:	LOADI	B,0
01/B0: A480     	STORE	[DBANK],B
01/B1: 8400     	LOAD	B,[ARGC]
01/B2: B901     	STOREF	[B+ARGV],C
01/B4:          	
01/B3:          	; DID WE EXCEED THE ALLOWED NUMBER OF ARGUMENTS
01/B3: 740D     	SUBI	B,MAXARGS
01/B4: F003     	BRC	ARGBAD
01/B5: 540E     	ADDI	B,MAXARGS+1
01/B6: A400     	STORE	[ARGC],B
01/B7: FFD0     	JUMP	0B
01/B9:          	
01/B8:          	; BAD ARGUMENT
01/B8: 3000     ARGBAD:	LOADI	A,0
01/B9: A080     	STORE	[DBANK],A
01/BB:          	
01/BA: 3001     	LOADI	A,ERR0_B
01/BB: A06C     	STORE	[ARG_BNK],A
01/BC: 3000     	LOADI	A,ERROR00
01/BE:          	
01/BD:          	; PRINT ERROR MESSAGE
01/BD: 3801     PRNTERR:LOADI	C,BI
01/BE: BB01     	STOREF	[D+1],C
01/BF: 3404     	LOADI	B,S_PUTS
01/C0: 38D0     	LOADI	C,EXIT
01/C1: FF42     	JUMP	SYSCALL	
01/C3:          
01/C2:          	; EXIT PROGRAM
01/C2: 3400     EXIT:	LOADI	B,0
01/C3: FF41     	JUMP	SYSJUMP
01/C5:          	
01/C4:          	; ARGUMENT PROCESSING DONE
01/C4: 3000     ARGDONE:LOADI	A,0
01/C5: A080     	STORE	[DBANK],A
01/C7:          	
01/C6:          	; CHECK ARG COUNT
01/C6: 8000     	LOAD	A,[ARGC]
01/C7: 5000     	ADDI	A,0
01/C8: F6EF     	BRZ	ARGBAD
01/CA:          	
01/C9:          	; RESET PASS TO FIRST
01/C9: 3000     	LOADI	A,0
01/CA: A044     	STORE	[PASS],A
01/CC:          	
01/CB:          	; SET NEXT FREE TO HEAP
01/CB: 300B     	LOADI	A,HEAP
01/CC: A04C     	STORE	[NFREE],A
01/CE:          	
01/CD:          	; BEGIN ASSEMBLY
01/CD: 3403     	LOADI	B,MAIN0_B
01/CE: 3880     	LOADI	C,DOPASS
01/CF: FF32     	JUMP	INDIR
01/D1:          
01/D0:          	; EXIT PROGRAM
01/D0: 3400     EXIT:	LOADI	B,S_EXIT
01/D1: FF33     	JUMP	SYSJUMP
01/D3:          	
01/D2:          	; DO AN ERROR
01/D2:          	; A = ERROR #
01/D2:          ERRNO	= OPERAND
01/D2: 3400     ERROR:	LOADI	B,0
01/D3: A480     	STORE	[DBANK],B
01/D4: A014     	STORE	[ERRNO],A
01/D5: 3402     	LOADI	B,PRNT0_B
01/D6: 3880     	LOADI	C,PERHEAD
01/D7: FF2A     	JUMP	INDIR
01/D9:          
01/D8:          BI	= BI+1
01/D8:          .TEXT
01/D8:          .BANK	BI
02/80:          PRNT0_B	= BI
02/80:          
02/80:          	; PRINTS THE ERROR HEADER
02/80:          	; THE CURRENTLY OPEN FILE AND LINE NUMBER WILL BE PRINTED
02/80:          	; FORMAT: '[FILE]:[LINE]: ERROR'
02/80: 3802     PERHEAD:LOADI	C,BI
02/81: BB01     	STOREF	[D+1],C
02/83:          	
02/82:          	; PRINT CURRENTLY OPEN FILE
02/82: 8047     	LOAD	A,[CURRFIL]
02/83: 9001     	LOADF	A,[A+ARGV]
02/85:          
02/84:          	; PRINT LOOP
02/84: A038     0:	STORE	[BUFPNTR],A
02/85: 8472     	LOAD	B,[CMDL_B]
02/86: A480     	STORE	[DBANK],B
02/87: 9000     	LOADF	A,[A]
02/88: 3400     	LOADI	B,0
02/89: A480     	STORE	[DBANK],B
02/8A: 3420     	LOADI	B,0X20
02/8B: D100     	CMP	A,B
02/8C: F906     	BRBE	1F
02/8D: 3401     	LOADI	B,S_PUTC
02/8E: 3890     	LOADI	C,@+2
02/8F: FF74     	JUMP	SYSCALL
02/90: 8038     	LOAD	A,[BUFPNTR]
02/91: 5001     	ADDI	A,1
02/92: FFF1     	JUMP	0B
02/94:          	
02/93:          	; PRINT ':'
02/93: 303A     1:	LOADI	A,':'
02/94: 3401     	LOADI	B,S_PUTC
02/95: 3897     	LOADI	C,@+2
02/96: FF6D     	JUMP	SYSCALL
02/98:          	
02/97:          	; PRINT LINE NUMBER
02/97: 8045     	LOAD	A,[LINENUM]
02/98: A012     	STORE	[VALUE],A
02/99: 8046     	LOAD	A,[LINENUM+1]
02/9A: A013     	STORE	[VALUE+1],A
02/9B: 389E     	LOADI	C,@+3
02/9C: BB00     	STOREF	[D],C
02/9D: FF16     	JUMP	PRINTD
02/9F:          	
02/9E:          	; PRINT ':' + SPACE
02/9E: 303A     	LOADI	A,':'
02/9F: 3401     	LOADI	B,S_PUTC
02/A0: 38A2     	LOADI	C,@+2
02/A1: FF62     	JUMP	SYSCALL
02/A2: 3020     	LOADI	A,0X20
02/A3: 3401     	LOADI	B,S_PUTC
02/A4: 38A6     	LOADI	C,@+2
02/A5: FF5E     	JUMP	SYSCALL
02/A7:          	
02/A6:          	; LOOKUP ERROR ON TABLE
02/A6: 8014     	LOAD	A,[ERRNO]
02/A7: 7080     	SUBI	A,0X80
02/A8: 4000     	SHIFTL	A
02/A9: 5034     	ADDI	A,ETABLE
02/AA: 3801     	LOADI	C,ERR0_B
02/AB: A880     	STORE	[DBANK],C
02/AC: 9400     	LOADF	B,[A]
02/AD: 9001     	LOADF	A,[A+1]
02/AE: 3800     	LOADI	C,0
02/AF: A880     	STORE	[DBANK],C
02/B0: A46C     	STORE	[ARG_BNK],B
02/B1: 3401     	LOADI	B,CORE0_B
02/B2: 38BD     	LOADI	C,PRNTERR
02/B3: FF4E     	JUMP	INDIR
02/B5:          	
02/B4:          
02/B4:          	; PRINTS VARIABLE [VALUE] AS A 16 BIT BASE-10 NUMBER
02/B4:          PADDING	= TEMP
02/B4:          LEAFRET	= TEMP+1
02/B4: 7C02     PRINTD: SUBI	D,2
02/B6:          	
02/B5:          	; SET PADDING TO NULL CHARACTER AT FIRST
02/B5: 3800     	LOADI	C,0
02/B6: A816     	STORE	[PADDING],C
02/B8:          	
02/B7:          V	= 10000
02/B7: 3027     	LOADI	A,V/256
02/B8: 3410     	LOADI	B,V%256
02/B9: 38BB     	LOADI	C,@+2
02/BA: FF14     	JUMP	DOCHAR
02/BC:          	
02/BB:          V	= 1000
02/BB: 3003     	LOADI	A,V/256
02/BC: 34E8     	LOADI	B,V%256
02/BD: 38BF     	LOADI	C,@+2
02/BE: FF10     	JUMP	DOCHAR
02/C0:          	
02/BF:          V	= 100
02/BF: 3000     	LOADI	A,V/256
02/C0: 3464     	LOADI	B,V%256
02/C1: 38C3     	LOADI	C,@+2
02/C2: FF0C     	JUMP	DOCHAR
02/C4:          	
02/C3:          V	= 10
02/C3: 3000     	LOADI	A,V/256
02/C4: 340A     	LOADI	B,V%256
02/C5: 38C7     	LOADI	C,@+2
02/C6: FF08     	JUMP	DOCHAR
02/C8:          	
02/C7: 3030     	LOADI	A,'0'
02/C8: A016     	STORE	[PADDING],A
02/CA:          	
02/C9:          V	= 1
02/C9: 3000     	LOADI	A,V/256
02/CA: 3401     	LOADI	B,V%256
02/CB: 38CD     	LOADI	C,@+2
02/CC: FF02     	JUMP	DOCHAR
02/CE:          	
02/CD: 5C02     	ADDI	D,2 
02/CE: FF34     	JUMP	IRET
02/D0:          	
02/CF:          	; A = UPPER SUB
02/CF:          	; B = LOWER SUB
02/CF: A817     DOCHAR:	STORE	[LEAFRET],C
02/D0: 8816     	LOAD	C,[PADDING]	
02/D1: A811     	STORE	[CHAR],C
02/D3:          	
02/D2:          	; PARK THE STACK
02/D2: AC10     	STORE	[SPARK],D
02/D4:          
02/D3:          	; GET THE SIZE
02/D3: 8812     0:	LOAD	C,[VALUE]
02/D4: 8C13     	LOAD	D,[VALUE+1]
02/D6:          	
02/D5:          	; DO 16 BIT SUBTRACTION
02/D5: 6D00     1:	SUB	D,B
02/D6: F002     	BRC	2F
02/D7: 7801     	SUBI	C,1
02/D8: F10D     	BRNC	4F
02/D9: 6800     2:	SUB	C,A
02/DA: F10B     	BRNC	4F
02/DC:          	
02/DB:          	; SAVE VALUE
02/DB: A812     	STORE	[VALUE],C
02/DC: AC13     	STORE	[VALUE+1],D
02/DE:          	
02/DD:          	; GET THE CHAR AND CHECK TO SEE IF IT IS A WHITESPACE
02/DD: 8811     	LOAD	C,[CHAR]
02/DE: 3C20     	LOADI	D,0X20
02/DF: DB00     	CMP	C,D
02/E0: F802     	BRA	3F
02/E2:          	
02/E1:          	; SET PADDING AND CHAR TO '0'
02/E1: 3830     	LOADI	C,'0'
02/E2: A816     	STORE	[PADDING],C
02/E4:          	
02/E3: 5801     3:	ADDI	C,1
02/E4: A811     	STORE	[CHAR],C
02/E5: FFED     	JUMP	0B
02/E7:          	
02/E6:          	; RESTORE THE STACK
02/E6: 8C10     4:	LOAD	D,[SPARK]
02/E8:          	
02/E7:          	; SEE IF IT IS ZERO
02/E7: 8011     	LOAD	A,[CHAR]
02/E8: 5000     	ADDI	A,0
02/E9: F702     	BRNZ	5F
02/EA: 8817     	LOAD	C,[LEAFRET]
02/EB: FE14     	JUMPR	C
02/ED:          	
02/EC:          	; PRINT THE CHARACTER
02/EC: 3401     5:	LOADI	B,S_PUTC
02/ED: 3802     	LOADI	C,BI
02/EE: BB01     	STOREF	[D+1],C
02/EF: 8817     	LOAD	C,[LEAFRET]
02/F0: FF13     	JUMP	SYSCALL
02/F2:          
02/F1:          
02/F1:          	; ZERO BANK VARIABLES
02/F1:          .BANK	0
00/80:          .BSS
00/80:          
00/00:          	; COMMAND LINE ARGUMENTS
00/00: 00       .DEFL BYTE ARGC		0
00/01: 00000000
       00000000 .DEFL BYTE ARGV		0,0,0,0,0,0,0,0,
00/09: 00000000
       0000     			0,0,0,0,0,0
00/0F:          
00/0F:          	; COMMAND LINE FLAGS
00/0F: 00       .DEFL BYTE LFLAG	0
00/10:          
00/10:          	; VARIOUS MISC VARIABLES
00/10:          	; TO BE USED IN LEAF-FUNCTIONS
00/10: 00       .DEFL BYTE SPARK	0
00/11: 00       .DEFL BYTE CHAR		0
00/12:          
00/12:          	; 16 BIT MATH STUFF
00/12: 0000     .DEFL WORD VALUE	0
00/14: 0000     .DEFL WORD OPERAND	0
00/16: 0000     .DEFL WORD TEMP		0
00/18:          
00/18:          	; EXPRESSION PARSING STUFF
00/18: 00000000
       00000000
       0000     .DEFL WORD ESTACK	0,0,0,0,0
00/22: 00000000
       00000000
       0000     .DEFL WORD VSTACK	0,0,0,0,0
00/2C: 00       .DEFL BYTE EINDEX	0
00/2D: 00       .DEFL BYTE VINDEX	0
00/2E: 00       .DEFL BYTE ISDEF	0
00/2F:          STACKSZ	= 5
00/2F:          
00/2F:          	; LATEST TOKEN
00/2F: 00       .DEFL BYTE TOKEN	0
00/30:          
00/30:          	; TOKEN STREAM STATE
00/30: 00       .DEFL BYTE SRCINDX	0
00/31: 00       .DEFL BYTE SRCBLK	0
00/32: 00       .DEFL BYTE SRCBANK	0
00/33: 00       .DEFL BYTE SRCPNTR	0
00/34: 00       .DEFL BYTE SRCSTAT	0
00/35: 00       .DEFL BYTE SRCCHAR	0
00/36: 0000     .DEFL WORD SRCLINE	0
00/38:          
00/38:          	; TOKEN BUFFER
00/38: 00       .DEFL BYTE BUFPNTR	0
00/39: 00000000
       00000000
       0000     .DEFL BYTE TBUF		0,0,0,0,0,0,0,0,0,0
00/43: 00       .DEFL BYTE TBUFEND	0
00/44:          	
00/44:          	; ASSEMBLY STATE
00/44: 00       .DEFL BYTE PASS		0
00/45: 0000     .DEFL WORD LINENUM	0
00/47: 00       .DEFL BYTE CURRFIL	0
00/48: 00       .DEFL BYTE COUNTER	0
00/49: 00       .DEFL BYTE SEGMENT	0
00/4A: 00       .DEFL BYTE SELBANK	0
00/4B: 00       .DEFL BYTE CURBANK	0
00/4C:          
00/4C:          	; SYMBOL STUFF
00/4C: 00       .DEFL BYTE NFREE	0
00/4D: 0000     .DEFL WORD NUMLOC	0
00/4F: 00       .DEFL BYTE DFBANK	0
00/50: 00       .DEFL BYTE DFPNTR	0
00/51:          
00/51:          	; ERROR BANK 0
00/51:          .BANK	BD
01/00:          .DATA
01/00:          ERR0_B	= BD
01/00:          
01/00:          	; INVALID ARGUMENTS
01/00: 494E5641
       4C494420
       41524755
       4D454E54
       530A0D   .DEFL BYTE ERROR00	"INVALID ARGUMENTS",0X0A,0X0D,
01/13: 55534147
       453A2041
       53205B2D
       4C5D2046
       494C4531
       2046494C
       4532202E
       2E2E0A0D
       00       			"USAGE: AS [-L] FILE1 FILE2 ...",0X0A,0X0D,0
01/34:          
01/34:          	; ERROR TABLE
01/34: 0200     .DEFL BYTE ETABLE	ERR1_B,ERROR10,	; ERROR 0X80: CANNOT OPEN FILE 
01/36: 0212     			ERR1_B,ERROR11,	; ERROR 0X81: UNEXPECTED CHAR IN NUMERIC
01/38: 022F     			ERR1_B,ERROR12,	; ERROR 0X82: UNDEFINED EXPRESSION
01/3A: 0246     			ERR1_B,ERROR13,	; ERROR 0X83: UNEXPECTED TOKEN
01/3C: 0259     			ERR1_B,ERROR14,	; ERROR 0X84: VALUE STACK OVERFLOW
01/3E: 0300     			ERR2_B,ERROR20,	; ERROR 0X85: EXPRESSION STACK OVERFLOW
01/40: 031C     			ERR2_B,ERROR21,	; ERROR 0X86: EXPRESSION STACK DEPLETION
01/42: 0334     			ERR2_B,ERROR22,	; ERROR 0X87: VALUE STACK DEPLETION
01/44: 0351     			ERR2_B,ERROR23,	; ERROR 0X88: PARENTHESIS MISMATCH
01/46: 0368     			ERR2_B,ERROR24,	; ERROR 0X89: DIVIDE BY ZERO
01/48: 0300     			ERR2_B,ERROR30	; ERROR 0X8A: OUT OF MEMORY
01/4A:          
01/4A:          	; ERROR VALUES
01/4A:          E_COPEN	= 0X80
01/4A:          E_UXNUM	= 0X81
01/4A:          E_UDEFX = 0X82
01/4A:          E_UXTOK	= 0X83
01/4A:          E_VSTKO = 0X84
01/4A:          E_ESTKO = 0X85
01/4A:          E_VSTKD	= 0X86
01/4A:          E_ESTKD = 0X87
01/4A:          E_PAREN = 0X88
01/4A:          E_DZERO	= 0X89
01/4A:          E_OMEM	= 0X8A
01/4A:          
01/4A:          BD	= BD+1	
01/4A:          	
01/4A:          	; ERROR BANK 1
01/4A:          .BANK	BD
02/00:          .DATA
02/00:          ERR1_B	= BD
02/00:          
02/00: 43414E27
       54204F50
       454E2046
       494C450A
       0D00     .DEFL BYTE ERROR10	"CAN'T OPEN FILE",0X0A,0X0D,0
02/12: 554E4558
       50454354
       45442043
       48415220
       494E204E
       554D4552
       49430A0D
       00       .DEFL BYTE ERROR11	"UNEXPECTED CHAR IN NUMERIC",0X0A,0X0D,0
02/2F: 554E4445
       46494E45
       44204558
       50524553
       53494F4E
       0A0D00   .DEFL BYTE ERROR12	"UNDEFINED EXPRESSION",0X0A,0X0D,0
02/46: 554E4558
       50454354
       45442054
       4F4B454E
       0A0D00   .DEFL BYTE ERROR13	"UNEXPECTED TOKEN",0X0A,0X0D,0
02/59: 56414C55
       45205354
       41434B20
       4F564552
       464C4F57
       0A0D00   .DEFL BYTE ERROR14	"VALUE STACK OVERFLOW",0X0A,0X0D,0
02/70:          
02/70:          
02/70:          BD	= BD+1
02/70:          
02/70:          	; ERROR BANK 2
02/70:          .BANK	BD
03/00:          .DATA
03/00:          ERR2_B	= BD
03/00:          
03/00: 45585052
       45535349
       4F4E2053
       5441434B
       204F5645
       52464C4F
       570A0D00 .DEFL BYTE ERROR20	"EXPRESSION STACK OVERFLOW",0X0A,0X0D,0
03/1C: 56414C55
       45205354
       41434B20
       4445504C
       4554494F
       4E0A0D00 .DEFL BYTE ERROR21	"VALUE STACK DEPLETION",0X0A,0X0D,0
03/34: 45585052
       45535349
       4F4E2053
       5441434B
       20444550
       4C455449
       4F4E0A0D
       00       .DEFL BYTE ERROR22	"EXPRESSION STACK DEPLETION",0X0A,0X0D,0
03/51: 50415245
       4E544845
       53495320
       4D49534D
       41544348
       0A0D00   .DEFL BYTE ERROR23	"PARENTHESIS MISMATCH",0X0A,0X0D,0
03/68: 44495649
       44452042
       59205A45
       524F0A0D
       00       .DEFL BYTE ERROR24	"DIVIDE BY ZERO",0X0A,0X0D,0
03/79:          
03/79:          BD	= BD+1
03/79:          
03/79:          	; ERROR BANK 3
03/79:          .BANK	BD
04/00:          .DATA
04/00:          ERR3_B	= BD
04/00:          
04/00: 4F555420
       4F46204D
       454D4F52
       590A0D00 .DEFL BYTE ERROR30	"OUT OF MEMORY",0X0A,0X0D,0
04/10:          
04/10:          BD	= BD+1
04/10:          
04/10:          	; SOURCE READ BUFFER
04/10:          .BANK	BD
05/00:          .DATA
05/00:          SRC_B	= BD
05/00:          
05/00:          BD	= BD+4
05/00:          ; AS1.S
05/00:          ; ASSEMBLER MAIN LOGIC
05/00:          ; GAVIN TERSTEEG, 2024
05/00:          ; SDMAY24-14
05/00:          
05/00:          BI	= BI+1
05/00:          .TEXT
05/80:          .BANK	BI
03/80:          MAIN0_B	= BI
03/80:          
03/80:          	; START A PASS
03/80:          	; BEGIN BY REWINDING THE SOURCE STREAM
03/80: 3803     DOPASS:	LOADI	C,BI
03/81: BB01     	STOREF	[D+1],C
03/82: 3887     	LOADI	C,@+5
03/83: BB00     	STOREF	[D],C
03/84: 3405     	LOADI	B,TOK0_B
03/85: 388A     	LOADI	C,REWIND
03/86: FF7B     	JUMP	INDIR
03/88:          	
03/87:          	; RESET LOCAL COUNTER
03/87: 3000     	LOADI	A,0
03/88: A04D     	STORE	[NUMLOC],A
03/8A:          
03/89:          	; TAKE IN A NEW LINE OF SOURCE CODE
03/89: 3803     ASMLINE:LOADI	C,BI
03/8A: BB01     	STOREF	[D+1],C
03/8B: 3890     	LOADI	C,@+5
03/8C: BB00     	STOREF	[D],C
03/8D: 3405     	LOADI	B,TOK0_B
03/8E: 3880     	LOADI	C,NEXTTOK
03/8F: FF72     	JUMP	INDIR
03/91:          	
03/90:          	; ARE WE AT THE END?
03/90: 802F     	LOAD	A,[TOKEN]
03/91: 5000     	ADDI	A,0
03/92: F607     	BRZ	ASMDONE
03/94:          	
03/93:          	; IS IT NEWLINE?
03/93: 700A     	SUBI	A,NEWLINE
03/94: F6F4     	BRZ	ASMLINE
03/96:          	
03/95:          	; IS IT A SYMBOL?
03/95: 7076     	SUBI	A,SYMBOL-NEWLINE
03/96: 3404     	LOADI	B,MAIN1_B
03/97: 3880     	LOADI	C,ASMSYM
03/98: F669     	BRZ	INDIR
03/9A:          	
03/99: FFEF     	JUMP	ASMLINE
03/9B:          	
03/9A:          
03/9A:          	; ALL DONE
03/9A: 3041     ASMDONE:LOADI	A,'A'
03/9B: A039     	STORE	[TBUF],A
03/9C: 3000     	LOADI	A,0
03/9D: A03A     	STORE	[TBUF+1],A
03/9F:          
03/9E:          	; PRINT SOME DEBUGGING STUFF
03/9E: 3803     	LOADI	C,BI
03/9F: BB01     	STOREF	[D+1],C
03/A0: 3041     	LOADI	A,'A'
03/A1: 3401     	LOADI	B,S_PUTC
03/A2: 38A4     	LOADI	C,@+2
03/A3: FF60     	JUMP	SYSCALL
03/A4: 303D     	LOADI	A,'='
03/A5: 3401     	LOADI	B,S_PUTC
03/A6: 38A8     	LOADI	C,@+2
03/A7: FF5C     	JUMP	SYSCALL
03/A9:          	
03/A8:          	; GET 'A' SYMBOL
03/A8: 38AD     	LOADI	C,@+5
03/A9: BB00     	STOREF	[D],C
03/AA: 340E     	LOADI	B,SYM0_B
03/AB: 3886     	LOADI	C,GETSYM
03/AC: FF55     	JUMP	INDIR
03/AD: 5000     	ADDI	A,0
03/AE: F705     	BRNZ	1F
03/B0:          	
03/AF:          	; PRINT VALUE
03/AF: 38B8     	LOADI	C,2F
03/B0: BB00     	STOREF	[D],C
03/B1: 3402     	LOADI	B,PRNT0_B
03/B2: 38B4     	LOADI	C,PRINTD
03/B3: FF4E     	JUMP	INDIR
03/B5:          	
03/B4:          	; UNDEFINED
03/B4: 303F     1:	LOADI	A,'?'
03/B5: 3401     	LOADI	B,S_PUTC
03/B6: 38B8     	LOADI	C,@+2
03/B7: FF4C     	JUMP	SYSCALL
03/B9:          
03/B8:          	; EXIT
03/B8: 3401     2:	LOADI	B,CORE0_B
03/B9: 38D0     	LOADI	C,EXIT
03/BA: FF47     	JUMP	INDIR
03/BC:          	
03/BB:          BI	= BI+1
03/BB:          .TEXT
03/BB:          .BANK	BI
04/80:          MAIN1_B	= BI
04/80:          	
04/80:          	; ATTEMPT TO DEFINE THIS SYMBOL
04/80: 3804     ASMSYM:	LOADI	C,BI
04/81: BB01     	STOREF	[D+1],C
04/82: 3887     	LOADI	C,@+5
04/83: BB00     	STOREF	[D],C
04/84: 340E     	LOADI	B,SYM0_B
04/85: 3880     	LOADI	C,DEFINE
04/86: FF7B     	JUMP	INDIR
04/88:          	
04/87:          	; CONSUME '=' SYMBOL
04/87: 388C     	LOADI	C,@+5
04/88: BB00     	STOREF	[D],C
04/89: 3405     	LOADI	B,TOK0_B
04/8A: 3880     	LOADI	C,NEXTTOK
04/8B: FF76     	JUMP	INDIR
04/8C: 802F     	LOAD	A,[TOKEN]
04/8D: 703D     	SUBI	A,'='
04/8E: F604     	BRZ	1F
04/90:          	
04/8F:          	; ERROR
04/8F: 3083     	LOADI	A,E_UXTOK
04/90: 3401     	LOADI	B,CORE0_B
04/91: 38D2     	LOADI	C,ERROR
04/92: FF6F     	JUMP	INDIR
04/94:          
04/93:          	; ATTEMPT TO PARSE EXPRESSION
04/93: 3898     1:	LOADI	C,@+5
04/94: BB00     	STOREF	[D],C
04/95: 3405     	LOADI	B,TOK0_B
04/96: 3880     	LOADI	C,NEXTTOK
04/97: FF6A     	JUMP	INDIR
04/99:          	
04/98: 389D     	LOADI	C,@+5
04/99: BB00     	STOREF	[D],C
04/9A: 3407     	LOADI	B,EXP0_B
04/9B: 3880     	LOADI	C,PARSEX
04/9C: FF65     	JUMP	INDIR
04/9E:          	
04/9D:          	; DID THE EXPRESSION RETURN DEFINED?
04/9D: 5000     	ADDI	A,0
04/9E: F70E     	BRNZ	2F
04/A0:          
04/9F:          	; STORE VALUE AND DEFINE
04/9F: AC10     	STORE	[SPARK],D
04/A0: 8012     	LOAD	A,[VALUE]
04/A1: 8413     	LOAD	B,[VALUE+1]
04/A2: 884F     	LOAD	C,[DFBANK]
04/A3: 8C50     	LOAD	D,[DFPNTR]
04/A4: A880     	STORE	[DBANK],C
04/A5: B301     	STOREF	[D+1],A
04/A6: B702     	STOREF	[D+2],B
04/A7: 30FF     	LOADI	A,0XFF
04/A8: B300     	STOREF	[D],A
04/A9: 3000     	LOADI	A,0
04/AA: A080     	STORE	[DBANK],A
04/AB: 8C10     	LOAD	D,[SPARK]
04/AD:          	
04/AC: FF07     	JUMP	9F
04/AE:          
04/AD:          	; UN-DEFINE THE EXPRESSION
04/AD: 3000     2:	LOADI	A,0
04/AE: 844F     	LOAD	B,[DFBANK]
04/AF: 8850     	LOAD	C,[DFPNTR]
04/B0: A480     	STORE	[DBANK],B
04/B1: B200     	STOREF	[C],A
04/B2: A080     	STORE	[DBANK],A
04/B3: FF00     	JUMP	9F
04/B5:          	
04/B4:          	; GO BACK TO ASMLINE
04/B4: 3403     9:	LOADI	B,MAIN0_B
04/B5: 3889     	LOADI	C,ASMLINE
04/B6: FF4B     	JUMP	INDIR
04/B8:          ; AS2.S
04/B7:          ; SOURCE INPUT AND TOKENIZER
04/B7:          ; GAVIN TERSTEEG, 2024
04/B7:          ; SDMAY24-14
04/B7:          
04/B7:          ; NEW LINE CHARACTER
04/B7:          NEWLINE	= 0X0A
04/B7:          SEMICOL	= 0X3B
04/B7:          SQUOTE	= 0X27
04/B7:          DQUOTE	= 0X22
04/B7:          SYMBOL	= 0X80
04/B7:          NUMERIC	= 0X81
04/B7:          
04/B7:          
04/B7:          BI	= BI+1
04/B7:          .TEXT
04/B7:          .BANK	BI
05/80:          TOK0_B	= BI
05/80:          
05/80:          	; GETS THE NEXT TOKEN
05/80:          	; TOKEN TYPE IS RETURNED IN [TOKEN]
05/80:          	; SPECIAL TYPES ARE:
05/80:          	; 'A' -> ALPHANUMERIC
05/80:          	; '0' -> NUMERIC
05/80:          	; NEWLINE -> NEW LINE
05/80:          	; ALL WHITESPACE IS IGNORED, UNLESS WE ARE INSIDE
05/80:          	; A STRING OR DEFINED CHAR
05/80:          	; 'A' AND '0' TOKENS WILL POPULATE THE TOKEN BUFFER
05/80:          	; AS WELL
05/80: 7C02     NEXTTOK:SUBI	D,2
05/82:          
05/81:          	; STORE RETURN ADDRESS
05/81: 3406     	LOADI	B,TOK1_B
05/82: B701     	STOREF	[D+1],B
05/84:          	
05/83:          	; CHECK SRCSTAT
05/83: 388B     	LOADI	C,NEXTTOB
05/84: 8034     	LOAD	A,[SRCSTAT]
05/85: 5001     	ADDI	A,1
05/86: F77B     	BRNZ	INDIR
05/87: A034     	STORE	[SRCSTAT],A
05/89:          	
05/88: 3880     	LOADI	C,NEXTTOA
05/89: FF78     	JUMP	INDIR
05/8B:          
05/8A:          	; RESETS THE STATE OF THE TOKEN STREAM BACK TO
05/8A:          	; BEGINNING OF SOURCE INPUT
05/8A: 30FF     REWIND:	LOADI	A,0-1
05/8B: A030     	STORE	[SRCINDX],A
05/8D:          	
05/8C:          	; SET FIRST CHAR AS INVALID
05/8C: 3080     	LOADI	A,0X80
05/8D: A035     	STORE	[SRCCHAR],A
05/8F:          	
05/8E:          	; RESET STAT
05/8E: 30FF     	LOADI	A,0XFF
05/8F: A034     	STORE	[SRCSTAT],A
05/91:          	
05/90:          	; GET THE NEXT FILE
05/90:          	; ALL POINTERS WILL BE RESET FOR FILE READ IN
05/90:          	; SRCINDX == ARGC IF WE ARE DONE READING IN BLOCK
05/90: 7C02     NEXTSRC:SUBI	D,2
05/92:          
05/91:          	; RESET POINTER, BLOCK, AND SRCLINE
05/91: 3000     	LOADI	A,0
05/92: A033     	STORE	[SRCPNTR],A
05/93: A031     	STORE	[SRCBLK],A
05/94: A036     	STORE	[SRCLINE],A
05/95: A037     	STORE	[SRCLINE+1],A
05/97:          	
05/96:          	; RESET BANK
05/96: 3005     	LOADI	A,SRC_B
05/97: A032     	STORE	[SRCBANK],A
05/99:          
05/98:          	; INCREMENT SOURCE INDEX
05/98: 8030     0:	LOAD	A,[SRCINDX]
05/99: 5001     	ADDI	A,1
05/9A: A030     	STORE	[SRCINDX],A
05/9C:          
05/9B:          	; CHECK BOUNDS OF INDEX
05/9B: 8400     	LOAD	B,[ARGC]
05/9C: D100     	CMP	A,B
05/9D: F01C     	BRAE	9F
05/9F:          	
05/9E:          	; ATTEMPT TO OPEN FILE
05/9E: 8472     	LOAD	B,[CMDL_B]
05/9F: A46C     	STORE	[ARG_BNK],B
05/A0: 9001     	LOADF	A,[A+ARGV]
05/A1: 3805     	LOADI	C,BI
05/A2: BB01     	STOREF	[D+1],C
05/A3: 38A5     	LOADI	C,@+2
05/A4: 3406     	LOADI	B,S_OPEN
05/A5: 38A7     	LOADI	C,@+2
05/A6: FF5D     	JUMP	SYSCALL
05/A7: 5000     	ADDI	A,0
05/A8: F609     	BRZ	1F
05/AA:          	
05/A9:          	; ERROR
05/A9:          	; ALSO SET LINENUM AND CURRFIL
05/A9: 8030     	LOAD	A,[SRCINDX]
05/AA: A047     	STORE	[CURRFIL],A
05/AB: 3000     	LOADI	A,0
05/AC: A045     	STORE	[LINENUM],A
05/AD: A046     	STORE	[LINENUM+1],A
05/AE: 3080     	LOADI	A,E_COPEN
05/AF: 3401     	LOADI	B,CORE0_B
05/B0: 38D2     	LOADI	C,ERROR
05/B1: FF50     	JUMP	INDIR
05/B3:          
05/B2:          	; ATTEMPT TO READ THE FIRST BLOCK
05/B2: 3405     1:	LOADI	B,SRC_B
05/B3: A46C     	STORE	[ARG_BNK],B
05/B4: 3000     	LOADI	A,0
05/B5: 3408     	LOADI	B,S_READ
05/B6: 38B8     	LOADI	C,@+2
05/B7: FF4C     	JUMP	SYSCALL
05/B9:          	
05/B8:          	; IF IT DIDN'T WORK, GET THE NEXT BLOCK
05/B8: 5000     	ADDI	A,0
05/B9: F7DE     	BRNZ	0B
05/BB:          
05/BA:          	; RETURN
05/BA: 5C02     9:	ADDI	D,2
05/BB: FF47     	JUMP	IRET
05/BD:          	
05/BC:          	; GET THE NEXT CHARACTER IN THE STREAM
05/BC:          	; RETURNS CHARACTER IN [SRCCHAR]
05/BC:          	; IF THERE ARE NO CHARACTER, 0 WILL BE RETURNED
05/BC: 7C02     NEXTCHR:SUBI	D,2
05/BE:          
05/BD:          	; CHECK SRCINDX != ARGC
05/BD: 8030     	LOAD	A,[SRCINDX]
05/BE: 8400     	LOAD	B,[ARGC]
05/BF: D100     	CMP	A,B
05/C0: F627     	BRZ	8F
05/C2:          
05/C1:          	; CHECK IF SRCPNTR IS VALID
05/C1: 8033     	LOAD	A,[SRCPNTR]
05/C2: 8432     	LOAD	B,[SRCBANK]
05/C3: 5000     	ADDI	A,0
05/C4: F40C     	BRN	3F
05/C6:          	
05/C5:          	; ALRIGHT, LETS JUST GRAB A CHARACTER FROM THE BANK
05/C5: A480     1:	STORE	[DBANK],B
05/C6: 9800     	LOADF	C,[A]
05/C7: 3400     	LOADI	B,0
05/C8: A480     	STORE	[DBANK],B
05/CA:          	
05/C9:          	; INCREMENT POINTER
05/C9: 5001     	ADDI	A,1
05/CA: A033     	STORE	[SRCPNTR],A
05/CC:          	
05/CB:          	; CAN WE RETURN?
05/CB: A835     2:	STORE	[SRCCHAR],C
05/CC: 5800     	ADDI	C,0
05/CD: F71C     	BRNZ	9F
05/CE: 380A     	LOADI	C,NEWLINE
05/CF: A835     	STORE	[SRCCHAR],C
05/D0: FF15     	JUMP	7F
05/D2:          	
05/D1:          	; INCREMENT BANK
05/D1: 3000     3:	LOADI	A,0
05/D2: 5401     	ADDI	B,1
05/D3: A432     	STORE	[SRCBANK],B
05/D4: 3809     	LOADI	C,SRC_B+4
05/D5: D600     	CMP	B,C
05/D6: F1EE     	BRB	1B
05/D8:          	
05/D7:          	; READ A NEW BLOCK
05/D7: 8031     	LOAD	A,[SRCBLK]
05/D8: 5001     	ADDI	A,1
05/D9: F00C     	BRC	7F
05/DA: A031     	STORE	[SRCBLK],A
05/DB: 3805     	LOADI	C,BI
05/DC: BB01     	STOREF	[D+1],C
05/DD: 3408     	LOADI	B,S_READ
05/DE: 38E0     	LOADI	C,@+2
05/DF: FF24     	JUMP	SYSCALL
05/E0: 5000     	ADDI	A,0
05/E1: F704     	BRNZ	7F
05/E3:          	
05/E2:          	; GO READ A CHARACTER NOW
05/E2: 3000     	LOADI	A,0
05/E3: 3405     	LOADI	B,SRC_B
05/E4: A432     	STORE	[SRCBANK],B
05/E5: FFDF     	JUMP	1B
05/E7:          
05/E6:          	; WE ARE DONE WITH THIS FILE
05/E6:          	; MOVE ON TO THE NEXT
05/E6: 5C02     7:	ADDI	D,2
05/E7: FFA8     	JUMP	NEXTSRC
05/E9:          	
05/E8:          	; STORE A ZERO IN SRCCHAR
05/E8: 3000     8:	LOADI	A,0
05/E9: A035     	STORE	[SRCCHAR],A
05/EB:          	
05/EA:          	; RETURN
05/EA: 5C02     9:	ADDI	D,2
05/EB: FF17     	JUMP	IRET
05/ED:          	
05/EC:          BI	= BI+1
05/EC:          .TEXT
05/EC:          .BANK	BI
06/80:          TOK1_B	= BI
06/80:          	
06/80:          	; SHADOW OF NEXTTOK
06/80:          	; DO WE NEED TO INCREMENT THE LINE?
06/80:          	; YES WE DO, UPDATE CURRENT FILE
06/80: 8030     NEXTTOA:LOAD	A,[SRCINDX]
06/81: A047     	STORE	[CURRFIL],A
06/83:          	
06/82:          	; INCRMENT SOURCE LINE
06/82: 8037     	LOAD	A,[SRCLINE+1]
06/83: 5001     	ADDI	A,1
06/84: A037     	STORE	[SRCLINE+1],A
06/85: 8436     	LOAD	B,[SRCLINE]
06/86: F102     	BRNC	1F
06/87: 5401     	ADDI	B,1
06/88: A436     	STORE	[SRCLINE],B
06/8A:          	
06/89:          	; REFLECT ON LINE NUMBER
06/89: A445     1:	STORE	[LINENUM],B
06/8A: A046     	STORE	[LINENUM+1],A
06/8C:          
06/8B:          
06/8B:          	; CHECK THE NEXT CHARACTER IN THE STREAM
06/8B: 8035     NEXTTOB:LOAD	A,[SRCCHAR]
06/8C: 5000     	ADDI	A,0
06/8D: F66C     	BRZ	8F
06/8E: F43D     	BRN	5F
06/90:          
06/8F:          	; IS IT A NEWLINE?
06/8F: 340A     	LOADI	B,NEWLINE
06/90: D100     	CMP	A,B
06/91: F703     	BRNZ	1F
06/93:          	
06/92:          	; NEW LINE
06/92: 34FF     	LOADI	B,0XFF
06/93: A434     	STORE	[SRCSTAT],B
06/94: FF65     	JUMP	8F
06/96:          
06/95:          	; CHECK STATE STUFF
06/95: 8834     1:	LOAD	C,[SRCSTAT]
06/96: 343B     	LOADI	B,SEMICOL
06/97: D600     	CMP	B,C
06/98: F633     	BRZ	5F
06/99: 5800     	ADDI	C,0
06/9A: F71B     	BRNZ	4F
06/9C:          
06/9B:          	; IS IT WHITESPACE?
06/9B: 3420     	LOADI	B,0X20
06/9C: D100     	CMP	A,B
06/9D: F92E     	BRBE	5F
06/9F:          	
06/9E:          	; IS IT A NUMBER?
06/9E: 3430     	LOADI	B,'0'
06/9F: D100     	CMP	A,B
06/A0: F104     	BRB	2F
06/A1: 3439     	LOADI	B,'9'
06/A2: D100     	CMP	A,B
06/A3: 3481     	LOADI	B,NUMERIC
06/A4: F92C     	BRBE	0F
06/A6:          	
06/A5:          	; NOPE, IS IT IS LETTER?
06/A5:          	; FIRST CONVERT FROM LOWERCASE TO UPPERCASE
06/A5: 3461     2:	LOADI	B,'A'+0X20
06/A6: D100     	CMP	A,B
06/A7: F104     	BRB	3F
06/A8: 347A     	LOADI	B,'Z'+0X20
06/A9: D100     	CMP	A,B
06/AA: F801     	BRA	3F
06/AB: 7020     	SUBI	A,0X20
06/AD:          	
06/AC:          	; CHECK FOR UPPER CASE LETTER
06/AC: 345F     3:	LOADI	B,'_'
06/AD: D100     	CMP	A,B
06/AE: F605     	BRZ	2F
06/AF: 3441     	LOADI	B,'A'
06/B0: D100     	CMP	A,B
06/B1: F104     	BRB	4F
06/B2: 345A     	LOADI	B,'Z'
06/B3: D100     	CMP	A,B
06/B4: 3480     2:	LOADI	B,SYMBOL
06/B5: F91B     	BRBE	0F
06/B7:          	
06/B6:          	; NOPE, PROCESS AS A LITERAL
06/B6: 8834     4:	LOAD	C,[SRCSTAT]
06/B7: D200     	CMP	A,C
06/B8: F703     	BRNZ	4F
06/BA:          	
06/B9:          	; END OF ENCLOSED SECTION
06/B9: 3800     	LOADI	C,0
06/BA: A834     	STORE	[SRCSTAT],C
06/BB: FF3E     	JUMP	8F
06/BD:          	
06/BC:          	; IF ENCLOSED, JUST SEND DIRECTLY TO OUTPUT
06/BC: 5800     4:	ADDI	C,0
06/BD: F73C     	BRNZ	8F
06/BF:          
06/BE:          	; IS IT A COMMENT?
06/BE: 343B     	LOADI	B,SEMICOL
06/BF: D100     	CMP	A,B
06/C0: F702     	BRNZ	4F
06/C2:          	
06/C1:          	; SET COMMENT MODE
06/C1: A034     	STORE	[SRCSTAT],A
06/C2: FF09     	JUMP	5F
06/C4:          
06/C3:          	; IS IT A SINGLE OR DOUBLE QUOTE?
06/C3: 3427     4:	LOADI	B,SQUOTE
06/C4: D100     	CMP	A,B
06/C5: F604     	BRZ	4F
06/C6: 3422     	LOADI	B,DQUOTE
06/C7: D100     	CMP	A,B
06/C8: F601     	BRZ	4F
06/C9: FF30     	JUMP	8F
06/CB:          
06/CA:          	; YES IT IS, SET THE STATE AND EXIT
06/CA: A034     4:	STORE	[SRCSTAT],A
06/CB: FF2E     	JUMP	8F
06/CD:          	
06/CC:          	; GRAB A NEW CHARACTER FROM THE STREAM
06/CC: 388B     5:	LOADI	C,NEXTTOB
06/CD: BB00     	STOREF	[D],C
06/CE: 3405     	LOADI	B,TOK0_B
06/CF: 38BC     	LOADI	C,NEXTCHR
06/D0: FF31     	JUMP	INDIR
06/D2:          
06/D1:          	; COPY THE SYMBOL OR NUMERIC INTO THE BUFFER
06/D1: A42F     0:	STORE	[TOKEN],B
06/D3:          
06/D2:          	; PREPARE TO COPY
06/D2: 3839     	LOADI	C,TBUF
06/D4:          	
06/D3:          	; STORE CHARACTER
06/D3: B200     1:	STOREF	[C],A
06/D4: A838     	STORE	[BUFPNTR],C
06/D6:          
06/D5:          	; GET NEXT CHARACTER
06/D5: 38DA     2:	LOADI	C,@+5
06/D6: BB00     	STOREF	[D],C
06/D7: 3405     	LOADI	B,TOK0_B
06/D8: 38BC     	LOADI	C,NEXTCHR
06/D9: FF28     	JUMP	INDIR
06/DB:          	
06/DA:          	; CHECK CHARACTER TYPE
06/DA: 8838     	LOAD	C,[BUFPNTR]
06/DB: 8035     	LOAD	A,[SRCCHAR]
06/DD:          	
06/DC:          	; IS IT A NUMBER?
06/DC: 3430     	LOADI	B,'0'
06/DD: D100     	CMP	A,B
06/DE: F118     	BRB	7F
06/DF: 3439     	LOADI	B,'9'
06/E0: D100     	CMP	A,B
06/E1: F910     	BRBE	4F
06/E3:          	
06/E2:          	; NOPE, IS IT IS LETTER?
06/E2:          	; FIRST CONVERT FROM LOWERCASE TO UPPERCASE
06/E2: 3461     	LOADI	B,'A'+0X20
06/E3: D100     	CMP	A,B
06/E4: F104     	BRB	3F
06/E5: 347A     	LOADI	B,'Z'+0X20
06/E6: D100     	CMP	A,B
06/E7: F801     	BRA	3F
06/E8: 7020     	SUBI	A,0X20
06/EA:          	
06/E9:          	; CHECK FOR UPPER CASE LETTER
06/E9: 345F     3:	LOADI	B,'_'
06/EA: D100     	CMP	A,B
06/EB: F606     	BRZ	4F
06/EC: 3441     	LOADI	B,'A'
06/ED: D100     	CMP	A,B
06/EE: F108     	BRB	7F
06/EF: 345A     	LOADI	B,'Z'
06/F0: D100     	CMP	A,B
06/F1: F805     	BRA	7F
06/F3:          	
06/F2:          	; ADD TO THE STRING
06/F2: 5801     4:	ADDI	C,1
06/F3: 3443     	LOADI	B,TBUFEND
06/F4: D600     	CMP	B,C
06/F5: F6DF     	BRZ	2B
06/F6: FFDC     	JUMP	1B
06/F8:          	
06/F7:          	; TERMINATE
06/F7: 3000     7:	LOADI	A,0
06/F8: B201     	STOREF	[C+1],A
06/F9: FF03     	JUMP	9F
06/FB:          	
06/FA:          	; SAVE TOKEN TYPE AND CONSUME THE SRCCHAR
06/FA: 3480     8:	LOADI	B,0X80
06/FB: A435     	STORE	[SRCCHAR],B
06/FC: A02F     	STORE	[TOKEN],A
06/FE:          	
06/FD:          	; RETURN
06/FD: 5C02     9:	ADDI	D,2
06/FE: FF04     	JUMP	IRET
06/100:          	
06/FF:          ; AS3.S
06/FF:          ; EXPRESSION HANDLING
06/FF:          ; GAVIN TERSTEEG, 2024
06/FF:          ; SDMAY24-14
06/FF:          
06/FF:          BI	= BI+1
06/FF:          .TEXT
06/FF:          .BANK	BI
07/80:          EXP0_B	= BI
07/80:          
07/80:          	; PARSE AN EXPRESSION
07/80:          	; THE FIRST PART OF THE EXPRESSION SHOULD BE IN THE TOKEN BUFFER
07/80:          	; IF PARSING IS SUCCESSFUL, THE RESULT WILL BE PLACED IN [VALUE]
07/80:          	; IF A SYMBOL IS UNDEFINED, AN ERROR CODE WILL BE RETURNED IN
07/80:          	; REGISTER A, OTHERWISE A = 0X00 WILL BE RETURNED.
07/80:          PRECED	= CHAR
07/80: 3408     PARSEX: LOADI	B,EXP1_B
07/81: 3880     	LOADI	C,PARSEXA
07/82: FF7F     	JUMP	INDIR
07/84:          
07/83:          	; PARSE A NUMERIC VALUE
07/83:          	; THE NUMERIC TO BE PARSED SHOULD BE IN THE TOKEN BUFFER
07/83:          	; IF THE PARSING IS SUCCESSFUL, THE RESULT WILL BE PLACED IN [VALUE]
07/83:          	; DEFAULT RADIX IS 10
07/83:          RADIX	= TEMP
07/83: 300A     PARSEN:	LOADI	A,10
07/84: A016     	STORE	[RADIX],A
07/86:          	
07/85:          	; RESET VALUE AS WELL
07/85: 3000     	LOADI	A,0
07/86: A012     	STORE	[VALUE],A
07/87: A013     	STORE	[VALUE+1],A
07/89:          	
07/88:          	; RESET BUFFER POINTER
07/88: 3039     	LOADI	A,TBUF
07/8A:          	
07/89:          	; DETECT LEADING ZERO
07/89: 9400     	LOADF	B,[A]
07/8A: 5400     	ADDI	B,0
07/8B: F677     	BRZ	IRET
07/8C: 7430     	SUBI	B,'0'
07/8D: F709     	BRNZ	1F
07/8F:          	
07/8E:          	; SET RADIX TO 8
07/8E: 3408     	LOADI	B,8
07/8F: A416     	STORE	[RADIX],B
07/91:          	
07/90:          	; GET NEXT CHARACTER, COMPARE WITH 9
07/90: 5001     	ADDI	A,1
07/91: 9400     	LOADF	B,[A]
07/92: 3839     	LOADI	C,'9'
07/93: D600     	CMP	B,C
07/94: F902     	BRBE	1F
07/96:          	
07/95:          	; RADIX FORMAT IS "0?..."
07/95: 5001     	ADDI	A,1
07/96: FF0E     	JUMP	4F
07/98:          	
07/97:          	; CHECK END OF NUMBER FOR RADIX
07/97: 2800     1:	MOV	C,A
07/98: 9600     2:	LOADF	B,[C]
07/99: 5400     	ADDI	B,0
07/9A: F602     	BRZ	3F
07/9B: 5801     	ADDI	C,1
07/9C: FFFB     	JUMP	2B
07/9E:          	
07/9D:          	; LOOK AT LAST CHARACTER
07/9D: 96FF     3:	LOADF	B,[C+0-1]
07/9E: 7439     	SUBI	B,'9'
07/9F: F915     	BRBE	0F
07/A0: 5439     	ADDI	B,'9'
07/A1: A038     	STORE	[BUFPNTR],A
07/A2: 3000     	LOADI	A,0
07/A3: B2FF     	STOREF	[C+0-1],A
07/A4: 8038     	LOAD	A,[BUFPNTR]
07/A6:          
07/A5:          	; B = POTENTIAL RADIX
07/A5: 3810     4:	LOADI	C,16
07/A6: 7458     	SUBI	B,'X'
07/A7: F60C     	BRZ	5F
07/A8: 74F0     	SUBI	B,'H'-'X'
07/A9: F60A     	BRZ	5F
07/AA: 3808     	LOADI	C,8
07/AB: 7407     	SUBI	B,'O'-'H'
07/AC: F607     	BRZ	5F
07/AD: 3802     	LOADI	C,2
07/AE: 74F3     	SUBI	B,'B'-'O'
07/AF: F604     	BRZ	5F
07/B1:          	
07/B0:          	; ISSUE WITH PARSING NUMERIC
07/B0: 3081     8:	LOADI	A,E_UXNUM
07/B1: 3401     	LOADI	B,CORE0_B
07/B2: 38D2     	LOADI	C,ERROR
07/B3: FF4E     	JUMP	INDIR
07/B5:          	
07/B4:          	; C = RADIX
07/B4: A816     5:	STORE	[RADIX],C
07/B6:          	
07/B5:          	; START PARSING THE NUMERIC
07/B5: A038     0:	STORE	[BUFPNTR],A
07/B7:          	
07/B6:          	; GRAB A CHARACTER
07/B6: 9000     	LOADF	A,[A]
07/B7: 5000     	ADDI	A,0
07/B8: F64A     	BRZ	IRET
07/BA:          	
07/B9:          	; MULTIPLY BY RADIX (SLOWISH)
07/B9: AC10     	STORE	[SPARK],D
07/BA: 8412     	LOAD	B,[VALUE]
07/BB: 8813     	LOAD	C,[VALUE+1]
07/BC: 8C16     	LOAD	D,[RADIX]
07/BE:          	
07/BD: 7C01     1:	SUBI	D,1
07/BE: F609     	BRZ	3F
07/BF: 8013     	LOAD	A,[VALUE+1]
07/C0: 4200     	ADD	A,C
07/C1: A013     	STORE	[VALUE+1],A
07/C2: 8012     	LOAD	A,[VALUE]
07/C3: F101     	BRNC	2F
07/C4: 5001     	ADDI	A,1
07/C5: 4100     2:	ADD	A,B
07/C6: A012     	STORE	[VALUE],A
07/C7: FFF5     	JUMP	1B
07/C9:          	
07/C8:          	; GET THE CHARACTER AGAIN, AND INCREMENT POINTER
07/C8: 8C10     3:	LOAD	D,[SPARK]
07/C9: 8038     	LOAD	A,[BUFPNTR]
07/CA: 9800     	LOADF	C,[A]
07/CB: 5001     	ADDI	A,1
07/CD:          	
07/CC:          	; CHECK BOUNDS
07/CC: 3430     	LOADI	B,'0'
07/CD: D900     	CMP	C,B
07/CE: F1E1     	BRB	8B 
07/CF: 3439     	LOADI	B,'9'
07/D0: D900     	CMP	C,B
07/D1: F907     	BRBE	4F
07/D3:          	
07/D2: 3441     	LOADI	B,'A'
07/D3: D900     	CMP	C,B
07/D4: F1DB     	BRB	8B
07/D5: 3446     	LOADI	B,'F'
07/D6: D900     	CMP	C,B
07/D7: F8D8     	BRA	8B
07/D9:          	
07/D8: 7807     	SUBI	C,'A'-('0'+10)
07/DA:          	
07/D9:          	; CONVERT FROM ASCII
07/D9: 7830     4:	SUBI	C,'0'
07/DB:          
07/DA:          	; COMPARE WITH RADIX
07/DA: 8416     	LOAD	B,[RADIX]
07/DB: D900     	CMP	C,B
07/DC: F0D3     	BRAE	8B
07/DE:          	
07/DD:          	; ADD TO VALUE
07/DD: 8413     	LOAD	B,[VALUE+1]
07/DE: 4600     	ADD	B,C
07/DF: A413     	STORE	[VALUE+1],B
07/E0: F1D4     	BRNC	0B
07/E1: 8412     	LOAD	B,[VALUE]
07/E2: 5401     	ADDI	B,1
07/E3: A412     	STORE	[VALUE],B
07/E4: FFD0     	JUMP	0B
07/E6:          
07/E5:          BI	= BI+1
07/E5:          .TEXT
07/E5:          .BANK	BI
08/80:          EXP1_B	= BI
08/80:          
08/80:          	; SHADOW OF PARSEX
08/80: 7C02     PARSEXA:SUBI	D,2
08/82:          
08/81:          	; RESET ISDEF
08/81: 3000     	LOADI	A,0
08/82: A02E     	STORE	[ISDEF],A
08/84:          
08/83:          	; RESET VALUE AND EXPRESSION STACKS
08/83: 3022     	LOADI	A,VSTACK
08/84: A02D     	STORE	[VINDEX],A
08/85: 3018     	LOADI	A,ESTACK
08/86: A02C     	STORE	[EINDEX],A
08/88:          	
08/87:          	; PROCESS TOKEN (VALUES AND LEFT PARENTHESIS)
08/87:          	; ALSO SET RETURN ADDRESS
08/87: 3808     0:	LOADI	C,BI
08/88: BB01     	STOREF	[D+1],C
08/89: 802F     	LOAD	A,[TOKEN]
08/8B:          
08/8A:          	; IS IT A NUMERIC?
08/8A: 7081     	SUBI	A,NUMERIC
08/8B: F711     	BRNZ	7F
08/8D:          	
08/8C:          	; CHECK FOR LOCAL SYMBOLS
08/8C: 803B     	LOAD	A,[TBUF+2]
08/8D: 5000     	ADDI	A,0
08/8E: F709     	BRNZ	2F
08/8F: 803A     	LOAD	A,[TBUF+1]
08/90: 7046     	SUBI	A,'F'
08/91: F602     	BRZ	1F
08/92: 70FC     	SUBI	A,'B'-'F'
08/93: F704     	BRNZ	2F
08/95:          
08/94:          	; TODO: PROCESS LOCAL SYMBOL
08/94: 3000     1:	LOADI	A,0
08/95: A012     	STORE	[VALUE],A
08/96: A013     	STORE	[VALUE+1],A
08/97: FF16     	JUMP	8F
08/99:          
08/98:          	; PARSE NUMERIC
08/98: 38AE     2:	LOADI	C,8F
08/99: BB00     	STOREF	[D],C
08/9A: 3407     	LOADI	B,EXP0_B
08/9B: 3883     	LOADI	C,PARSEN
08/9C: FF65     	JUMP	INDIR
08/9E:          	
08/9D:          	; IS IT A SYMBOL?
08/9D: 70FF     7:	SUBI	A,SYMBOL-NUMERIC
08/9E: F709     	BRNZ	7F
08/A0:          
08/9F:          	; PROCESS SYMBOL
08/9F: 38A4     	LOADI	C,@+5
08/A0: BB00     	STOREF	[D],C
08/A1: 340E     	LOADI	B,SYM0_B
08/A2: 3886     	LOADI	C,GETSYM
08/A3: FF5E     	JUMP	INDIR
08/A5:          	
08/A4:          	; IS IT DEFINED?
08/A4: 5000     	ADDI	A,0
08/A5: F608     	BRZ	8F
08/A6: A02E     	STORE	[ISDEF],A
08/A7: FF06     	JUMP	8F
08/A9:          
08/A8:          	; IS IT A '@'?
08/A8: 70C0     7:	SUBI	A,'@'-SYMBOL
08/A9: F710     	BRNZ	7F
08/AB:          
08/AA:          	; PROCESS COUNTER
08/AA: 3000     	LOADI	A,0
08/AB: A012     	STORE	[VALUE],A
08/AC: 8048     	LOAD	A,[COUNTER]
08/AD: A013     	STORE	[VALUE+1],A
08/AF:          
08/AE:          	; PLACE [VALUE] ONTO THE VSTACK
08/AE: 842D     8:	LOAD	B,[VINDEX]
08/AF: 3027     	LOADI	A,VSTACK+STACKSZ
08/B0: D400     	CMP	B,A
08/B1: 3084     	LOADI	A,E_VSTKO
08/B2: F017     	BRAE	8F	; ERROR!
08/B3: 8012     	LOAD	A,[VALUE]
08/B4: B100     	STOREF	[B],A
08/B5: 8013     	LOAD	A,[VALUE+1]
08/B6: B101     	STOREF	[B+1],A
08/B7: 5402     	ADDI	B,2
08/B8: A42D     	STORE	[VINDEX],B
08/B9: FF13     	JUMP	PARSEXC
08/BB:          	
08/BA:          	; IS IT A '('?
08/BA: 70E8     7:	SUBI	A,'('-'@'
08/BB: F70D     	BRNZ	7F
08/BD:          	
08/BC: A011     	STORE	[PRECED],A
08/BE:          	
08/BD:          	; PLACE LEFT PARENTHESIS ON ESTACK
08/BD: 842C     PARSEXB:LOAD	B,[EINDEX]
08/BE: 301D     	LOADI	A,ESTACK+STACKSZ
08/BF: D400     	CMP	B,A
08/C0: 3085     	LOADI	A,E_ESTKO
08/C1: F008     	BRAE	8F	; ERROR!
08/C2: 802F     	LOAD	A,[TOKEN]
08/C3: B100     	STOREF	[B],A
08/C4: 8011     	LOAD	A,[PRECED]
08/C5: B101     	STOREF	[B+1],A
08/C6: 5402     	ADDI	B,2
08/C7: A42C     	STORE	[EINDEX],B
08/C8: FF04     	JUMP	PARSEXC
08/CA:          	
08/C9:          	; ERROR!
08/C9: 3083     7:	LOADI	A,E_UXTOK
08/CA: 3401     8:	LOADI	B,CORE0_B
08/CB: 38D2     	LOADI	C,ERROR
08/CC: FF35     	JUMP	INDIR
08/CE:          
08/CD:          	; GET THE NEXT TOKEN
08/CD: 3808     PARSEXC:LOADI	C,BI
08/CE: BB01     	STOREF	[D+1],C
08/CF: 38D4     	LOADI	C,@+5
08/D0: BB00     	STOREF	[D],C
08/D1: 3405     	LOADI	B,TOK0_B
08/D2: 3880     	LOADI	C,NEXTTOK
08/D3: FF2E     	JUMP	INDIR
08/D5:          	
08/D4:          	; LOOK FOR SPECIAL VALUES
08/D4: 802F     	LOAD	A,[TOKEN]
08/D5: A011     	STORE	[CHAR],A
08/D6: 7081     	SUBI	A,NUMERIC
08/D7: F6AF     	BRZ	0B
08/D8: 70FF     	SUBI	A,SYMBOL-NUMERIC
08/D9: F6AD     	BRZ	0B
08/DA: 70C0     	SUBI	A,'@'-SYMBOL
08/DB: F6AB     	BRZ	0B
08/DC: 70E8     	SUBI	A,'('-'@'
08/DD: F6A9     	BRZ	0B
08/DF:          	
08/DE:          	; THIS IS SOME SORT OF OPERATION
08/DE: 3409     	LOADI	B,EXP2_B
08/DF: 3880     	LOADI	C,PARSEXD
08/E0: FF21     	JUMP	INDIR
08/E2:          
08/E1:          BI	= BI+1
08/E1:          .TEXT
08/E1:          .BANK	BI
09/80:          EXP2_B	= BI
09/80:          
09/80:          	; HANDLE OTHER CHARACTERS IN EXPRESSION
09/80:          	; ALSO GET PRECEDENCE OF CHARACTER
09/80: 3809     PARSEXD:LOADI	C,BI
09/81: BB01     	STOREF	[D+1],C
09/82: 3802     	LOADI	C,2
09/83: 7003     	SUBI	A,'+'-'('
09/84: F63C     	BRZ	5F
09/85: 7002     	SUBI	A,'-'-'+'
09/86: F63A     	BRZ	5F
09/87: 3803     	LOADI	C,3
09/88: 70FD     	SUBI	A,'*'-'-'
09/89: F637     	BRZ	5F
09/8A: 7005     	SUBI	A,'/'-'*'
09/8B: F635     	BRZ	5F
09/8C: 70F6     	SUBI	A,'%'-'/'
09/8D: F633     	BRZ	5F
09/8E: 3807     	LOADI	C,7
09/8F: 7057     	SUBI	A,'|'-'%'
09/90: F630     	BRZ	5F
09/91: 3805     	LOADI	C,5
09/92: 70AA     	SUBI	A,'&'-'|'
09/93: F62D     	BRZ	5F
09/94: 3801     	LOADI	C,1
09/95: 70FB     	SUBI	A,'!'-'&'
09/96: F62A     	BRZ	5F
09/97: 3806     	LOADI	C,6
09/98: 703D     	SUBI	A,'^'-'!'
09/99: F627     	BRZ	5F
09/9A: 70CB     	SUBI	A,')'-'^'
09/9B: F713     	BRNZ	3F
09/9D:          	
09/9C:          	; PROCESS RIGHT PARENTHESIS
09/9C: 802C     0:	LOAD	A,[EINDEX]
09/9D: 3418     	LOADI	B,ESTACK
09/9E: D100     	CMP	A,B
09/9F: F804     	BRA	1F
09/A1:          	
09/A0:          	; ERROR! PARENTHESIS ARE MISMATCHED
09/A0: 3088     	LOADI	A,E_PAREN
09/A1: 3401     	LOADI	B,CORE0_B
09/A2: 38D2     	LOADI	C,ERROR
09/A3: FF5E     	JUMP	INDIR
09/A5:          	
09/A4:          	; CHECK TOP OF ESTACK
09/A4: 94FE     1:	LOADF	B,[A+0-2]
09/A5: 7428     	SUBI	B,'('
09/A6: F605     	BRZ	2F
09/A8:          	
09/A7:          	; POP OFF OF ESTACK
09/A7: 389C     	LOADI	C,0B
09/A8: BB00     	STOREF	[D],C
09/A9: 340A     	LOADI	B,EXP3_B
09/AA: 3880     	LOADI	C,ESTKPOP
09/AB: FF56     	JUMP	INDIR
09/AD:          
09/AC:          	; DO FINAL POP AND EXIT
09/AC: 7002     2:	SUBI	A,2
09/AD: A02C     	STORE	[EINDEX],A
09/AE: FF23     	JUMP	6F
09/B0:          	
09/AF:          	; CHECK FOR SHIFTS
09/AF: 7015     3:	SUBI	A,'>'-')'
09/B0: F602     	BRZ	4F
09/B1: 70FE     	SUBI	A,'<'-'>'
09/B2: F722     	BRNZ	9F
09/B4:          
09/B3:          	; IT'S A '<<' OR '>>' (HOPEFULLY)
09/B3:          	; GET THE NEXT TOKEN TO CONFIRM
09/B3: 38B8     4:	LOADI	C,@+5
09/B4: BB00     	STOREF	[D],C
09/B5: 3405     	LOADI	B,TOK0_B
09/B6: 3880     	LOADI	C,NEXTTOK
09/B7: FF4A     	JUMP	INDIR
09/B9:          	
09/B8:          	; IF THE NEXT TOKEN IS THE SAME AS THE LAST, WE ARE GOOD
09/B8: 802F     	LOAD	A,[TOKEN]
09/B9: 8411     	LOAD	B,[CHAR]
09/BA: D100     	CMP	A,B
09/BB: 3804     	LOADI	C,4
09/BC: F604     	BRZ	5F
09/BE:          	
09/BD:          	; BOO! ERROR!
09/BD: 3083     	LOADI	A,E_UXTOK
09/BE: 3401     	LOADI	B,CORE0_B
09/BF: 38D2     	LOADI	C,ERROR
09/C0: FF41     	JUMP	INDIR
09/C2:          
09/C1:          	; WE HAVE A SPECIAL CHARACTER
09/C1:          	; C = PRECEDENCE
09/C1: A811     5:	STORE	[PRECED],C
09/C3:          	
09/C2:          	; CHECK EINDEX
09/C2: 802C     0:	LOAD	A,[EINDEX]
09/C3: 3418     	LOADI	B,ESTACK
09/C4: D100     	CMP	A,B
09/C5: F609     	BRZ	5F
09/C7:          
09/C6:          	; COMPARE PRECEDENCE OF THE TOP OF THE ESTACK WITH OP PRECEDENCE
09/C6: 94FF     	LOADF	B,[A+0-1]
09/C7: 8011     	LOAD	A,[PRECED]
09/C8: D100     	CMP	A,B
09/C9: F805     	BRA	5F
09/CB:          	
09/CA:          	; POP OFF OF ESTACK
09/CA: 38C2     	LOADI	C,0B
09/CB: BB00     	STOREF	[D],C
09/CC: 340A     	LOADI	B,EXP3_B
09/CD: 3880     	LOADI	C,ESTKPOP
09/CE: FF33     	JUMP	INDIR
09/D0:          	
09/CF:          	; PUSH ONTO ESTACK
09/CF: 3408     5:	LOADI	B,EXP1_B
09/D0: 38BD     	LOADI	C,PARSEXB
09/D1: FF30     	JUMP	INDIR
09/D3:          
09/D2:          	; GET THE NEXT TOKEN
09/D2: 3408     6:	LOADI	B,EXP1_B
09/D3: 38CD     	LOADI	C,PARSEXC
09/D4: FF2D     	JUMP	INDIR
09/D6:          	
09/D5:          	; CAN'T RECOGNIZE CHARACTER, END OF EXPRESSION
09/D5:          	; AND WE ARE DONE
09/D5: 802C     9:	LOAD	A,[EINDEX]
09/D6: 3418     	LOADI	B,ESTACK
09/D7: D100     	CMP	A,B
09/D8: F605     	BRZ	0F
09/DA:          	
09/D9:          	; POP OFF THE EXPRESSION STACK
09/D9: 38D5     	LOADI	C,9B
09/DA: BB00     	STOREF	[D],C
09/DB: 340A     	LOADI	B,EXP3_B
09/DC: 3880     	LOADI	C,ESTKPOP
09/DD: FF24     	JUMP	INDIR
09/DF:          	
09/DE:          	; ESTACK IS EMPTY
09/DE:          	; MAKE SURE THERE IS 1 IN VSTACK
09/DE: 802D     0:	LOAD	A,[VINDEX]
09/DF: 3424     	LOADI	B,VSTACK+2
09/E0: D100     	CMP	A,B
09/E1: F604     	BRZ	1F
09/E3:          
09/E2:          	; ERROR :(
09/E2: 3084     	LOADI	A,E_VSTKO
09/E3: 3401     	LOADI	B,CORE0_B
09/E4: 38D2     	LOADI	C,ERROR
09/E5: FF1C     	JUMP	INDIR
09/E7:          
09/E6:          	; SET RESULT TO VALUE
09/E6: 98FF     1:	LOADF	C,[A+0-1]
09/E7: 94FE     	LOADF	B,[A+0-2]
09/E8: A412     	STORE	[VALUE],B
09/E9: A813     	STORE	[VALUE+1],C
09/EB:          
09/EA:          	; RETURN
09/EA: 802E     	LOAD	A,[ISDEF]
09/EB: 5C02     	ADDI	D,2
09/EC: FF16     	JUMP	IRET
09/EE:          
09/ED:          BI	= BI+1
09/ED:          .TEXT
09/ED:          .BANK	BI
0A/80:          EXP3_B	= BI
0A/80:          
0A/80:          	; POP A VALUE OFF OF THE ESTACK
0A/80:          	; CHECK IF EXPRESSION STACK IS EMPTY
0A/80: 3087     ESTKPOP:LOADI	A,E_ESTKD
0A/81: 842C     	LOAD	B,[EINDEX]
0A/82: 3818     	LOADI	C,ESTACK
0A/83: D600     	CMP	B,C
0A/84: F966     	BRBE	9F
0A/86:          	
0A/85:          	; POP VALUE OFF ESTACK
0A/85: 7402     	SUBI	B,2
0A/86: A42C     	STORE	[EINDEX],B
0A/88:          	
0A/87:          	; CHECK IF VALUE STACK HAS TWO VALUES
0A/87: 3086     	LOADI	A,E_VSTKD
0A/88: 842D     	LOAD	B,[VINDEX]
0A/89: 3824     	LOADI	C,VSTACK+2
0A/8A: D600     	CMP	B,C
0A/8B: F95F     	BRBE	9F
0A/8D:          
0A/8C:          	; MOVE TWO VSTACK VALUES INTO [VALUE] AND [OPERAND]
0A/8C: 91FF     	LOADF	A,[B+0-1]
0A/8D: A015     	STORE	[OPERAND+1],A
0A/8E: 91FE     	LOADF	A,[B+0-2]
0A/8F: A014     	STORE	[OPERAND],A
0A/90: 91FD     	LOADF	A,[B+0-3]
0A/91: A013     	STORE	[VALUE+1],A
0A/92: 91FC     	LOADF	A,[B+0-4]
0A/93: A012     	STORE	[VALUE],A
0A/95:          	
0A/94:          	; RESPOSITION VSTACK POINTER
0A/94: 7402     	SUBI	B,2
0A/95: A42D     	STORE	[VINDEX],B
0A/97:          	
0A/96:          	; ARE WE DEFINED?
0A/96: 802E     	LOAD	A,[ISDEF]
0A/97: 5000     	ADDI	A,0
0A/98: F74C     	BRNZ	8F
0A/9A:          	
0A/99:          	; GRAB THE OPERATION
0A/99: 802C     	LOAD	A,[EINDEX]
0A/9A: 9000     	LOADF	A,[A]
0A/9C:          	
0A/9B:          	; CHECK FOR ADDITION '+'
0A/9B: 702B     	SUBI	A,'+'
0A/9C: F70B     	BRNZ	7F
0A/9E:          	
0A/9D:          	; DO ADDITION OPERATION
0A/9D: 8013     	LOAD	A,[VALUE+1]
0A/9E: 8415     	LOAD	B,[OPERAND+1]
0A/9F: 4100     	ADD	A,B
0A/A0: A013     	STORE	[VALUE+1],A
0A/A1: 8012     	LOAD	A,[VALUE]
0A/A2: 8414     	LOAD	B,[OPERAND]
0A/A3: F101     	BRNC	1F
0A/A4: 5001     	ADDI	A,1
0A/A5: 4100     1:	ADD	A,B
0A/A6: A012     	STORE	[VALUE],A
0A/A7: FF3D     	JUMP	8F
0A/A9:          	
0A/A8:          	; CHECK FOR SUBTRACTION '-'
0A/A8: 7002     7:	SUBI	A,'-'-'+'
0A/A9: F70B     	BRNZ	7F
0A/AB:          	
0A/AA:          	; DO SUBTRACTION OPERATION
0A/AA: 8013     	LOAD	A,[VALUE+1]
0A/AB: 8415     	LOAD	B,[OPERAND+1]
0A/AC: 6100     	SUB	A,B
0A/AD: A013     	STORE	[VALUE+1],A
0A/AE: 8012     	LOAD	A,[VALUE]
0A/AF: 8414     	LOAD	B,[OPERAND]
0A/B0: F001     	BRC	1F
0A/B1: 7001     	SUBI	A,1
0A/B2: 6100     1:	SUB	A,B
0A/B3: A012     	STORE	[VALUE],A
0A/B4: FF30     	JUMP	8F
0A/B6:          	
0A/B5:          	; CHECK FOR MULTIPLICATION
0A/B5: 70FD     7:	SUBI	A,'*'-'-'
0A/B6: F72B     	BRNZ	7F
0A/B8:          	
0A/B7:          	; DO MULTIPLICATION OPERATION
0A/B7: AC10     	STORE	[SPARK],D
0A/B8: 8812     	LOAD	C,[VALUE]
0A/B9: 8C13     	LOAD	D,[VALUE+1]
0A/BA: 3000     	LOADI	A,0
0A/BB: A012     	STORE	[VALUE],A
0A/BC: A013     	STORE	[VALUE+1],A
0A/BE:          	
0A/BD:          	; ARE WE DONE YET?
0A/BD: 5800     0:	ADDI	C,0
0A/BE: F704     	BRNZ	1F
0A/BF: 5C00     	ADDI	D,0
0A/C0: F702     	BRNZ	1F
0A/C1: 8C10     	LOAD	D,[SPARK]
0A/C2: FF22     	JUMP	8F
0A/C4:          	
0A/C3:          	; SHIFT 'CD' RIGHT
0A/C3:          	; IF BIT ZERO OF D IS NOT SET, THEN SKIP ADDING
0A/C3:          	; [OPERAND] TO [VALUE]
0A/C3: CD00     1:	SHIFTR	D
0A/C4: F004     	BRC	3F
0A/C5: C900     	SHIFTR	C
0A/C6: F10F     	BRNC	6F
0A/C7: 5C80     	ADDI	D,0X80
0A/C8: FF0D     2:	JUMP	6F
0A/C9: C900     3:	SHIFTR	C
0A/CA: F101     	BRNC	4F
0A/CB: 5C80     	ADDI	D,0X80
0A/CD:          
0A/CC:          	; ADD [OPERAND] TO [VALUE]
0A/CC: 8013     4:	LOAD	A,[VALUE+1]
0A/CD: 8415     	LOAD	B,[OPERAND+1]
0A/CE: 4100     	ADD	A,B
0A/CF: A013     	STORE	[VALUE+1],A
0A/D0: 8012     	LOAD	A,[VALUE]
0A/D1: 8414     	LOAD	B,[OPERAND]
0A/D2: F101     	BRNC	5F
0A/D3: 5001     	ADDI	A,1
0A/D4: 4100     5:	ADD	A,B
0A/D5: A012     	STORE	[VALUE],A
0A/D7:          
0A/D6:          	; SHIFT [OPERAND] LEFT
0A/D6: 8015     6:	LOAD	A,[OPERAND+1]
0A/D7: 4000     	SHIFTL	A
0A/D8: A015     	STORE	[OPERAND+1],A
0A/D9: 8014     	LOAD	A,[OPERAND]
0A/DA: F104     	BRNC	6F
0A/DB: 4000     	SHIFTL	A
0A/DC: 5001     	ADDI	A,1
0A/DD: A014     	STORE	[OPERAND],A
0A/DE: FFDE     	JUMP	0B
0A/DF: 4000     6:	SHIFTL	A
0A/E0: A014     	STORE	[OPERAND],A
0A/E1: FFDB     	JUMP	0B
0A/E3:          
0A/E2:          	; SWITCH INSTRUCTION BANK
0A/E2: 340B     7:	LOADI	B,EXP4_B
0A/E3: 3880     	LOADI	C,ESTKPOA
0A/E4: FF1D     	JUMP	INDIR
0A/E6:          
0A/E5:          	; PLACE [VALUE] ON STACK AND RETURN
0A/E5: 802D     8:	LOAD	A,[VINDEX]
0A/E6: 8413     	LOAD	B,[VALUE+1]
0A/E7: B4FF     	STOREF	[A+0-1],B
0A/E8: 8412     	LOAD	B,[VALUE]
0A/E9: B4FE     	STOREF	[A+0-2],B
0A/EA: FF18     	JUMP	IRET
0A/EC:          
0A/EB:          	; HANDLE ERROR
0A/EB: 3401     9:	LOADI	B,CORE0_B
0A/EC: 38D2     	LOADI	C,ERROR
0A/ED: FF14     	JUMP	INDIR
0A/EF:          	
0A/EE:          BI	= BI+1
0A/EE:          .TEXT
0A/EE:          .BANK	BI
0B/80:          EXP4_B	= BI
0B/80:          
0B/80:          	; CONTINUATION OF ESTKPOP
0B/80:          	; CHECK FOR DIVISION OR MODULUS
0B/80: 7005     ESTKPOA:SUBI	A,'/'-'*'
0B/81: F602     	BRZ	0F
0B/82: 70F6     	SUBI	A,'%'-'/'
0B/83: F74F     	BRNZ	7F
0B/85:          	
0B/84:          	; MAKE SURE OPERAND ISN'T ZERO
0B/84:          DVCOUNT	= CHAR
0B/84: 8014     0:	LOAD	A,[OPERAND]
0B/85: 5000     	ADDI	A,0
0B/86: F705     	BRNZ	0F
0B/87: 8015     	LOAD	A,[OPERAND+1]
0B/88: 5000     	ADDI	A,0 
0B/89: F702     	BRNZ	0F
0B/8B:          
0B/8A:          	; ERROR! DIVIDE BY ZERO
0B/8A: 3089     	LOADI	A,E_DZERO
0B/8B: FF50     	JUMP	9F
0B/8D:          	
0B/8C:          	; DO DIVISION
0B/8C: AC10     0:	STORE	[SPARK],D
0B/8E:          	
0B/8D:          	; RESET REMAINDER
0B/8D: 3800     	LOADI	C,0
0B/8E: 3C00     	LOADI	D,0
0B/90:          
0B/8F:          	; RESET COUNTER
0B/8F: 3010     	LOADI	A,16
0B/91:          
0B/90:          	; SHIFT QUOTIENT LEFT
0B/90:          	; MAIN DIVISOR LOOP
0B/90: A011     0:	STORE	[DVCOUNT],A
0B/91: 8013     	LOAD	A,[VALUE+1]
0B/92: 4000     	SHIFTL	A
0B/93: A013     	STORE	[VALUE+1],A
0B/94: 8012     	LOAD	A,[VALUE]
0B/95: F109     	BRNC	2F
0B/96: 4000     	SHIFTL	A
0B/97: F003     	BRC	1F
0B/98: 5001     	ADDI	A,1
0B/99: A012     	STORE	[VALUE],A
0B/9A: FF06     	JUMP	3F
0B/9B: 5001     1:	ADDI	A,1
0B/9C: 7000     	SUBI	A,0
0B/9D: A012     	STORE	[VALUE],A 
0B/9E: FF02     	JUMP	3F
0B/9F: 4000     2:	SHIFTL	A
0B/A0: A012     	STORE	[VALUE],A
0B/A2:          	
0B/A1:          	; SHIFT REMAINDER LEFT + CARRY
0B/A1: F002     3:	BRC	1F
0B/A2: 4F00     	SHIFTL	D
0B/A3: FF06     	JUMP	3F 
0B/A5:          	
0B/A4:          	; SHIFT BOTTOM 8 BITS (SHIFT IN)
0B/A4: 4F00     1:	SHIFTL	D
0B/A5: F002     	BRC	2F
0B/A6: 5C01     	ADDI	D,1
0B/A7: FF02     	JUMP	3F
0B/A8: 5C01     2:	ADDI	D,1
0B/A9: 7C00     	SUBI	D,0
0B/AB:          
0B/AA:          	; DO TOP 8 BITS
0B/AA: F002     3:	BRC	1F
0B/AB: 4A00     	SHIFTL	C
0B/AC: FF06     	JUMP	3F 
0B/AE:          	
0B/AD:          	; SHIFT TOP 8 BITS (SHIFT IN)
0B/AD: 4A00     1:	SHIFTL	C
0B/AE: F002     	BRC	2F
0B/AF: 5801     	ADDI	C,1
0B/B0: FF02     	JUMP	3F
0B/B1: 5801     2:	ADDI	C,1
0B/B2: 7800     	SUBI	C,0
0B/B4:          	
0B/B3:          	; SUBTRACT BY DIVISOR ([OPERAND])
0B/B3: 8015     3:	LOAD	A,[OPERAND+1]
0B/B4: 6C00     	SUB	D,A
0B/B5: 8014     	LOAD	A,[OPERAND]
0B/B6: F005     	BRC	1F
0B/B8:          	
0B/B7:          	; SUBTRACT THE CARRY
0B/B7: 7801     	SUBI	C,1
0B/B8: F003     	BRC	1F
0B/B9: 6800     	SUB	C,A
0B/BA: 5000     	ADDI	A,0
0B/BB: FF01     	JUMP	2F
0B/BD:          	
0B/BC: 6800     1:	SUB	C,A
0B/BE:          
0B/BD:          	; IF CARRY IS SET, SKIP ADD AND INCREMENT 
0B/BD: F007     2:	BRC	2F
0B/BF:          
0B/BE:          	; ADD DIVISOR TO REMAINDER
0B/BE: 8015     	LOAD	A,[OPERAND+1]
0B/BF: 4C00     	ADD	D,A
0B/C0: F101     	BRNC	1F
0B/C1: 5801     	ADDI	C,1
0B/C2: 8014     1:	LOAD	A,[OPERAND]
0B/C3: 4800     	ADD	C,A
0B/C4: FF03     	JUMP	3F
0B/C6:          	
0B/C5:          	; INCREMENT [VALUE+1]
0B/C5: 8013     2:	LOAD	A,[VALUE+1]
0B/C6: 5001     	ADDI	A,1
0B/C7: A013     	STORE	[VALUE+1],A
0B/C9:          	
0B/C8:          	; DECREMENT COUNTER
0B/C8: 8011     3:	LOAD	A,[DVCOUNT]
0B/C9: 7001     	SUBI	A,1
0B/CA: F7C5     	BRNZ	0B
0B/CC:          
0B/CB:          	; DONE, REGRAB THE OPERATION
0B/CB: 802C     6:	LOAD	A,[EINDEX]
0B/CC: 9000     	LOADF	A,[A]
0B/CD: 702F     	SUBI	A,'/'
0B/CE: F602     	BRZ	6F
0B/D0:          	
0B/CF:          	; SET [VALUE] TO REMAINDER
0B/CF: A812     	STORE	[VALUE],C
0B/D0: AC13     	STORE	[VALUE+1],D
0B/D2:          
0B/D1:          	; RESTORE STACK
0B/D1: 8C10     6:	LOAD	D,[SPARK]
0B/D2: FF03     	JUMP	8F
0B/D4:          
0B/D3:          	; SWITCH INSTRUCTION BANK
0B/D3: 340C     7:	LOADI	B,EXP5_B
0B/D4: 3880     	LOADI	C,ESTKPOB
0B/D5: FF2C     	JUMP	INDIR
0B/D7:          
0B/D6:          	; PLACE [VALUE] ON STACK AND RETURN
0B/D6: 802D     8:	LOAD	A,[VINDEX]
0B/D7: 8413     	LOAD	B,[VALUE+1]
0B/D8: B4FF     	STOREF	[A+0-1],B
0B/D9: 8412     	LOAD	B,[VALUE]
0B/DA: B4FE     	STOREF	[A+0-2],B
0B/DB: FF27     	JUMP	IRET
0B/DD:          
0B/DC:          	; HANDLE ERROR
0B/DC: 3401     9:	LOADI	B,CORE0_B
0B/DD: 38D2     	LOADI	C,ERROR
0B/DE: FF23     	JUMP	INDIR
0B/E0:          	
0B/DF:          BI	= BI+1
0B/DF:          .TEXT
0B/DF:          .BANK	BI
0C/80:          EXP5_B	= BI
0C/80:          
0C/80:          	; CONTINUATION OF ESTKPOP
0C/80:          	; CHECK FOR RIGHT SHIFT
0C/80: 7019     ESTKPOB:SUBI	A,'>'-'%'
0C/81: F715     	BRNZ	7F
0C/83:          	
0C/82:          	; GET THE COUNT
0C/82: 8814     	LOAD	C,[OPERAND]
0C/83: 5800     	ADDI	C,0
0C/84: 3410     	LOADI	B,16
0C/85: F704     	BRNZ	0F
0C/86: 8815     	LOAD	C,[OPERAND+1]
0C/87: D600     	CMP	B,C
0C/88: F901     	BRBE	0F
0C/89: 2600     	MOV	B,C
0C/8B:          
0C/8A:          	; CHECK COUNTER
0C/8A: 5400     0:	ADDI	B,0
0C/8B: F663     	BRZ	8F
0C/8D:          	
0C/8C:          	; SHIFT [VALUE] RIGHT
0C/8C: 8012     	LOAD	A,[VALUE]
0C/8D: C100     	SHIFTR	A
0C/8E: A012     	STORE	[VALUE],A
0C/8F: 8013     	LOAD	A,[VALUE+1]
0C/90: F104     	BRNC	1F
0C/91: C100     	SHIFTR	A
0C/92: 5080     	ADDI	A,0X80
0C/93: A013     	STORE	[VALUE+1],A
0C/94: FF19     	JUMP	2F
0C/95: C100     1:	SHIFTR	A
0C/96: A013     	STORE	[VALUE+1],A
0C/98:          	
0C/97:          	; CHECK FOR LEFT SHIFT
0C/97: 70FE     7:	SUBI	A,'<'-'>'
0C/98: F717     	BRNZ	7F
0C/9A:          	
0C/99:          	; GET THE COUNT
0C/99: 8814     	LOAD	C,[OPERAND]
0C/9A: 5800     	ADDI	C,0
0C/9B: 3410     	LOADI	B,16
0C/9C: F704     	BRNZ	0F
0C/9D: 8815     	LOAD	C,[OPERAND+1]
0C/9E: D600     	CMP	B,C
0C/9F: F901     	BRBE	0F
0C/A0: 2600     	MOV	B,C
0C/A2:          
0C/A1:          	; CHECK COUNTER
0C/A1: 5400     0:	ADDI	B,0
0C/A2: F64C     	BRZ	8F
0C/A4:          	
0C/A3:          	; SHIFT [VALUE] LEFT
0C/A3: 8013     	LOAD	A,[VALUE+1]
0C/A4: 4000     	SHIFTL	A
0C/A5: A013     	STORE	[VALUE+1],A
0C/A6: 8012     	LOAD	A,[VALUE]
0C/A7: F104     	BRNC	1F
0C/A8: 4000     	SHIFTL	A
0C/A9: 5001     	ADDI	A,1
0C/AA: A012     	STORE	[VALUE],A
0C/AB: FF02     	JUMP	2F
0C/AC: 4000     1:	SHIFTL	A
0C/AD: A012     	STORE	[VALUE],A
0C/AF:          
0C/AE: 7401     2:	SUBI	B,1
0C/AF: FFF1     	JUMP	0B
0C/B1:          	
0C/B0:          	; CHECK FOR LOGICAL AND
0C/B0: 70EA     7:	SUBI	A,'&'-'<'
0C/B1: F71B     	BRNZ	7F
0C/B3:          	
0C/B2:          	; DO LOGICAL AND BETWEEN [VALUE] AND [OPERAND]
0C/B2: 8012     	LOAD	A,[VALUE]
0C/B3: 8414     	LOAD	B,[OPERAND]
0C/B4: 3808     	LOADI	C,8
0C/B6:          	
0C/B5: 4000     0:	SHIFTL	A
0C/B6: F104     	BRNC	1F
0C/B7: 4500     	SHIFTL	B
0C/B8: F103     	BRNC	2F
0C/B9: 5001     	ADDI	A,1
0C/BA: FF01     	JUMP	2F
0C/BB: 4500     1:	SHIFTL	B
0C/BC: 7801     2:	SUBI	C,1
0C/BD: F7F7     	BRNZ	0B
0C/BE: A012     	STORE	[VALUE],A
0C/C0:          	
0C/BF:          	; DO LOGICAL AND BETWEEN [VALUE+1] AND [OPERAND+1]
0C/BF: 8013     	LOAD	A,[VALUE+1]
0C/C0: 8415     	LOAD	B,[OPERAND+1]
0C/C1: 3808     	LOADI	C,8
0C/C3:          	
0C/C2: 4000     0:	SHIFTL	A
0C/C3: F104     	BRNC	1F
0C/C4: 4500     	SHIFTL	B
0C/C5: F103     	BRNC	2F
0C/C6: 5001     	ADDI	A,1
0C/C7: FF01     	JUMP	2F
0C/C8: 4500     1:	SHIFTL	B
0C/C9: 7801     2:	SUBI	C,1
0C/CA: F7F7     	BRNZ	0B
0C/CB: A013     	STORE	[VALUE+1],A
0C/CC: FF22     	JUMP	8F
0C/CE:          	
0C/CD:          	; CHECK FOR LOGICAL OR
0C/CD: 7056     7:	SUBI	A,'|'-'&'
0C/CE: F71D     	BRNZ	7F
0C/D0:          	
0C/CF:          	; DO LOGICAL OR BETWEEN [VALUE] AND [OPERAND]
0C/CF: 8012     	LOAD	A,[VALUE]
0C/D0: 8414     	LOAD	B,[OPERAND]
0C/D1: 3808     	LOADI	C,8
0C/D3:          	
0C/D2: 4000     0:	SHIFTL	A
0C/D3: F103     	BRNC	1F
0C/D4: 4500     	SHIFTL	B
0C/D5: 5001     	ADDI	A,1
0C/D6: FF03     	JUMP	2F
0C/D7: 4500     1:	SHIFTL	B
0C/D8: F101     	BRNC	2F
0C/D9: 5001     	ADDI	A,1
0C/DA: 7801     2:	SUBI	C,1
0C/DB: F7F6     	BRNZ	0B
0C/DC: A012     	STORE	[VALUE],A
0C/DE:          
0C/DD:          	; DO LOGICAL OR BETWEEN [VALUE+1] AND [OPERAND+1]
0C/DD: 8013     	LOAD	A,[VALUE+1]
0C/DE: 8415     	LOAD	B,[OPERAND+1]
0C/DF: 3808     	LOADI	C,8
0C/E1:          	
0C/E0: 4000     0:	SHIFTL	A
0C/E1: F103     	BRNC	1F
0C/E2: 4500     	SHIFTL	B
0C/E3: 5001     	ADDI	A,1
0C/E4: FF03     	JUMP	2F
0C/E5: 4500     1:	SHIFTL	B
0C/E6: F101     	BRNC	2F
0C/E7: 5001     	ADDI	A,1
0C/E8: 7801     2:	SUBI	C,1
0C/E9: F7F6     	BRNZ	0B
0C/EA: A013     	STORE	[VALUE+1],A
0C/EB: FF03     	JUMP	8F
0C/ED:          	
0C/EC:          	; SWITCH INSTRUCTION BANK
0C/EC: 340D     7:	LOADI	B,EXP6_B
0C/ED: 3880     	LOADI	C,ESTKPOC
0C/EE: FF13     	JUMP	INDIR
0C/F0:          
0C/EF:          	; PLACE [VALUE] ON STACK AND RETURN
0C/EF: 802D     8:	LOAD	A,[VINDEX]
0C/F0: 8413     	LOAD	B,[VALUE+1]
0C/F1: B4FF     	STOREF	[A+0-1],B
0C/F2: 8412     	LOAD	B,[VALUE]
0C/F3: B4FE     	STOREF	[A+0-2],B
0C/F4: FF0E     	JUMP	IRET
0C/F6:          	
0C/F5:          BI	= BI+1
0C/F5:          .TEXT
0C/F5:          .BANK	BI
0D/80:          EXP6_B	= BI
0D/80:          
0D/80:          	; CONTINUATION OF ESTKPOP
0D/80:          	; CHECK FOR XOR
0D/80: 70E2     ESTKPOC:SUBI	A,'^'-'|'
0D/81: F71F     	BRNZ	7F
0D/83:          	
0D/82:          	; DO LOGICAL XOR BETWEEN [VALUE] AND [OPERAND]
0D/82: 8012     	LOAD	A,[VALUE]
0D/83: 8414     	LOAD	B,[OPERAND]
0D/84: 3808     	LOADI	C,8
0D/86:          	
0D/85: 4000     0:	SHIFTL	A
0D/86: F104     	BRNC	1F
0D/87: 4500     	SHIFTL	B
0D/88: F005     	BRC	2F
0D/89: 5001     	ADDI	A,1
0D/8A: FF03     	JUMP	2F
0D/8B: 4500     1:	SHIFTL	B
0D/8C: F101     	BRNC	2F
0D/8D: 5001     	ADDI	A,1
0D/8E: 7801     2:	SUBI	C,1
0D/8F: F7F5     	BRNZ	0B
0D/90: A012     	STORE	[VALUE],A
0D/92:          	
0D/91:          	; DO LOGICAL XOR BETWEEN [VALUE+1] AND [OPERAND+1]
0D/91: 8013     	LOAD	A,[VALUE+1]
0D/92: 8415     	LOAD	B,[OPERAND+1]
0D/93: 3808     	LOADI	C,8
0D/95:          	
0D/94: 4000     0:	SHIFTL	A
0D/95: F104     	BRNC	1F
0D/96: 4500     	SHIFTL	B
0D/97: F005     	BRC	2F
0D/98: 5001     	ADDI	A,1
0D/99: FF03     	JUMP	2F
0D/9A: 4500     1:	SHIFTL	B
0D/9B: F101     	BRNC	2F
0D/9C: 5001     	ADDI	A,1
0D/9D: 7801     2:	SUBI	C,1
0D/9E: F7F5     	BRNZ	0B
0D/9F: A013     	STORE	[VALUE+1],A
0D/A0: FF29     	JUMP	8F
0D/A2:          	
0D/A1:          	; CHECK FOR NOT-OR
0D/A1: 70C3     7:	SUBI	A,'!'-'^'
0D/A2: F721     	BRNZ	7F
0D/A4:          	
0D/A3:          	; DO LOGICAL NOT-OR BETWEEN [VALUE] AND [OPERAND]
0D/A3: 8012     	LOAD	A,[VALUE]
0D/A4: 8814     	LOAD	C,[OPERAND]
0D/A5: 34FF     	LOADI	B,0XFF
0D/A6: 6600     	SUB	B,C
0D/A7: 3808     	LOADI	C,8
0D/A9:          	
0D/A8: 4000     0:	SHIFTL	A
0D/A9: F103     	BRNC	1F
0D/AA: 4500     	SHIFTL	B
0D/AB: 5001     	ADDI	A,1
0D/AC: FF03     	JUMP	2F
0D/AD: 4500     1:	SHIFTL	B
0D/AE: F101     	BRNC	2F
0D/AF: 5001     	ADDI	A,1
0D/B0: 7801     2:	SUBI	C,1
0D/B1: F7F6     	BRNZ	0B
0D/B2: A012     	STORE	[VALUE],A
0D/B4:          
0D/B3:          	; DO LOGICAL NOT-OR BETWEEN [VALUE+1] AND [OPERAND+1]
0D/B3: 8013     	LOAD	A,[VALUE+1]
0D/B4: 8815     	LOAD	C,[OPERAND+1]
0D/B5: 34FF     	LOADI	B,0XFF
0D/B6: 6600     	SUB	B,C
0D/B7: 3808     	LOADI	C,8
0D/B9:          	
0D/B8: 4000     0:	SHIFTL	A
0D/B9: F103     	BRNC	1F
0D/BA: 4500     	SHIFTL	B
0D/BB: 5001     	ADDI	A,1
0D/BC: FF03     	JUMP	2F
0D/BD: 4500     1:	SHIFTL	B
0D/BE: F101     	BRNC	2F
0D/BF: 5001     	ADDI	A,1
0D/C0: 7801     2:	SUBI	C,1
0D/C1: F7F6     	BRNZ	0B
0D/C2: A013     	STORE	[VALUE+1],A
0D/C3: FF06     	JUMP	8F
0D/C5:          	
0D/C4:          	; ENSURE THIS ISN'T A LEFT PARENTHESIS
0D/C4: 7007     7:	SUBI	A,'('-'!'
0D/C5: F704     	BRNZ	8F	; IDK WHAT IT IS AT THIS POINT
0D/C7:          	
0D/C6:          	; ERROR! PARENTHESIS ARE MISMATCHED
0D/C6: 3088     	LOADI	A,E_PAREN
0D/C7: 3401     	LOADI	B,CORE0_B
0D/C8: 38D2     	LOADI	C,ERROR
0D/C9: FF38     	JUMP	INDIR
0D/CB:          
0D/CA:          	; PLACE [VALUE] ON STACK AND RETURN
0D/CA: 802D     8:	LOAD	A,[VINDEX]
0D/CB: 8413     	LOAD	B,[VALUE+1]
0D/CC: B4FF     	STOREF	[A+0-1],B
0D/CD: 8412     	LOAD	B,[VALUE]
0D/CE: B4FE     	STOREF	[A+0-2],B
0D/CF: FF33     	JUMP	IRET
0D/D1:          ; AS4.S
0D/D0:          ; ASSEMBLER SYMBOL MANAGEMENT
0D/D0:          ; GAVIN TERSTEEG, 2024
0D/D0:          ; SDMAY24-14
0D/D0:          
0D/D0:          BI	= BI+1
0D/D0:          .TEXT
0D/D0:          .BANK	BI
0E/80:          SYM0_B	= BI
0E/80:          
0E/80:          	; DEFINES OR UPDATES A SYMBOL IN THE TABLE
0E/80:          	; [TBUF] = SYMBOL NAME
0E/80:          	; RETURNS POINTER TO THE SYMBOL VALUE IN [DFBANK] AND [DFPNTR]
0E/80: 340F     DEFINE:	LOADI	B,SYM1_B
0E/81: 3880     	LOADI	C,DEFINEA
0E/82: FF7F     	JUMP	INDIR
0E/84:          	
0E/83:          	; ADDS A NEW LOCAL SYMBOL TO THE TABLE
0E/83:          	; A = SYMBOL NAME
0E/83:          	; [COUNTER] = SYMBOL VALUE
0E/83: 3410     ADDLOC:	LOADI	B,SYM2_B
0E/84: 3880     	LOADI	C,ADDLOCA
0E/85: FF7C     	JUMP	INDIR
0E/87:          	
0E/86:          	; GET A SYMBOL VALUE
0E/86:          	; [TBUF] = SYMBOL NAME
0E/86:          	; RETURNS VALUE OF SYMBOL IN [VALUE]
0E/86:          	; A = 0X00 IF DEFINE, 0XFF IF UNDEFINED 
0E/86: AC10     GETSYM:STORE	[SPARK],D
0E/88:          
0E/87:          	; START BY SEARCHING THE SYMBOL TABLE FOR EXISTING SYMBOLS
0E/87: 3409     	LOADI	B,STAB_B
0E/88: 3C01     0:	LOADI	D,ST_NAME
0E/89: A416     	STORE	[TEMP],B
0E/8B:          	
0E/8A:          	; DO NAME CHECK
0E/8A: 38F8     1:	LOADI	C,0-8
0E/8C:          
0E/8B:          	; COMPARE TBUF AND CURRENT SYMBOL
0E/8B: 5808     2:	ADDI	C,8
0E/8C: 9239     	LOADF	A,[C+TBUF]
0E/8D: 8416     	LOAD	B,[TEMP]
0E/8E: A480     	STORE	[DBANK],B
0E/8F: 9700     	LOADF	B,[D]
0E/90: D100     	CMP	A,B
0E/91: F715     	BRNZ	3F
0E/92: 5000     	ADDI	A,0
0E/93: F606     	BRZ	2F
0E/94: 3000     	LOADI 	A,0
0E/95: A080     	STORE	[DBANK],A
0E/96: 5C01     	ADDI	D,1
0E/97: 7807     	SUBI	C,7
0E/98: F7F2     	BRNZ	2B
0E/99: 3808     	LOADI	C,8
0E/9B:          	
0E/9A:          	; STRING TERMINATES, FIX POINTER AND RETURN
0E/9A: 6E00     2:	SUB	D,C
0E/9B: 5C08     	ADDI	D,8
0E/9D:          	
0E/9C:          	; FOUND THE SYMBOL, GRAB THE VALUE
0E/9C: 9301     	LOADF	A,[D+1]
0E/9D: 9702     	LOADF	B,[D+2]
0E/9E: 9F00     	LOADF	D,[D]
0E/9F: 3800     	LOADI	C,0
0E/A0: A880     	STORE	[DBANK],C
0E/A1: A012     	STORE	[VALUE],A
0E/A2: A413     	STORE	[VALUE+1],B
0E/A3: 30FF     	LOADI	A,0XFF
0E/A4: 6300     	SUB	A,D
0E/A5: 8C10     	LOAD	D,[SPARK]
0E/A6: FF5C     	JUMP	IRET
0E/A8:          
0E/A7:          	; SYMBOL DOES NOT MATCH
0E/A7: 6E00     3:	SUB	D,C
0E/A8: 9300     	LOADF	A,[D]
0E/A9: 5000     	ADDI	A,0
0E/AA: F60E     	BRZ	5F
0E/AC:          	
0E/AB:          	; EXIT SYMBOL ENTRY
0E/AB: 5C16     	ADDI	D,ST_SIZE*2
0E/AC: F404     	BRN	4F
0E/AD: 7C0B     	SUBI	D,ST_SIZE
0E/AE: 3000     	LOADI	A,0
0E/AF: A080     	STORE	[DBANK],A
0E/B0: FFD9     	JUMP	1B
0E/B2:          
0E/B1:          	; NEXT BANK
0E/B1: 8000     4:	LOAD	A,[ST_NEXT]
0E/B2: 5000     	ADDI	A,0
0E/B3: 3800     	LOADI	C,0
0E/B4: A880     	STORE	[DBANK],C
0E/B5: F603     	BRZ	5F
0E/B6: A016     	STORE	[TEMP],A
0E/B7: 3C01     	LOADI	D,ST_NAME
0E/B8: FFD1     	JUMP	1B
0E/BA:          
0E/B9:          	; CANNOT FIND SYMBOL!
0E/B9: 3800     5:	LOADI	C,0
0E/BA: A880     	STORE	[DBANK],C
0E/BB: 8C10     	LOAD	D,[SPARK]
0E/BC: 30FF     	LOADI	A,0XFF
0E/BD: FF45     	JUMP	IRET
0E/BF:          	
0E/BE:          	; GETS THE VALUE OF A LOCAL SYMBOL
0E/BE:          	; A COUNTER MUST BE SUPPLIED TO DETERMINE
0E/BE:          	; HOW MANY ENTRIES WILL BE SKIPPED BEFORE
0E/BE:          	; A MATCH CAN BE MADE
0E/BE:          	; A = NAME OF LOCAL SYMBOL
0E/BE:          	; [NUMLOC] = # OF LOCALS TO SKIP
0E/BE:          	; RETURNS A = 0XFF IF UNDEFINED, 0X00 OTHERWISE
0E/BE:          	; VALUE IS RETURNED IN [VALUE]
0E/BE: A011     GETLOC:	STORE	[CHAR],A
0E/BF: 804D     	LOAD	A,[NUMLOC]
0E/C0: 844E     	LOAD	B,[NUMLOC+1]
0E/C1: A016     	STORE	[TEMP],A
0E/C2: A417     	STORE	[TEMP+1],B
0E/C3: 380A     	LOADI	C,LTAB_B
0E/C5:          
0E/C4: 8412     0:	LOAD	B,[VALUE]
0E/C5: 5400     	ADDI	B,0
0E/C6: F700     	BRNZ	1F
0E/C8:          	
0E/C7:          	
0E/C7:          	; NEXT BANK
0E/C7: A880     1:	STORE	[DBANK],C
0E/C8: 8800     	LOAD	C,[LT_NEXT]
0E/C9: 3000     	LOADI	A,0
0E/CA: A080     	STORE	[DBANK],A
0E/CB: 5800     	ADDI	C,0
0E/CC: F6B8     	BRZ	2F
0E/CE:          	
0E/CD:          	; SUBTRACT FROM [VALUE]
0E/CD: 8413     	LOAD	B,[VALUE+1]
0E/CE: 303F     	LOADI	A,LT_CNT
0E/CF: 6400     	SUB	B,A
0E/D0: A413     	STORE	[VALUE+1],B
0E/D1: F0F2     	BRC	0B
0E/D2: 8412     	LOAD	B,[VALUE]
0E/D3: 7401     	SUBI	B,1
0E/D4: A412     	STORE	[VALUE],B
0E/D5: FFEE     	JUMP	0B
0E/D7:          
0E/D6:          
0E/D6:          BI	= BI+1
0E/D6:          .TEXT
0E/D6:          .BANK	BI
0F/80:          SYM1_B	= BI
0F/80:          
0F/80:          	; SHADOW OF DEFINE
0F/80: AC10     DEFINEA:STORE	[SPARK],D
0F/82:          
0F/81:          	; START BY SEARCHING THE SYMBOL TABLE FOR EXISTING SYMBOLS
0F/81: 3409     	LOADI	B,STAB_B
0F/82: 3C01     0:	LOADI	D,ST_NAME
0F/83: A44F     	STORE	[DFBANK],B
0F/85:          	
0F/84:          	; DO NAME CHECK
0F/84: 38F8     1:	LOADI	C,0-8
0F/86:          
0F/85:          	; COMPARE TBUF AND CURRENT SYMBOL
0F/85: 5808     2:	ADDI	C,8
0F/86: 9239     	LOADF	A,[C+TBUF]
0F/87: 844F     	LOAD	B,[DFBANK]
0F/88: A480     	STORE	[DBANK],B
0F/89: 9700     	LOADF	B,[D]
0F/8A: D100     	CMP	A,B
0F/8B: F70F     	BRNZ	3F
0F/8C: 5000     	ADDI	A,0
0F/8D: F606     	BRZ	2F
0F/8E: 3000     	LOADI 	A,0
0F/8F: A080     	STORE	[DBANK],A
0F/90: 5C01     	ADDI	D,1
0F/91: 7807     	SUBI	C,7
0F/92: F7F2     	BRNZ	2B
0F/93: 3808     	LOADI	C,8
0F/95:          	
0F/94:          	; STRING TERMINATES, FIX POINTER AND RETURN
0F/94: 6E00     2:	SUB	D,C
0F/95: 5C08     	ADDI	D,8
0F/97:          	
0F/96:          	; FOUND THE SYMBOL, SAVE POINTER
0F/96: 3000     9:	LOADI	A,0
0F/97: A080     	STORE	[DBANK],A
0F/98: AC50     	STORE	[DFPNTR],D
0F/99: 8C10     	LOAD	D,[SPARK]
0F/9A: FF68     	JUMP	IRET
0F/9C:          
0F/9B:          	; SYMBOL DOES NOT MATCH
0F/9B: 6E00     3:	SUB	D,C
0F/9C: 9300     	LOADF	A,[D]
0F/9D: 5000     	ADDI	A,0
0F/9E: F626     	BRZ	0F
0F/A0:          	
0F/9F:          	; EXIT SYMBOL ENTRY
0F/9F: 5C16     	ADDI	D,ST_SIZE*2
0F/A0: F404     	BRN	4F
0F/A1: 7C0B     	SUBI	D,ST_SIZE
0F/A2: 3000     	LOADI	A,0
0F/A3: A080     	STORE	[DBANK],A
0F/A4: FFDF     	JUMP	1B
0F/A6:          
0F/A5:          	; NEXT BANK
0F/A5: 8000     4:	LOAD	A,[ST_NEXT]
0F/A6: 5000     	ADDI	A,0
0F/A7: 3800     	LOADI	C,0
0F/A8: A880     	STORE	[DBANK],C
0F/A9: F603     	BRZ	5F
0F/AA: A04F     	STORE	[DFBANK],A
0F/AB: 3C01     	LOADI	D,ST_NAME
0F/AC: FFD7     	JUMP	1B
0F/AE:          	
0F/AD:          	; ALLOCATE A NEW BANK
0F/AD: 844C     5:	LOAD	B,[NFREE]
0F/AE: 8071     	LOAD	A,[MAX_DB]
0F/AF: D400     	CMP	B,A
0F/B0: F904     	BRBE	6F
0F/B2:          	
0F/B1:          	; ERROR! OUT OF MEMORY
0F/B1: 308A     	LOADI	A,E_OMEM
0F/B2: 3401     	LOADI	B,CORE0_B
0F/B3: 38D2     	LOADI	C,ERROR
0F/B4: FF4D     	JUMP	INDIR
0F/B6:          	
0F/B5:          	; SETUP NEW BANK 
0F/B5: 5401     6:	ADDI	B,1
0F/B6: A44C     	STORE	[NFREE],B
0F/B7: 7401     	SUBI	B,1
0F/B8: 804F     	LOAD	A,[DFBANK]
0F/B9: A080     	STORE	[DBANK],A
0F/BA: A400     	STORE	[ST_NEXT],B
0F/BB: A480     	STORE	[DBANK],B
0F/BD:          	
0F/BC: 3000     	LOADI	A,0
0F/BD: 3801     	LOADI	C,ST_NAME
0F/BE: A000     	STORE	[ST_NEXT],A
0F/BF: B200     7:	STOREF	[C+(ST_NAME-ST_NAME)],A
0F/C0: B208     	STOREF	[C+(ST_DEF-ST_NAME)],A
0F/C1: 580B     	ADDI	C,ST_SIZE
0F/C2: F5FC     	BRNN	7B
0F/C4:          	
0F/C3: A080     	STORE	[DBANK],A
0F/C4: FFBD     	JUMP	0B
0F/C6:          	
0F/C5:          	; EMPTY SLOT, PUT THE SYMBOL HERE
0F/C5: 3808     0:	LOADI	C,8
0F/C6: 5C08     	ADDI	D,8
0F/C7: 3400     1:	LOADI	B,0
0F/C8: A480     	STORE	[DBANK],B
0F/C9: 7C01     	SUBI	D,1
0F/CA: 7801     	SUBI	C,1
0F/CB: F105     	BRNC	2F
0F/CC: 9239     	LOADF	A,[C+TBUF]
0F/CD: 844F     	LOAD	B,[DFBANK]
0F/CE: A480     	STORE	[DBANK],B
0F/CF: B300     	STOREF	[D],A
0F/D0: FFF6     	JUMP	1B
0F/D2:          
0F/D1:          	; GO PLACE THE POINTER
0F/D1: 5C09     2:	ADDI	D,9
0F/D2: FFC3     	JUMP	9B
0F/D4:          	
0F/D3:          BI	= BI+1
0F/D3:          .TEXT
0F/D3:          .BANK	BI
10/80:          SYM2_B	= BI
10/80:          
10/80:          	; SHADOW OF ADDLOC
10/80:          	; FIND THE END OF THE TABLE
10/80: 380A     ADDLOCA:LOADI	C,LTAB_B
10/81: A880     0:	STORE	[DBANK],C
10/82: 8800     	LOAD	C,[LT_NEXT]
10/83: 5800     	ADDI	C,0
10/84: F7FC     	BRNZ	0B
10/86:          	
10/85:          	; IS THERE SPACE TO STORE A NEW LOCAL HERE?
10/85: 8401     	LOAD	B,[LT_FREE]
10/86: 5401     	ADDI	B,LT_SIZE-1
10/87: F51E     	BRNN	3F
10/89:          	
10/88:          	; ALLOCATE A NEW BANK
10/88: 3400     	LOADI	B,0
10/89: A480     	STORE	[DBANK],B
10/8A: AC10     	STORE	[SPARK],D
10/8B: 844C     	LOAD	B,[NFREE]
10/8C: 8C71     	LOAD	D,[MAX_DB]
10/8D: D700     	CMP	B,D
10/8E: 8C10     	LOAD	D,[SPARK]
10/8F: F904     	BRBE	1F
10/91:          	
10/90:          	; ERROR! OUT OF MEMORY
10/90: 308A     	LOADI	A,E_OMEM
10/91: 3401     	LOADI	B,CORE0_B
10/92: 38D2     	LOADI	C,ERROR
10/93: FF6E     	JUMP	INDIR
10/95:          	
10/94:          	; LINK AND SETUP
10/94: 5401     1:	ADDI	B,1
10/95: A44C     	STORE	[NFREE],B
10/96: A880     	STORE	[DBANK],C
10/97: A400     	STORE	[LT_NEXT],B
10/98: 2900     	MOV	C,B
10/99: A880     	STORE	[DBANK],C
10/9A: 3C02     	LOADI	D,LT_NAME
10/9B: AC01     	STORE	[LT_FREE],D
10/9C: 3400     	LOADI	B,0
10/9D: A400     	STORE	[LT_NEXT],B
10/9F:          
10/9E:          	; ZERO OUT ALL NAMES
10/9E: B700     2:	STOREF	[D],B
10/9F: 5C02     	ADDI	D,LT_SIZE
10/A0: F5FD     	BRNN	2B
10/A1: 3400     	LOADI	B,0
10/A2: A480     	STORE	[DBANK],B
10/A3: 8C10     	LOAD	D,[SPARK]
10/A4: A880     	STORE	[DBANK],C
10/A5: 3403     	LOADI	B,LT_NAME+(LT_SIZE-1)
10/A7:          
10/A6:          	; THERE IS SPACE, LETS USE IT
10/A6: 7401     3:	SUBI	B,LT_SIZE-1
10/A7: B100     	STOREF	[B],A
10/A8: 3000     	LOADI	A,0
10/A9: A080     	STORE	[DBANK],A
10/AA: 8048     	LOAD	A,[COUNTER]
10/AB: A880     	STORE	[DBANK],C
10/AC: B101     	STOREF	[B+1],A
10/AD: 3000     	LOADI	A,0
10/AE: A080     	STORE	[DBANK],A
10/AF: FF53     	JUMP	IRET
10/B1:          
10/B0:          	; START OF SYMBOL TABLE
10/B0:          .BANK	BD
09/ED:          .DATA
09/00:          STAB_B	= BD
09/00:          
09/00:          	; SYMBOL TABLE DEFINITION
09/00:          	; 0X00     : NEXT BANK
09/00:          	; 0X01-0X08: SYMBOL 0 NAME
09/00:          	; 0X09     : SYMBOL 0 DEFINED
09/00:          	; 0X0A-0X0B: SYMBOL 0 VALUE
09/00:          	; 0X0C-0X14: SYMBOL 1 NAME
09/00:          	; 0X15     : SYMBOL 1 DEFINED
09/00:          	; 0X16-0X17: SYMBOL 1 VALUE
09/00:          	
09/00:          	; IF A SYMBOL NAME STARTS WITH 0X00, IT IS UNALLOCATED
09/00:          
09/00: 00       .DEFL BYTE ST_NEXT	0
09/01: 00000000
       00000000 .DEFL BYTE ST_NAME	0,0,0,0,0,0,0,0
09/09: 00       .DEFL BYTE ST_DEF	0
09/0A: 0000     .DEFL BYTE ST_VAL	0,0
09/0C:          ST_SIZE	= @-ST_NAME
09/0C:          
09/0C:          BD	= BD+1
09/0C:          
09/0C:          	; START OF LOCAL TABLE
09/0C:          .BANK	BD
0A/00:          .DATA
0A/00:          LTAB_B	= BD
0A/00:          
0A/00:          	; LOCAL TABLE DEFINITION
0A/00:          	; 0X00: NEXT BANK
0A/00:          	; 0X01: NEXT FREE SYMBOL
0A/00:          	; 0X02: SYMBOL 0 NAME
0A/00:          	; 0X03: SYMBOL 0 VALUE
0A/00:          	; 0X04: SYMBOL 1 NAME
0A/00:          	; 0X05: SYMBOL 1 VALUE
0A/00:          	
0A/00: 00       .DEFL BYTE LT_NEXT	0
0A/01: 00       .DEFL BYTE LT_FREE	0
0A/02: 00       .DEFL BYTE LT_NAME	0
0A/03: 00       .DEFL BYTE LT_VAL	0
0A/04:          LT_SIZE = @-LT_NAME
0A/04:          LT_CNT	= (128-LT_NAME)/LT_SIZE
0A/04:          	
0A/04:          
0A/04:          BD	= BD+1
0A/04:          
0A/04:          	; SET START OF HEAP
0A/04:          HEAP	= BD
