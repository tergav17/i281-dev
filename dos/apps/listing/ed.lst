00/00:          ; DOSDEF.S
01/80:          ; THIS FILE CONTAINS IMPORTANT DEFINES USED IN DOS/281
01/80:          
01/80:          ; BIOS CALLS
01/80:          REBOOT	= 0
01/80:          INDIR	= 1
01/80:          IRET	= 2
01/80:          SYSCALL	= 3
01/80:          SYSJUMP	= 4
01/80:          PRGM	= 5
01/80:          IWRITE	= 6
01/80:          
01/80:          HALT	= 0X7F
01/80:          
01/80:          ; SYSTEM CALLS
01/80:          S_EXIT	= 0
01/80:          S_PUTC	= 1
01/80:          S_GETC	= 2
01/80:          S_STAT	= 3
01/80:          S_PUTS	= 4
01/80:          S_INPUT	= 5
01/80:          S_OPEN	= 6
01/80:          S_CLOSE	= 7
01/80:          S_READ	= 8
01/80:          S_WRITE	= 9
01/80:          S_FSRCH	= 10
01/80:          S_NEXT	= 11
01/80:          S_DELET	= 12
01/80:          S_CREAT	= 13
01/80:          S_FREE	= 14
01/80:          S_EXEC	= 15
01/80:          
01/80:          ; MEMORY AREAS
01/80:          CF_NAME	= 0X60
01/80:          CF_SIZE	= 0X68
01/80:          CF_USR	= 0X6A
01/80:          DFT_USR	= 0X6B
01/80:          ARG_BNK	= 0X6C
01/80:          BD_FREE	= 0X6E
01/80:          MAX_IB	= 0X70
01/80:          MAX_DB	= 0X71
01/80:          CMDL_B	= 0X72
01/80:          AUTO_B	= 0X73
01/80:          KERNMEM	= 0X78
01/80:          BIOSMEM	= 0X7C
01/80:          
01/80:          ; DATA BANK ADDRESS
01/80:          DBANK	= 0X80		
01/80:          
01/80:          ; ED0.S
01/80:          ; SIMPLE TEXT EDITOR
01/80:          ; USER INTERFACE AND MEMORY MANAGEMENT
01/80:          
01/80:          ; BANK ALLOCATION STUFF
01/80:          BI	= 1
01/80:          BD	= 1
01/80:          
01/80:          .TEXT
01/80:          .BANK	BI
01/80:          CORE0_B	= BI
01/80:          
01/80:          	; START BY PROCESSING THE ARGUMENTS
01/80: 3000     START:	LOADI	A,0
01/81: A080     	STORE	[DBANK],A
01/83:          	
01/82:          	; SET UP STACK
01/82: 3C5E     	LOADI	D,0X60-2
01/84:          	
01/83:          	; SYSCALL RETURN BANK
01/83: 3801     	LOADI	C,BI
01/84: BB01     	STOREF	[D+1],C
01/86:          	
01/85:          	; SAVE STACK
01/85: AC0C     	STORE	[SPARK],D
01/87:          	
01/86:          	; GO TO ARGUMENT BANK
01/86: 8072     	LOAD	A,[CMDL_B]
01/87: A06C     	STORE	[ARG_BNK],A
01/88: A080     	STORE	[DBANK],A
01/8A:          	
01/89:          	; SET UP POINTER
01/89: 3800     	LOADI	C,0
01/8B:          	
01/8A:          	; SKIP THE INITIAL COMMAND
01/8A: 9200     0:	LOADF	A,[C]
01/8B: 3420     	LOADI	B,0X20
01/8C: D100     	CMP	A,B
01/8D: F902     	BRBE	1F
01/8E: 5801     	ADDI	C,1
01/8F: FFFA     	JUMP	0B
01/91:          
01/90:          	; LOOK FOR THE FIRST ARGUMENT
01/90: 9200     1:	LOADF	A,[C]
01/91: 5000     	ADDI	A,0
01/92: F604     	BRZ	2F
01/93: D100     	CMP	A,B
01/94: F809     	BRA	OPENARG
01/95: 5801     	ADDI	C,1
01/96: FFF9     	JUMP	1B
01/98:          	
01/97:          	; NO ARGUMENT, EMPTY BUFFER
01/97: 3000     2:	LOADI	A,0
01/98: A080     	STORE	[DBANK],A
01/9A:          	
01/99:          	; INIT MEMORY
01/99: 38B9     	LOADI	C,9F
01/9A: BB00     	STOREF	[D],C
01/9B: 3402     	LOADI	B,MEM0_B
01/9C: 3880     	LOADI	C,MINIT
01/9D: FF63     	JUMP	INDIR
01/9F:          	
01/9E:          	; JUMP TO COMMAND LINE
01/9E:          
01/9E:          	
01/9E:          	; THERE IS AN ARG, TRY AND OPEN IT
01/9E: 3C18     OPENARG:LOADI	D,FNAME
01/9F: 9200     0:	LOADF	A,[C]
01/A0: 3400     	LOADI	B,0
01/A1: A480     	STORE	[DBANK],B
01/A2: B300     	STOREF	[D],A
01/A3: 7020     	SUBI	A,0X20
01/A4: F908     	BRBE	1F
01/A5: 5801     	ADDI	C,1
01/A6: 5C01     	ADDI	D,1
01/A7: 3427     	LOADI	B,FNAME+15
01/A8: DD00     	CMP	D,B
01/A9: F003     	BRAE	1F
01/AA: 8472     	LOAD	B,[CMDL_B]
01/AB: A480     	STORE	[DBANK],B
01/AC: FFF2     	JUMP	0B
01/AE:          	
01/AD:          	; TERMINATE STRING AND OPEN FILE
01/AD: 3000     1:	LOADI	A,0
01/AE: B300     	STOREF	[D],A
01/AF: 8C0C     	LOAD	D,[SPARK]
01/B1:          	
01/B0:          	; OPEN THE FILE
01/B0: 38B5     	LOADI	C,@+5
01/B1: BB00     	STOREF	[D],C
01/B2: 3403     	LOADI	B,FIO0_B
01/B3: 3880     	LOADI	C,FREAD
01/B4: FF4C     	JUMP	INDIR
01/B6:          
01/B5:          	; CHECK ERROR STATUS
01/B5: 5000     	ADDI	A,0
01/B6: 3405     	LOADI	B,CMD0_B
01/B7: 3880     	LOADI	C,ERROR
01/B8: F748     	BRNZ	INDIR
01/BA:          
01/B9:          	; MEMORY SETUP IS DONE
01/B9:          	; SEND UP THE COMMAND PROMPT
01/B9: 3405     9:	LOADI	B,CMD0_B
01/BA: 3882     	LOADI	C,PROMPT
01/BB: FF45     	JUMP	INDIR
01/BD:          
01/BC:          	; EXIT PROGRAM
01/BC: 3400     DONE:	LOADI	B,S_EXIT
01/BD: FF46     	JUMP	SYSJUMP
01/BF:          
01/BE:          	; BANK IS DONE, MOVE ON TO THE NEXT
01/BE:          BI	= BI+1
01/BE:          .TEXT
01/BE:          .BANK	BI
02/80:          MEM0_B	= BI
02/80:          
02/80:          	; MEMORY INIT
02/80:          	; DIVIDE THE HEAP INTO BLOCKS OF 32 BYTES, AND ADD IT
02/80:          	; TO THE FREE TABLE
02/80:          	; USES: A, B, C
02/80: 3806     MINIT:	LOADI	C,HEAP
02/81: 8471     	LOAD	B,[MAX_DB]
02/83:          	
02/82:          	; SET THE START OF THE FREE BLOCK LIST
02/82: 3000     	LOADI	A,0
02/83: A80D     	STORE	[FREETAB],C
02/84: A00E     	STORE	[FREETAB+1],A
02/86:          	
02/85:          	; MOVE TO BANK
02/85: A880     0:	STORE	[DBANK],C
02/86: 3020     	LOADI	A,32
02/88:          	
02/87:          	; WRITE FIRST 3 HEADERS
02/87: B0E1     1:	STOREF	[A+1-32],A
02/88: B8E0     	STOREF	[A+0-32],C
02/89: 5020     	ADDI	A,32
02/8A: F5FC     	BRNN	1B
02/8C:          	
02/8B:          	; WRITE 4TH HEADER
02/8B: 3000     2:	LOADI	A,0
02/8C: A061     	STORE	[32*3+1],A
02/8D: 5801     	ADDI	C,1
02/8E: D900     	CMP	C,B
02/8F: F802     	BRA	3F
02/90: A860     	STORE	[32*3],C
02/91: FFF3     	JUMP	0B
02/93:          	
02/92:          	; STORE BANK ZERO TO INDICATE END OF LIST
02/92: A060     3:	STORE	[32*3],A
02/93: A080     	STORE	[DBANK],A
02/94: FF6D     	JUMP	IRET
02/96:          	
02/95:          	
02/95:          	; ALLOCATE A BLOCK OF MEMORY
02/95:          	; BLOCK WILL BE LINKED ONTO THE CURRENT BLOCK IN [CBLOCK]
02/95:          	; THEN IT WILL BE RETURN IN [CBLOCK]
02/95:          	; RETURNS A = 0XFF OUT OF BLOCKS, A = 0X00 OTHERWISE
02/95:          	; USES: A, B, C
02/95: AC0C     MALLOC:	STORE	[SPARK],D
02/96: 840D     	LOAD	B,[FREETAB]
02/98:          	
02/97:          	; MAKE SURE THE NEXT BLOCK ISN'T NULL
02/97: 5400     	ADDI	B,0
02/98: 30FF     	LOADI	A,0XFF
02/99: F668     	BRZ	IRET
02/9B:          	
02/9A:          	; STORE THE BLOCK ADDRESS IN LBLOCK
02/9A: 880E     	LOAD	C,[FREETAB+1]
02/9B: A402     	STORE	[LBLOCK],B
02/9C: A803     	STORE	[LBLOCK+1],C
02/9E:          	
02/9D:          	; MOVE THE NEXT BLOCK INTO FREETAB POINTER
02/9D: A480     	STORE	[DBANK],B
02/9E: 9200     	LOADF	A,[C]
02/9F: 9601     	LOADF	B,[C+1]
02/A0: 3C00     	LOADI	D,0
02/A1: BE00     	STOREF	[C],D
02/A2: BE01     	STOREF	[C+1],D
02/A3: AC80     	STORE	[DBANK],D
02/A4: A00D     	STORE	[FREETAB],A
02/A5: A40E     	STORE	[FREETAB+1],B
02/A7:          	
02/A6:          	; RETURN TO USER BANK
02/A6: 3000     	LOADI	A,0
02/A7: A080     	STORE	[DBANK],A
02/A9:          	
02/A8:          	; SET THE MEMORY BLOCK POINTER
02/A8: 8003     	LOAD	A,[LBLOCK+1]
02/A9: 5003     	ADDI	A,3
02/AA: A007     	STORE	[MB_PNTR],A
02/AB: 501D     	ADDI	A,32-3
02/AC: A008     	STORE	[MB_END],A
02/AD: 3000     	LOADI	A,0
02/AE: A00B     	STORE	[LINES],A
02/B0:          	
02/AF:          	; RESTORE STACK AND LINK
02/AF: 8C0C     	LOAD	D,[SPARK]
02/B1:          	
02/B0:          	
02/B0:          	; LINK THE BLOCK IN LBLOCK ONTO CBLOCK
02/B0:          	; THE NEXT BLOCK OF CBLOCK WILL BECOME
02/B0:          	; THE NEXT BLOCK OF LBLOCK
02/B0:          	; AFTER THAT, CBLOCK WILL BECOME LBLOCK
02/B0:          	; RETURNS A = 0X00
02/B0:          	; USES: A, B, C
02/B0: AC0C     LINK:	STORE	[SPARK],D
02/B2:          	
02/B1:          	; GET ADDRESS OF CBLOCK AND LBLOCK
02/B1: 8000     	LOAD	A,[CBLOCK]
02/B2: 8401     	LOAD	B,[CBLOCK+1]
02/B3: 8802     	LOAD	C,[LBLOCK]
02/B4: 8C03     	LOAD	D,[LBLOCK+1]
02/B6:          	
02/B5:          	; GRAB THE NEXT ADDRESS OF CBLOCK AND SET IT IN LBLOCK
02/B5: A080     	STORE	[DBANK],A
02/B6: 9100     	LOADF	A,[B]
02/B7: 9501     	LOADF	B,[B+1]
02/B8: A880     	STORE	[DBANK],C
02/B9: B300     	STOREF	[D],A
02/BA: B701     	STOREF	[D+1],B
02/BB: 3000     	LOADI	A,0
02/BC: A080     	STORE	[DBANK],A
02/BE:          	
02/BD:          	; RE-GRAB ADDRESS OF CBLOCK
02/BD: 8000     	LOAD	A,[CBLOCK]
02/BE: 8401     	LOAD	B,[CBLOCK+1]
02/C0:          	
02/BF:          	; SET CBLOCK NEXT TO LBLOCK
02/BF: A080     	STORE	[DBANK],A
02/C0: B900     	STOREF	[B],C
02/C1: BD01     	STOREF	[B+1],D
02/C3:          	
02/C2:          	; SET CBLOCK TO LBLOCK
02/C2: 3000     	LOADI	A,0
02/C3: A080     	STORE	[DBANK],A
02/C4: 8002     	LOAD	A,[LBLOCK]
02/C5: 8403     	LOAD	B,[LBLOCK+1]
02/C6: A000     	STORE	[CBLOCK],A
02/C7: A401     	STORE	[CBLOCK+1],B
02/C9:          	
02/C8:          	; DONE, RESTORE STACK AND RETURN
02/C8: 8C0C     	LOAD	D,[SPARK]
02/C9: FF38     	JUMP	IRET
02/CB:          
02/CA:          
02/CA:          	
02/CA:          	; PLACES THE BLOCK IN [CBLOCK] ONTO THE FREE
02/CA:          	; TABLE
02/CA:          	; USES: A, B, C
02/CA: AC0C     FREE:	STORE	[SPARK],D
02/CC:          
02/CB:          	; GRAB ADDRESSES OF THE FREETAB AND CBLOCK
02/CB: 8000     	LOAD	A,[CBLOCK]
02/CC: 8401     	LOAD	B,[CBLOCK+1]
02/CD: 880D     	LOAD	C,[FREETAB]
02/CE: 8C0E     	LOAD	D,[FREETAB+1]
02/D0:          	
02/CF:          	; LINK REST OF FREETAB AFTER CBLOCK
02/CF: A080     	STORE	[DBANK],A
02/D0: B900     	STOREF	[B],C
02/D1: BD01     	STOREF	[B+1],D
02/D3:          	
02/D2:          	; SET CBLOCK AS BEGINNING OF FREE TABLE
02/D2: 3800     	LOADI	C,0
02/D3: A880     	STORE	[DBANK],C
02/D4: A00D     	STORE	[FREETAB],A
02/D5: A40E     	STORE	[FREETAB+1],B
02/D7:          	
02/D6:          	; RESTORE STACK AND RETURN
02/D6: 8C0C     	LOAD	D,[SPARK]
02/D7: FF2A     	JUMP	IRET
02/D9:          	
02/D8:          	; GETS THE NEXT BLOCK AFTER CBLOCK
02/D8:          	; RETURNS A=0X00 IF NEXT, 0XFF OTHERWISE
02/D8:          	; USES: A, B, C
02/D8: 8400     NEXT:	LOAD	B,[CBLOCK]
02/D9: 8801     	LOAD	C,[CBLOCK+1]
02/DA: 30FF     	LOADI	A,0XFF
02/DB: 5400     	ADDI	B,0
02/DC: F625     	BRZ	IRET
02/DD: A480     	STORE	[DBANK],B
02/DE: 9600     	LOADF	B,[C]
02/DF: 9A01     	LOADF	C,[C+1]
02/E0: 3000     	LOADI	A,0
02/E1: A080     	STORE	[DBANK],A
02/E2: 30FF     	LOADI	A,0XFF
02/E3: 5400     	ADDI	B,0
02/E4: F61D     	BRZ	IRET
02/E5: A400     	STORE	[CBLOCK],B
02/E6: A801     	STORE	[CBLOCK+1],C
02/E7: 3000     	LOADI	A,0
02/E8: FF19     	JUMP	IRET
02/EA:          	
02/E9:          	
02/E9:          	; BANK IS DONE, MOVE ON TO THE NEXT
02/E9:          BI	= BI+1
02/E9:          .TEXT
02/E9:          .BANK	BI
03/80:          FIO0_B	= BI
03/80:          	
03/80:          	; READS A FILE INTO THE BUFFER
03/80:          	; [FNAME] = FILE TO OPEN
03/80:          	; RETURNS A = 0XFF IF ERROR, A = 0X00 RETURN OTHERWISE
03/80:          	; USES: A, B, C
03/80: 7C02     FREAD:	SUBI	D,2
03/82:          
03/81:          	; SET FIRST FLAG
03/81: 380A     	LOADI	C,0X0A
03/82: A809     	STORE	[FIRST],C
03/84:          	
03/83:          	; SET OPEN TARGET
03/83: 3000     	LOADI	A,0
03/84: A06C     	STORE	[ARG_BNK],A
03/85: 3018     	LOADI	A,FNAME
03/87:          	
03/86:          	; ATTEMPT TO OPEN FILE
03/86: 3803     	LOADI	C,BI
03/87: BB01     	STOREF	[D+1],C
03/88: 3406     	LOADI	B,S_OPEN
03/89: 388B     	LOADI	C,@+2
03/8A: FF78     	JUMP	SYSCALL
03/8C:          	
03/8B:          	; CHECK RESULT OF OPERATION
03/8B: 5000     	ADDI	A,0
03/8C: F712     	BRNZ	9F
03/8E:          	
03/8D:          	; RE-INITALIZE MEMORY
03/8D: 3892     	LOADI	C,@+5
03/8E: BB00     	STOREF	[D],C
03/8F: 3402     	LOADI	B,MEM0_B
03/90: 3880     	LOADI	C,MINIT
03/91: FF6F     	JUMP	INDIR
03/93:          	
03/92:          	; MALLOC FIRST BLOCK
03/92: 3000     	LOADI	A,0
03/93: A000     	STORE	[CBLOCK],A
03/94: A00F     	STORE	[LINETAB],A
03/95: A010     	STORE	[LINETAB+1],A
03/96: 300F     	LOADI	A,LINETAB
03/97: A001     	STORE	[CBLOCK+1],A
03/99:          	
03/98:          	; SET RETURN TO FREADA
03/98: 3880     	LOADI	C,FREADA
03/99: BB00     	STOREF	[D],C
03/9A: 3804     	LOADI	C,FIO1_B
03/9B: BB01     	STOREF	[D+1],C
03/9C: 3402     	LOADI	B,MEM0_B
03/9D: 3895     	LOADI	C,MALLOC
03/9E: FF62     	JUMP	INDIR
03/A0:          
03/9F:          	; RESTORE STACK AND RETURN
03/9F: 5C02     9:	ADDI	D,2
03/A0: FF61     	JUMP	IRET
03/A2:          
03/A1:          
03/A1:          	; BANK IS DONE, MOVE ON TO THE NEXT
03/A1:          BI	= BI+1
03/A1:          .TEXT
03/A1:          .BANK	BI
04/80:          FIO1_B	= BI
04/80:          
04/80:          	; CONTINUED FREAD CODE
04/80:          	; RESET BLOCK READ IN STATE
04/80: 3002     FREADA:	LOADI	A,BUF_B
04/81: A06C     	STORE	[ARG_BNK],A
04/82: 3000     	LOADI	A,0
04/84:          	
04/83:          	; READ THE BLOCK
04/83: 3804     	LOADI	C,BI
04/84: BB01     	STOREF	[D+1],C
04/85: A004     0:	STORE	[BLOCK],A
04/86: 3408     	LOADI	B,S_READ
04/87: 3889     	LOADI	C,@+2
04/88: FF7A     	JUMP	SYSCALL
04/8A:          
04/89:          	; DID IT WORK?
04/89: 5000     	ADDI	A,0
04/8A: F74B     	BRNZ	0F
04/8C:          	
04/8B:          	; RESET THE BLOCK READ STATE
04/8B: 3002     	LOADI	A,BUF_B
04/8C: A005     	STORE	[BANK],A
04/8D: 3400     	LOADI	B,0
04/8F:          	
04/8E:          	; CHECK FIRST FLAG
04/8E: 8809     	LOAD	C,[FIRST]
04/8F: A409     	STORE	[FIRST],B
04/90: 7401     	SUBI	B,1
04/91: 5800     	ADDI	C,0
04/92: F706     	BRNZ	2F
04/94:          	
04/93:          	; SET POINTER TO ZERO
04/93: 5401     	ADDI	B,1
04/95:          	
04/94:          	; READ CHARACTER FROM BLOCK
04/94: A005     1:	STORE	[BANK],A
04/95: A080     	STORE	[DBANK],A
04/96: 9900     	LOADF	C,[B]
04/97: 3000     	LOADI	A,0
04/98: A080     	STORE	[DBANK],A
04/9A:          	
04/99:          	; PROCESS CHARACTER
04/99:          	; C = CHARACTER
04/99: A406     2:	STORE	[POINTER],B
04/9A: 3420     	LOADI	B,0X20
04/9B: D900     	CMP	C,B
04/9C: F104     	BRB	3F
04/9D: 3480     	LOADI	B,0X80
04/9E: D900     	CMP	C,B
04/9F: F10A     	BRB	6F
04/A0: FF29     	JUMP	8F
04/A2:          	
04/A1:          	; CONTROL CHARACTERS
04/A1: 3409     3:	LOADI	B,0X09	; TAB CHARACTER
04/A2: D900     	CMP	C,B
04/A3: F606     	BRZ	6F
04/A4: 5800     	ADDI	C,0	; NULL CHARACTER
04/A5: F630     	BRZ	0F
04/A6: 340A     	LOADI	B,0X0A	; NEW LINE CHARACTER
04/A7: D900     	CMP	C,B
04/A8: F721     	BRNZ	8F
04/A9: 3800     	LOADI	C,0
04/AB:          	
04/AA:          	; STORE VALUE IN MEMORY BLOCK
04/AA: 8007     6:	LOAD	A,[MB_PNTR]
04/AB: 8408     	LOAD	B,[MB_END]
04/AC: D100     	CMP	A,B
04/AD: 8400     	LOAD	B,[CBLOCK]
04/AE: F60C     	BRZ	7F
04/AF: A480     	STORE	[DBANK],B
04/B0: B800     	STOREF	[A],C
04/B1: 3400     	LOADI	B,0
04/B2: A480     	STORE	[DBANK],B
04/B3: 5800     	ADDI	C,0
04/B4: F703     	BRNZ	FREADNX		; NOT A NEXT LINE
04/B5: 840B     	LOAD	B,[LINES]	; INCREMENT LINE
04/B6: 5401     	ADDI	B,1
04/B7: A40B     	STORE	[LINES],B
04/B8: 5001     FREADNX:ADDI	A,1
04/B9: A007     	STORE	[MB_PNTR],A
04/BA: FF0F     	JUMP	8F
04/BC:          
04/BB:          	; FINISH BLOCK
04/BB: A80A     7:	STORE	[CHAR],C
04/BC: 8001     	LOAD	A,[CBLOCK+1]
04/BD: 880B     	LOAD	C,[LINES]
04/BE: A480     	STORE	[DBANK],B
04/BF: B802     	STOREF	[A+2],C
04/C0: 3400     	LOADI	B,0
04/C1: A480     	STORE	[DBANK],B
04/C2: A40B     	STORE	[LINES],B
04/C4:          
04/C3:          	; ALLOC ANOTHER BLOCK
04/C3: 38C8     	LOADI	C,@+5
04/C4: BB00     	STOREF	[D],C
04/C5: 3402     	LOADI	B,MEM0_B
04/C6: 3895     	LOADI	C,MALLOC
04/C7: FF39     	JUMP	INDIR
04/C9:          	
04/C8:          	; RESTORE CHARACTER
04/C8: 880A     	LOAD	C,[CHAR]
04/C9: FFE0     	JUMP	6B
04/CB:          	
04/CA:          	; INCREMENT BLOCK POINTER
04/CA: 8005     8:	LOAD	A,[BANK]
04/CB: 8406     	LOAD	B,[POINTER]
04/CC: 5401     	ADDI	B,1
04/CD: F5C6     	BRNN	1B
04/CE: 3400     	LOADI	B,0
04/CF: 5001     	ADDI	A,1
04/D0: 3806     	LOADI	C,BUF_B+4 
04/D1: D200     	CMP	A,C
04/D2: F7C1     	BRNZ	1B
04/D3: 8004     	LOAD	A,[BLOCK]
04/D4: 5001     	ADDI	A,1
04/D5: F7AF     	BRNZ	0B
04/D7:          	
04/D6:          	; TERMINATE THE RECORD
04/D6: AC0C     0:	STORE	[SPARK],D
04/D7: 8007     	LOAD	A,[MB_PNTR]
04/D8: 8408     	LOAD	B,[MB_END]
04/D9: 880B     	LOAD	C,[LINES]
04/DA: 8C00     	LOAD	D,[CBLOCK]
04/DB: AC80     	STORE	[DBANK],D
04/DC: D100     	CMP	A,B
04/DD: F602     	BRZ	1F
04/DE: 3CFF     	LOADI	D,0XFF
04/DF: BC00     	STOREF	[A],D
04/E0: B9E2     1:	STOREF	[B+2-32],C
04/E1: 3C00     	LOADI	D,0
04/E2: AC80     	STORE	[DBANK],D
04/E3: 8C0C     	LOAD	D,[SPARK]
04/E5:          	
04/E4:          	; RESTORE STACK AND RETURN
04/E4: 3000     	LOADI	A,0
04/E5: 3400     9:	LOADI	B,0
04/E6: A480     	STORE	[DBANK],B
04/E7: 5C02     	ADDI	D,2
04/E8: FF19     	JUMP	IRET
04/EA:          	
04/E9:          	; BANK IS DONE, MOVE ON TO THE NEXT
04/E9:          BI	= BI+1
04/E9:          .TEXT
04/E9:          .BANK	BI
05/80:          CMD0_B	= BI
05/80:          
05/80: 3000     ERROR:	LOADI	A,ST_ERR
05/81: FF01     	JUMP	0F
05/83:          
05/82:          	; SEND UP THE COMMAND LINE PROMPT
05/82: 3003     PROMPT:	LOADI	A,ST_PRM
05/83: 3805     0:	LOADI	C,BI
05/84: BB01     	STOREF	[D+1],C
05/85: 3401     	LOADI	B,STR0_B
05/86: A46C     	STORE	[ARG_BNK],B
05/87: 3404     	LOADI	B,S_PUTS
05/88: 388A     	LOADI	C,@+2
05/89: FF79     	JUMP	SYSCALL
05/8B:          	
05/8A:          	; GET USER INPUT
05/8A: 3402     	LOADI	B,BUF_B
05/8B: A46C     	STORE	[ARG_BNK],B
05/8C: 3405     	LOADI	B,S_INPUT
05/8D: 388F     	LOADI	C,@+2
05/8E: FF74     	JUMP	SYSCALL
05/90:          	
05/8F:          	; BEGIN COMMAND PROCESSING
05/8F: 3000     	LOADI	A,0
05/90: A006     	STORE	[POINTER],A
05/91: A011     	STORE	[STATE],A
05/93:          	
05/92:          	; START PROCESSING A NUMBER (MAYBE?)
05/92: 8012     0:	LOAD	A,[ARGA]
05/93: 8413     	LOAD	B,[ARGA+1]
05/94: A014     	STORE	[ARGB],A
05/95: A415     	STORE	[ARGB+1],B
05/96: 3000     	LOADI	A,0
05/97: A012     	STORE	[ARGA],A
05/98: A013     	STORE	[ARGA+1],A
05/9A:          
05/99:          	; READ A CHARACTER FROM THE COMMAND LINE
05/99: 8006     1:	LOAD	A,[POINTER]
05/9A: 3402     	LOADI	B,BUF_B
05/9B: A480     	STORE	[DBANK],B
05/9C: 9800     	LOADF	C,[A]
05/9D: 3400     	LOADI	B,0
05/9E: A480     	STORE	[DBANK],B
05/9F: 5001     	ADDI	A,1
05/A0: A006     	STORE	[POINTER],A
05/A2:          	
05/A1:          	; CHECK CHARACTER
05/A1: 302C     	LOADI	A,','
05/A2: D800     	CMP	C,A
05/A3: F62A     	BRZ	8F
05/A4: 3030     	LOADI	A,'0'
05/A5: D800     	CMP	C,A
05/A6: F1D9     	BRB	ERROR
05/A7: 3039     	LOADI	A,'9'
05/A8: D800     	CMP	C,A
05/A9: F82B     	BRA	9F
05/AB:          	
05/AA:          	; MAKE SURE STATE ISN'T 0
05/AA: 8011     	LOAD	A,[STATE]
05/AB: 5000     	ADDI	A,0
05/AC: F702     	BRNZ	2F
05/AD: 3001     	LOADI	A,1
05/AE: A011     	STORE	[STATE],A
05/B0:          	
05/AF:          	; MULTIPLY ARG A BY 10
05/AF: A80A     2:	STORE	[CHAR],C
05/B0: 3809     	LOADI	C,9
05/B1: 8012     	LOAD	A,[ARGA]
05/B2: 8413     	LOAD	B,[ARGA+1]
05/B3: A016     	STORE	[TEMP],A
05/B4: A417     	STORE	[TEMP+1],B
05/B5: 8017     3:	LOAD	A,[TEMP+1]
05/B6: 8413     	LOAD	B,[ARGA+1]
05/B7: 4400     	ADD	B,A
05/B8: A413     	STORE	[ARGA+1],B
05/B9: 8016     	LOAD	A,[TEMP]
05/BA: 8412     	LOAD	B,[ARGA]
05/BB: F102     	BRNC	4F
05/BC: 5401     	ADDI	B,1
05/BD: F0C2     	BRC	ERROR 
05/BE: 4400     4:	ADD	B,A
05/BF: F0C0     	BRC	ERROR 
05/C0: A412     	STORE	[ARGA],B
05/C1: 7801     	SUBI	C,1
05/C2: F7F2     	BRNZ	3B
05/C3: 880A     	LOAD	C,[CHAR]
05/C5:          	
05/C4:          	; ADD THE NUMBER
05/C4: 7830     	SUBI	C,'0'
05/C5: 8013     	LOAD	A,[ARGA+1]
05/C6: 4200     	ADD	A,C
05/C7: A013     	STORE	[ARGA+1],A
05/C8: F104     	BRNC	5F
05/C9: 8012     	LOAD	A,[ARGA]
05/CA: 5001     	ADDI	A,1
05/CB: A012     	STORE	[ARGA],A
05/CC: F0B3     	BRC	ERROR 
05/CE:          	
05/CD: FFCB     5:	JUMP	1B
05/CF:          	
05/CE:          	; PROCESS COMMA
05/CE: 8011     8:	LOAD	A,[STATE]
05/CF: 7001     	SUBI	A,1
05/D0: F4AF     	BRN	ERROR
05/D1: F7AE     	BRNZ	ERROR
05/D2: 3002     	LOADI	A,2
05/D3: A011     	STORE	[STATE],A
05/D4: FFBD     	JUMP	0B
05/D6:          
05/D5:          	; PROCESS COMMAND
05/D5: 3051     9:	LOADI	A,'Q'
05/D6: D200     	CMP	A,C
05/D7: 3400     	LOADI	B,S_EXIT
05/D8: F62B     	BRZ	SYSJUMP
05/DA:          	
05/D9:          	; MAKE SURE COMMAND IS ZERO TERMINATED
05/D9: 8006     	LOAD	A,[POINTER]
05/DA: 3402     	LOADI	B,BUF_B
05/DB: A480     	STORE	[DBANK],B
05/DC: 9000     	LOADF	A,[A]
05/DD: 3400     	LOADI	B,0
05/DE: A480     	STORE	[DBANK],B
05/DF: 5000     	ADDI	A,0
05/E0: F79F     	BRNZ	ERROR
05/E2:          	
05/E1: 2200     	MOV	A,C
05/E2: 3406     	LOADI	B,CMD1_B
05/E3: 3880     	LOADI	C,PROCMD
05/E4: FF1C     	JUMP	INDIR
05/E6:          	
05/E5:          	; BANK IS DONE, MOVE ON TO THE NEXT
05/E5:          BI	= BI+1
05/E5:          .TEXT
05/E5:          .BANK	BI
06/80:          CMD1_B	= BI
06/80:          
06/80:          	; PROCESS STANDARD COMMAND
06/80: 3450     PROCMD:	LOADI	B,'P'
06/81: D100     	CMP	A,B
06/82: 3407     	LOADI	B,UOP0_B
06/83: 3880     	LOADI	C,PRINTL
06/84: F67C     	BRZ	INDIR
06/86:          	
06/85:          	; CAN'T FIND COMMAND
06/85: 3405     	LOADI	B,CMD0_B
06/86: 3880     	LOADI	C,ERROR
06/87: FF79     	JUMP	INDIR
06/89:          	
06/88:          	; GO BACK TO COMMAND LINE
06/88: 3405     9:	LOADI	B,CMD0_B
06/89: 3882     	LOADI	C,PROMPT
06/8A: FF76     	JUMP	INDIR
06/8C:          
06/8B:          		; BANK IS DONE, MOVE ON TO THE NEXT
06/8B:          BI	= BI+1
06/8B:          	
06/8B:          	; ZERO BANK
06/8B:          .BANK	0
00/80:          .BSS
00/80:          
00/00:          	; CURRENT BLOCK OF MEMORY TO WORK ON
00/00: 0000     .DEFL BYTE CBLOCK	0,0
00/02:          
00/02:          	; BLOCK TO BE LINKED ONTO THE CURRENT BLOCK
00/02: 0000     .DEFL BYTE LBLOCK	0,0
00/04:          
00/04:          	; MISC CONTEXT INFORMATION
00/04: 00       .DEFL BYTE BLOCK	0
00/05: 00       .DEFL BYTE BANK		0
00/06: 00       .DEFL BYTE POINTER	0
00/07:          
00/07:          	; MEMORY BLOCK PARSE INFORMATION
00/07: 00       .DEFL BYTE MB_PNTR	0
00/08: 00       .DEFL BYTE MB_END	0
00/09:          
00/09:          	; FIRST CHARACTER?
00/09: 00       .DEFL BYTE FIRST	0
00/0A:          
00/0A:          	; CURRENT CHARACTER
00/0A: 00       .DEFL BYTE CHAR		0
00/0B:          
00/0B:          	; LINE COUNT
00/0B: 00       .DEFL BYTE LINES	0
00/0C:          
00/0C:          	; STACK PARKING SPACE
00/0C: 00       .DEFL BYTE SPARK	0
00/0D:          
00/0D:          	; FREE MEMORY TABLE
00/0D: 0000     .DEFL BYTE FREETAB	0,0
00/0F:          
00/0F:          	; LINE EDITOR CONTENT
00/0F: 0000     .DEFL BYTE LINETAB	0,0
00/11:          
00/11:          	; STATE FOR COMMAND PROCESSING
00/11: 00       .DEFL BYTE STATE	0
00/12:          
00/12:          	; LINE NUMBER ARGUMENTS FOR COMMANDS
00/12: 0000     .DEFL BYTE ARGA		0,0
00/14: 0000     .DEFL BYTE ARGB		0,0
00/16: 0000     .DEFL BYTE TEMP		0,0
00/18:          
00/18:          	; CURRENT FILE NAME
00/18: 00000000
       00000000 .DEFL BYTE FNAME	0,0,0,0,0,0,0,0,
00/20: 00000000
       00000000 			0,0,0,0,0,0,0,0
00/28:          
00/28:          	; START OF DEFINED MEMORY
00/28:          .BANK	BD
01/00:          .DATA
01/00:          STR0_B = BD	; STRING BANK
01/00:          
01/00:          	; ERROR MESSAGE / PROMPT
01/00: 0A0D3F   .DEFL BYTE ST_ERR	0X0A,0X0D,'?'
01/03: 0A0D2500 .DEFL BYTE ST_PRM	0X0A,0X0D,'%',0
01/07:          
01/07:          	; BANK IS DONE, MOVE ON TO THE NEXT
01/07:          BD	= BD+1
01/07:          BUF_B	= BD	; BUFFER BANK
01/07:          
01/07:          	; BANK IS DONE, MOVE ON TO THE NEXT
01/07:          BD	= BD+4
01/07:          HEAP	= BD	; THE REST OF MEMORY IS HEAP
01/07:          
01/07:          ; ED1.S
01/07:          ; SIMPLE TEXT EDITOR
01/07:          ; USER AND LINE OPERATIONS
01/07:          
01/07:          .TEXT
01/BE:          .BANK	BI
07/80:          UOP0_B	= BI
07/80:          
07/80:          	; PRINT LINES
07/80:          	; REQUESTS 1 OR 2 ARGUMENTS
07/80: 8011     PRINTL:	LOAD	A,[STATE]
07/81: 7001     	SUBI	A,1
07/82: F459     	BRN	9F
07/83: F704     	BRNZ	1F
07/85:          	
07/84:          	; SET ARGB = ARGA
07/84: 8012     	LOAD	A,[ARGA]
07/85: 8413     	LOAD	B,[ARGA+1]
07/86: A014     	STORE	[ARGB],A
07/87: A415     	STORE	[ARGB+1],B
07/89:          	
07/88:          	; ENSURE ARGA >= ARGB
07/88: 8012     1:	LOAD	A,[ARGA]
07/89: 8414     	LOAD	B,[ARGB]
07/8A: D100     	CMP	A,B
07/8B: F150     	BRB	9F
07/8C: F804     	BRA	2F
07/8D: 8013     	LOAD	A,[ARGA+1]
07/8E: 8415     	LOAD	B,[ARGB+1]
07/8F: D100     	CMP	A,B
07/90: F14B     	BRB	9F
07/92:          	
07/91:          	; START LOOKING FOR THE LINE
07/91:          	; TEMP = ARGB
07/91: 8014     2:	LOAD	A,[ARGB]
07/92: 8415     	LOAD	B,[ARGB+1]
07/93: A016     	STORE	[TEMP],A
07/94: A417     	STORE	[TEMP+1],B
07/95: 3807     	LOADI	C,BI
07/96: BB01     	STOREF	[D+1],C
07/97: 389C     	LOADI	C,@+5
07/98: BB00     	STOREF	[D],C
07/99: 3408     	LOADI	B,LOP0_B
07/9A: 3880     	LOADI	C,FINDL
07/9B: FF65     	JUMP	INDIR
07/9C: 5000     	ADDI	A,0
07/9D: F73E     	BRNZ	9F
07/9F:          	
07/9E:          	; START PRINTING OFF LINES
07/9E: 8800     3:	LOAD	C,[CBLOCK]
07/9F: 8407     	LOAD	B,[MB_PNTR]
07/A0: A880     	STORE	[DBANK],C
07/A1: 9100     	LOADF	A,[B]
07/A2: 3800     	LOADI	C,0
07/A3: A880     	STORE	[DBANK],C
07/A5:          	
07/A4:          	; IS IT ZERO OR NEGATIVE
07/A4: 5000     	ADDI	A,0
07/A5: F40A     	BRN	5F
07/A6: F619     	BRZ	6F
07/A8:          	
07/A7:          	; PRINT THE CHARACTER
07/A7: 3401     	LOADI	B,S_PUTC
07/A8: 38AA     	LOADI	C,@+2
07/A9: FF59     	JUMP	SYSCALL
07/AB:          	
07/AA:          	; NEXT CHARACTER
07/AA: 8407     4:	LOAD	B,[MB_PNTR]
07/AB: 8808     	LOAD	C,[MB_END]
07/AC: 5401     	ADDI	B,1
07/AD: A407     	STORE	[MB_PNTR],B
07/AE: D600     	CMP	B,C
07/AF: F7EE     	BRNZ	3B
07/B1:          	
07/B0:          	; NEXT BLOCK
07/B0: 8000     5:	LOAD	A,[CBLOCK]
07/B1: 8401     	LOAD	B,[CBLOCK+1]
07/B2: A080     	STORE	[DBANK],A
07/B3: 9100     	LOADF	A,[B]
07/B4: 9501     	LOADF	B,[B+1]
07/B5: 3800     	LOADI	C,0
07/B6: A880     	STORE	[DBANK],C
07/B8:          	
07/B7:          	; MAKE SURE IT ISN'T ZERO
07/B7: 5000     	ADDI	A,0
07/B8: F620     	BRZ	8F
07/BA:          	
07/B9:          	; SET CBLOCK AND UPDATE MB_PNTR AND MB_END
07/B9: A000     	STORE	[CBLOCK],A
07/BA: A401     	STORE	[CBLOCK+1],B
07/BB: 5403     	ADDI	B,3
07/BC: A407     	STORE	[MB_PNTR],B
07/BD: 541D     	ADDI	B,32-3
07/BE: A408     	STORE	[MB_END],B
07/BF: FFDE     	JUMP	3B
07/C1:          
07/C0:          	; START OF A NEW LINE
07/C0:          	; ENSURE ARGA >= ARGB
07/C0: 8012     6:	LOAD	A,[ARGA]
07/C1: 8414     	LOAD	B,[ARGB]
07/C2: D100     	CMP	A,B
07/C3: F115     	BRB	8F
07/C4: F804     	BRA	7F
07/C5: 8013     	LOAD	A,[ARGA+1]
07/C6: 8415     	LOAD	B,[ARGB+1]
07/C7: D100     	CMP	A,B
07/C8: F110     	BRB	8F
07/CA:          	
07/C9:          	; INCREMENT ARGB
07/C9: 8015     7:	LOAD	A,[ARGB+1]
07/CA: 5001     	ADDI	A,1
07/CB: A015     	STORE	[ARGB+1],A
07/CC: F104     	BRNC	7F
07/CD: 8014     	LOAD	A,[ARGB]
07/CE: 5001     	ADDI	A,1
07/CF: A014     	STORE	[ARGB],A
07/D0: F008     	BRC	8F
07/D2:          	
07/D1:          	; PRINT CR/LF
07/D1: 300A     7:	LOADI	A,0X0A
07/D2: 3401     	LOADI	B,S_PUTC
07/D3: 38D5     	LOADI	C,@+2
07/D4: FF2E     	JUMP	SYSCALL
07/D5: 300D     	LOADI	A,0X0D
07/D6: 3401     	LOADI	B,S_PUTC
07/D7: 38AA     	LOADI	C,4B
07/D8: FF2A     	JUMP	SYSCALL
07/DA:          	
07/D9:          	; GO BACK TO COMMAND LINE
07/D9: 3405     8:	LOADI	B,CMD0_B
07/DA: 3882     	LOADI	C,PROMPT
07/DB: FF25     	JUMP	INDIR
07/DD:          	
07/DC:          	; ERROR
07/DC: 3405     9:	LOADI	B,CMD0_B
07/DD: 3880     	LOADI	C,ERROR
07/DE: FF22     	JUMP	INDIR
07/E0:          	
07/DF:          	; BANK IS DONE, MOVE ON TO THE NEXT
07/DF:          BI	= BI+1
07/DF:          .TEXT
07/DF:          .BANK	BI
08/80:          LOP0_B	= BI
08/80:          
08/80:          	; SETS [CBLOCK] AND [MB_PNTR] TO THE START OF A LINE
08/80:          	; [MB_END] WILL ALSO BE UPDATED FOR THE CURRENT BLOCK
08/80:          	; [TEMP] = LINE NUMBER TO FIND
08/80:          	; RETURNS A=0X00 IF FOUND, 0XFF OTHERWISE
08/80:          	; USES: A, B, C
08/80: 840F     FINDL:	LOAD	B,[LINETAB]
08/81: 8810     	LOAD	C,[LINETAB+1]
08/83:          	
08/82: A400     0:	STORE	[CBLOCK],B
08/83: A801     	STORE	[CBLOCK+1],C
08/84: 30FF     	LOADI	A,0XFF
08/85: 5400     	ADDI	B,0
08/86: F67B     	BRZ	IRET
08/87: A480     	STORE	[DBANK],B
08/88: 9602     	LOADF	B,[C+2]
08/89: 3000     	LOADI	A,0
08/8A: A080     	STORE	[DBANK],A
08/8B: 5400     	ADDI	B,0
08/8C: F60D     	BRZ	2F
08/8E:          	
08/8D:          	; B = LINES ON BLOCK
08/8D: 8017     	LOAD	A,[TEMP+1]
08/8E: 2800     	MOV	C,A
08/8F: 6100     	SUB	A,B
08/90: A017     	STORE	[TEMP+1],A
08/91: 8416     	LOAD	B,[TEMP]
08/92: F003     	BRC	1F
08/93: 7401     	SUBI	B,1
08/94: A416     	STORE	[TEMP],B
08/95: F10C     	BRNC	3F
08/97:          	
08/96:          	; SEE IF TEMP = 0
08/96: 5000     1:	ADDI	A,0
08/97: F702     	BRNZ	2F
08/98: 5400     	ADDI	B,0
08/99: F608     	BRZ	3F
08/9B:          	
08/9A:          	; NEXT BLOCK
08/9A: 8400     2:	LOAD	B,[CBLOCK]
08/9B: 8801     	LOAD	C,[CBLOCK+1]
08/9C: A480     	STORE	[DBANK],B
08/9D: 9600     	LOADF	B,[C]
08/9E: 9A01     	LOADF	C,[C+1]
08/9F: 3000     	LOADI	A,0
08/A0: A080     	STORE	[DBANK],A
08/A1: FFE0     	JUMP	0B
08/A3:          
08/A2:          	; FOUND BLOCK, FIND THE START OF LINE
08/A2:          	; C = OFFSET
08/A2: 8000     3:	LOAD	A,[CBLOCK]
08/A3: 8401     	LOAD	B,[CBLOCK+1]
08/A4: 5420     	ADDI	B,32
08/A5: A408     	STORE	[MB_END],B
08/A6: 8401     	LOAD	B,[CBLOCK+1]
08/A7: 5403     	ADDI	B,3
08/A8: A080     	STORE	[DBANK],A
08/A9: 9100     4:	LOADF	A,[B]
08/AA: 5000     	ADDI	A,0
08/AB: F602     	BRZ	6F
08/AC: 5401     5:	ADDI	B,1
08/AD: FFFB     	JUMP	4B
08/AF:          	
08/AE:          	; DECREMENT AND MAYBE RETURN
08/AE: 7801     6:	SUBI	C,1
08/AF: F105     	BRNC	7F
08/B0: F7FB     	BRNZ	5B
08/B1: 3000     	LOADI	A,0
08/B2: A080     	STORE	[DBANK],A
08/B3: A407     	STORE	[MB_PNTR],B
08/B4: FF4D     	JUMP	IRET
08/B6:          	
08/B5:          	; ERROR!
08/B5: 3000     7:	LOADI	A,0
08/B6: A080     	STORE	[DBANK],A
08/B7: 30FF     	LOADI	A,0XFF
08/B8: FF49     	JUMP	IRET
08/BA:          
08/B9:          	; BANK IS DONE, MOVE ON TO THE NEXT
08/B9:          BI	= BI+1
