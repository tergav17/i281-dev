; AS4.S
; ASSEMBLER SYMBOL MANAGEMENT
; GAVIN TERSTEEG, 2024
; SDMAY24-14

BI	= BI+1
.TEXT
.BANK	BI
SYM0_B	= BI

	; DEFINES OR UPDATES A SYMBOL IN THE TABLE
	; [TBUF] = SYMBOL NAME
	; RETURNS POINTER TO THE SYMBOL VALUE IN [DFBANK] AND [DFPNTR]
DEFINE:	LOADI	B,SYM1_B
	LOADI	C,DEFINEA
	JUMP	INDIR
	
	; ADDS A NEW LOCAL SYMBOL TO THE TABLE
	; A = SYMBOL NAME
	; [VALUE] = SYMBOL VALUE
ADDLOC:	LOADI	B,SYM2_B
	LOADI	C,ADDLOCA
	JUMP	INDIR
	
	; GET A SYMBOL VALUE
	; [TBUF] = SYMBOL NAME
	; RETURNS VALUE OF SYMBOL IN [VALUE]
	; A = 0X00 IF DEFINE, 0XFF IF UNDEFINED 
GETSYM:STORE	[SPARK],D

	; START BY SEARCHING THE SYMBOL TABLE FOR EXISTING SYMBOLS
	LOADI	B,STAB_B
0:	LOADI	D,ST_NAME
	STORE	[TEMP],B
	
	; DO NAME CHECK
1:	LOADI	C,0-8

	; COMPARE TBUF AND CURRENT SYMBOL
2:	ADDI	C,8
	LOADF	A,[C+TBUF]
	LOAD	B,[TEMP]
	STORE	[DBANK],B
	LOADF	B,[D]
	CMP	A,B
	BRNZ	3F
	ADDI	A,0
	BRZ	2F
	LOADI 	A,0
	STORE	[DBANK],A
	ADDI	D,1
	SUBI	C,7
	BRNZ	2B
	LOADI	C,8
	
	; STRING TERMINATES, FIX POINTER AND RETURN
2:	SUB	D,C
	ADDI	D,8
	
	; FOUND THE SYMBOL, GRAB THE VALUE
	LOADF	A,[D+1]
	LOADF	B,[D+2]
	LOADF	D,[D]
	LOADI	C,0
	STORE	[DBANK],C
	STORE	[VALUE],A
	STORE	[VALUE+1],B
	LOADI	A,0XFF
	SUB	A,D
	LOAD	D,[SPARK]
	JUMP	IRET

	; SYMBOL DOES NOT MATCH
3:	SUB	D,C
	LOADF	A,[D]
	ADDI	A,0
	BRZ	5F
	
	; EXIT SYMBOL ENTRY
	ADDI	D,ST_SIZE*2
	BRN	4F
	SUBI	D,ST_SIZE
	LOADI	A,0
	STORE	[DBANK],A
	JUMP	1B

	; NEXT BANK
4:	LOAD	A,[ST_NEXT]
	ADDI	A,0
	LOADI	C,0
	STORE	[DBANK],C
	BRZ	5F
	STORE	[TEMP],A
	LOADI	D,ST_NAME
	JUMP	1B

	; CANNOT FIND SYMBOL!
5:	LOADI	C,0
	STORE	[DBANK],C
	LOAD	D,[SPARK]
	LOADI	A,0XFF
	JUMP	IRET


BI	= BI+1
.TEXT
.BANK	BI
SYM1_B	= BI

	; SHADOW OF DEFINE
DEFINEA:STORE	[SPARK],D

	; START BY SEARCHING THE SYMBOL TABLE FOR EXISTING SYMBOLS
	LOADI	B,STAB_B
0:	LOADI	D,ST_NAME
	STORE	[DFBANK],B
	
	; DO NAME CHECK
1:	LOADI	C,0-8

	; COMPARE TBUF AND CURRENT SYMBOL
2:	ADDI	C,8
	LOADF	A,[C+TBUF]
	LOAD	B,[DFBANK]
	STORE	[DBANK],B
	LOADF	B,[D]
	CMP	A,B
	BRNZ	3F
	ADDI	A,0
	BRZ	2F
	LOADI 	A,0
	STORE	[DBANK],A
	ADDI	D,1
	SUBI	C,7
	BRNZ	2B
	LOADI	C,8
	
	; STRING TERMINATES, FIX POINTER AND RETURN
2:	SUB	D,C
	ADDI	D,8
	
	; FOUND THE SYMBOL, SAVE POINTER
9:	LOADI	A,0
	STORE	[DBANK],A
	STORE	[DFPNTR],D
	LOAD	D,[SPARK]
	JUMP	IRET

	; SYMBOL DOES NOT MATCH
3:	SUB	D,C
	LOADF	A,[D]
	ADDI	A,0
	BRZ	0F
	
	; EXIT SYMBOL ENTRY
	ADDI	D,ST_SIZE*2
	BRN	4F
	SUBI	D,ST_SIZE
	LOADI	A,0
	STORE	[DBANK],A
	JUMP	1B

	; NEXT BANK
4:	LOAD	A,[ST_NEXT]
	ADDI	A,0
	LOADI	C,0
	STORE	[DBANK],C
	BRZ	5F
	STORE	[DFBANK],A
	LOADI	D,ST_NAME
	JUMP	1B
	
	; ALLOCATE A NEW BANK
5:	LOAD	B,[NFREE]
	LOAD	A,[MAX_DB]
	CMP	B,A
	BRBE	6F
	
	; ERROR! OUT OF MEMORY
	LOADI	A,E_OMEM
	LOADI	B,CORE0_B
	LOADI	C,ERROR
	JUMP	INDIR
	
	; SETUP NEW BANK 
6:	ADDI	B,1
	STORE	[NFREE],B
	SUBI	B,1
	LOAD	A,[DFBANK]
	STORE	[DBANK],A
	STORE	[ST_NEXT],B
	STORE	[DBANK],B
	
	LOADI	A,0
	LOADI	C,ST_NAME
	STORE	[ST_NEXT],A
7:	STOREF	[C+(ST_NAME-ST_NAME)],A
	STOREF	[C+(ST_DEF-ST_NAME)],A
	ADDI	C,ST_SIZE
	BRNN	7B
	
	STORE	[DBANK],A
	JUMP	0B
	
	; EMPTY SLOT, PUT THE SYMBOL HERE
0:	LOADI	C,8
	ADDI	D,8
1:	LOADI	B,0
	STORE	[DBANK],B
	SUBI	D,1
	SUBI	C,1
	BRNC	2F
	LOADF	A,[C+TBUF]
	LOAD	B,[DFBANK]
	STORE	[DBANK],B
	STOREF	[D],A
	JUMP	1B

	; GO PLACE THE POINTER
2:	ADDI	D,9
	JUMP	9B
	
BI	= BI+1
.TEXT
.BANK	BI
SYM2_B	= BI

	; SHADOW OF ADDLOC
	; FIND THE END OF THE TABLE
ADDLOCA:LOADI	C,LTAB_B
0:	STORE	[DBANK],C
	LOAD	C,[LT_NEXT]
	ADDI	C,0
	BRNZ	0B
	
	; IS THERE SPACE TO STORE A NEW LOCAL HERE?
	LOAD	B,[LT_FREE]
	ADDI	B,LT_SIZE-1
	BRNN	2F
	
	; ALLOCATE A NEW BANK
	LOADI	B,0
	STORE	[DBANK],B
	STORE	[SPARK],D
	LOAD	B,[NFREE]
	LOAD	D,[MAX_DB]
	CMP	B,D
	LOAD	D,[SPARK]
	BRBE	1F
	
	; ERROR! OUT OF MEMORY
	LOADI	A,E_OMEM
	LOADI	B,CORE0_B
	LOADI	C,ERROR
	JUMP	INDIR
	
	; LINK AND SETUP
1:	ADDI	B,1
	STORE	[NFREE],B
	STORE	[DBANK],C
	STORE	[LT_NEXT],B
	

	; THERE IS SPACE, LETS USE IT
2:	SUBI	B,LT_SIZE-1
	STOREF	[B],A
	LOADI	A,0
	STORE	[DBANK],A
	LOAD	A,[VALUE+1]
	STORE	[DBANK],C
	STOREF	[B+1],A
	LOADI	A,0
	STORE	[DBANK],A
	JUMP	IRET

	; START OF SYMBOL TABLE
.BANK	BD
.DATA
STAB_B	= BD

	; SYMBOL TABLE DEFINITION
	; 0X00     : NEXT BANK
	; 0X01-0X08: SYMBOL 0 NAME
	; 0X09     : SYMBOL 0 DEFINED
	; 0X0A-0X0B: SYMBOL 0 VALUE
	; 0X0C-0X14: SYMBOL 1 NAME
	; 0X15     : SYMBOL 1 DEFINED
	; 0X16-0X17: SYMBOL 1 VALUE
	
	; IF A SYMBOL NAME STARTS WITH 0X00, IT IS UNALLOCATED

.DEFL BYTE ST_NEXT	0
.DEFL BYTE ST_NAME	0,0,0,0,0,0,0,0
.DEFL BYTE ST_DEF	0
.DEFL BYTE ST_VAL	0,0
ST_SIZE	= @-ST_NAME

BD	= BD+1

	; START OF LOCAL TABLE
.BANK	BD
.DATA
LTAB_B	= BD

	; LOCAL TABLE DEFINITION
	; 0X00: NEXT BANK
	; 0X01: NEXT FREE SYMBOL
	; 0X02: SYMBOL 0 NAME
	; 0X03: SYMBOL 0 VALUE
	; 0X04: SYMBOL 1 NAME
	; 0X05: SYMBOL 1 VALUE
	
.DEFL BYTE LT_NEXT	0
.DEFL BYTE LT_FREE	0
.DEFL BYTE LT_NAME	0
.DEFL BYTE LT_VAL	0
LT_SIZE = @-LT_NAME
	

BD	= BD+1

	; SET START OF HEAP
HEAP	= BD