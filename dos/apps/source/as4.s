; AS4.S
; ASSEMBLER SYMBOL MANAGEMENT
; GAVIN TERSTEEG, 2024
; SDMAY24-14

BI	= BI+1
.TEXT
.BANK	BI
SYM0_B	= BI

	; DEFINES OR UPDATES A SYMBOL IN THE TABLE
	; [TBUF] = SYMBOL NAME
	; RETURNS POINTER TO THE SYMBOL VALUE IN [DFBANK] AND [DFPNTR]
DEFINE:	LOADI	B,SYM1_B
	LOADI	C,DEFINEA
	JUMP	INDIR
	
	; GET A SYMBOL VALUE
	; [TBUF] = SYMBOL NAME
	; RETURNS VALUE OF SYMBOL IN [VALUE]
	; A = 0X00 IF DEFINE, 0XFF IF UNDEFINED 
GETSYM:STORE	[SPARK],D

	; START BY SEARCHING THE SYMBOL TABLE FOR EXISTING SYMBOLS
	LOADI	B,STAB_B
0:	LOADI	D,ST_NAME
	STORE	[TEMP],B
	
	; DO NAME CHECK
1:	LOADI	C,0-8

	; COMPARE TBUF AND CURRENT SYMBOL
2:	ADDI	C,8
	LOADF	A,[C+TBUF]
	LOAD	B,[TEMP]
	STORE	[DBANK],B
	LOADF	B,[D]
	CMP	A,B
	BRNZ	3F
	ADDI	A,0
	BRZ	2F
	LOADI 	A,0
	STORE	[DBANK],A
	ADDI	D,1
	SUBI	C,7
	BRNZ	2B
	LOADI	C,8
	
	; STRING TERMINATES, FIX POINTER AND RETURN
2:	SUB	D,C
	ADDI	D,8
	
	; FOUND THE SYMBOL, GRAB THE VALUE
	LOADF	A,[D+1]
	LOADF	B,[D+2]
	LOADF	D,[D]
	LOADI	C,0
	STORE	[DBANK],C
	STORE	[VALUE],A
	STORE	[VALUE+1],B
	LOADI	A,0XFF
	SUB	A,D
	LOAD	D,[SPARK]
	JUMP	IRET

	; SYMBOL DOES NOT MATCH
3:	SUB	D,C
	LOADF	A,[D]
	ADDI	A,0
	BRZ	5F
	
	; EXIT SYMBOL ENTRY
	ADDI	D,ST_SIZE*2
	BRN	4F
	SUBI	D,ST_SIZE
	LOADI	A,0
	STORE	[DBANK],A
	JUMP	1B

	; NEXT BANK
4:	LOAD	A,[ST_NEXT]
	ADDI	A,0
	LOADI	C,0
	STORE	[DBANK],C
	BRZ	5F
	STORE	[TEMP],A
	LOADI	D,ST_NAME
	JUMP	1B

	; CANNOT FIND SYMBOL!
5:	LOADI	C,0
	STORE	[DBANK],C
	LOAD	D,[SPARK]
	LOADI	A,0XFF
	JUMP	IRET


BI	= BI+1
.TEXT
.BANK	BI
SYM1_B	= BI

	; SHADOW OF DEFINE
DEFINEA:STORE	[SPARK],D

	; START BY SEARCHING THE SYMBOL TABLE FOR EXISTING SYMBOLS
	LOADI	B,STAB_B
0:	LOADI	D,ST_NAME
	STORE	[DFBANK],B
	
	; DO NAME CHECK
1:	LOADI	C,0-8

	; COMPARE TBUF AND CURRENT SYMBOL
2:	ADDI	C,8
	LOADF	A,[C+TBUF]
	LOAD	B,[DFBANK]
	STORE	[DBANK],B
	LOADF	B,[D]
	CMP	A,B
	BRNZ	3F
	ADDI	A,0
	BRZ	2F
	LOADI 	A,0
	STORE	[DBANK],A
	ADDI	D,1
	SUBI	C,7
	BRNZ	2B
	LOADI	C,8
	
	; STRING TERMINATES, FIX POINTER AND RETURN
2:	SUB	D,C
	ADDI	D,8
	
	; FOUND THE SYMBOL, SAVE POINTER
9:	LOADI	A,0
	STORE	[DBANK],A
	STORE	[DFPNTR],D
	LOAD	D,[SPARK]
	JUMP	IRET

	; SYMBOL DOES NOT MATCH
3:	SUB	D,C
	LOADF	A,[D]
	ADDI	A,0
	BRZ	0F
	
	; EXIT SYMBOL ENTRY
	ADDI	D,ST_SIZE*2
	BRN	4F
	SUBI	D,ST_SIZE
	LOADI	A,0
	STORE	[DBANK],A
	JUMP	1B

	; NEXT BANK
4:	LOAD	A,[ST_NEXT]
	ADDI	A,0
	LOADI	C,0
	STORE	[DBANK],C
	BRZ	5F
	STORE	[DFBANK],A
	LOADI	D,ST_NAME
	JUMP	1B
	
	; ALLOCATE A NEW BANK
5:	LOAD	B,[NFREE]
	LOAD	A,[MAX_DB]
	CMP	B,A
	BRBE	6F
	
	; ERROR! OUT OF MEMORY
	LOADI	A,E_OMEM
	LOADI	B,CORE0_B
	LOADI	C,ERROR
	JUMP	INDIR
	
	; SETUP NEW BANK 
6:	ADDI	B,1
	STORE	[NFREE],B
	SUBI	B,1
	LOAD	A,[DFBANK]
	STORE	[DBANK],A
	STORE	[ST_NEXT],B
	STORE	[DBANK],B
	
	LOADI	A,0
	LOADI	C,ST_NAME
	STORE	[ST_NEXT],A
7:	STOREF	[C+(ST_NAME-ST_NAME)],A
	STOREF	[C+(ST_DEF-ST_NAME)],A
	ADDI	C,ST_SIZE
	BRNN	7B
	
	STORE	[DBANK],A
	JUMP	0B
	
	; EMPTY SLOT, PUT THE SYMBOL HERE
0:	LOADI	C,8
	ADDI	D,8
1:	LOADI	B,0
	STORE	[DBANK],B
	SUBI	D,1
	SUBI	C,1
	BRNC	2F
	LOADF	A,[C+TBUF]
	LOAD	B,[DFBANK]
	STORE	[DBANK],B
	STOREF	[D],A
	JUMP	1B

	; GO PLACE THE POINTER
2:	ADDI	D,9
	JUMP	9B

	; START OF SYMBOL TABLE
.BANK	BD
.DATA
STAB_B	= BD

	; SYMBOL TABLE DEFINITION
	; 0X00: NEXT BANK
	; 0X01-0X08: SYMBOL 0 NAME
	; 0X09-0X0A: SYMBOL 1 VALUE
	; 0X0B-0X13: SYMBOL 2 NAME
	; 0X14-0X15: SYMBOL 2 VALUE
	
	; IF A SYMBOL NAME STARTS WITH 0X00, IT IS UNALLOCATED

.DEFL BYTE ST_NEXT	0
.DEFL BYTE ST_NAME	0,0,0,0,0,0,0,0
.DEFL BYTE ST_DEF	0
.DEFL BYTE ST_VAL	0,0
ST_SIZE	= @-ST_NAME

BD	= BD+1

	; SET START OF HEAP
HEAP	= BD