; CD.S
; CHANGE DEFAULT USER AREA

	; START BY PROCESSING THE ARGUMENTS
START:	LOADI	A,0
	STORE	[DBANK],A
	
	; SET UP STACK
	LOADI	D,0X60-2
	
	; SYSCALL RETURN BANK
	LOADI	C,1	
	STOREF	[D+1],C
	
	; GO TO ARGUMENT BANK
	LOAD	A,[CMDL_B]
	STORE	[ARG_BNK],A
	STORE	[DBANK],A
	
	; SET UP POINTER
	LOADI	C,0
	
	; SKIP THE INITIAL COMMAND
0:	LOADF	A,[C]
	LOADI	B,0X20
	CMP	A,B
	BRBE	1F
	ADDI	C,1
	JUMP	0B

	; LOOK FOR THE FIRST ARGUMENT
1:	LOADF	A,[C]
	ADDI	A,0
	BRZ	2F
	CMP	A,B
	BRA	PROCARG
	ADDI	C,1
	JUMP	1B
	
	; NO ARGUMENT, ERROR
2:	LOADI	A,0
	STORE	[DBANK],A
	
	LOADI	A,1
	STORE	[ARG_BNK],A
	LOADI	A,ERROR0
	
	; PRINT ERROR MESSAGE
	LOADI	B,S_PUTS
	LOADI	C,EXIT
	JUMP	SYSCALL
	
	; PROCESS THE ARGUMENT
PROCARG:LOADI	B,'0'
	CMP	A,B
	BRB	INVALID
	LOADI	B,'9'
	CMP	A,B
	BRA	INVALID
	
	; CHECK ENDING
	LOADF	A,[C+1]
	LOADI	B,':'
	CMP	A,B
	BRNZ	INVALID
	LOADF	A,[C+2]
	LOADI	B,0X20
	CMP	A,B
	BRA	INVALID
	
	; STORE DEFAULT USER AREA
	LOADF	A,[C]
	LOADI	B,0
	STORE	[DBANK],B
	STORE	[DFT_USR],A
	JUMP	EXIT

	; INVALID USER AREA
INVALID:LOADI	A,0
	STORE	[DBANK],A
	
	LOADI	A,1
	STORE	[ARG_BNK],A
	LOADI	A,ERROR1
	
	; PRINT ERROR MESSAGE
	LOADI	B,S_PUTS
	LOADI	C,EXIT
	JUMP	SYSCALL

	; EXIT PROGRAM
EXIT:	LOADI	B,0
	JUMP	SYSJUMP

.DATA

	; ERROR MESSAGES
.DEFL BYTE ERROR0	"NO ARGUMENT",0X0A,0X0D,0
.DEFL BYTE ERROR1	"INVALID USER AREA",0X0A,0X0D,0