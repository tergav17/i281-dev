; RM.S
; REMOVE A FILE

	; START BY PROCESSING THE ARGUMENTS
START:	LOADI	A,0
	STORE	[DBANK],A
	
	; SET UP STACK
	LOADI	D,0X60-2
	
	; SET FIRST FLAG
	LOADI	A,0XFF
	STORE	[FIRST],A 
	
	; SYSCALL RETURN BANK
	LOADI	C,1	
	STOREF	[D+1],C
	
	; GO TO ARGUMENT BANK
	LOAD	A,[CMDL_B]
	STORE	[DBANK],A
	
	; SET UP POINTER
	LOADI	C,0
	
	; SKIP THE INITIAL COMMAND
0:	LOADF	A,[C]
	LOADI	B,0X20
	CMP	A,B
	BRBE	1F
	ADDI	C,1
	JUMP	0B

	; LOOK FOR THE FIRST ARGUMENT
1:	LOADF	A,[C]
	ADDI	A,0
	BRZ	2F
	CMP	A,B
	BRA	RMARG
	ADDI	C,1
	JUMP	1B
	
	; NO ARGUMENT, ERROR
2:	LOADI	A,0
	STORE	[DBANK],A
	
	LOADI	A,1
	STORE	[ARG_BNK],A
	LOADI	A,ERROR0
	
	; PRINT ERROR MESSAGE
	LOADI	B,S_PUTS
	LOADI	C,EXIT
	JUMP	SYSCALL

	
	; THERE IS AN ARG, TRY AND OPEN IT
RMARG:	LOADI	A,0
	STORE	[DBANK],A
	
	; C = FILE STRING START
	STORE	[POINTER],C
	
	; SET ARGUMENTS
0:	LOAD	A,[CMDL_B]
	STORE	[ARG_BNK],A
	LOAD	A,[POINTER]
	
	; OPEN FILE
	LOADI	B,S_OPEN
	LOADI	C,@+2
	JUMP	SYSCALL
	
	; CHECK FOR ERRORS
	ADDI	A,0
	BRNZ	1F
	
	; REMOVE FILE
	LOADI	B,S_DELET
	LOADI	C,@+2
	JUMP	SYSCALL

	; CHECK FOR ERRORS
	ADDI	A,0
	BRZ	2F
	
	; IS THIS THE FIRST?
1:	LOAD	A,[FIRST]
	ADDI	A,0
	BRZ	EXIT
	
	; ERROR MESSAGE ARGS
	LOADI	A,1
	STORE	[ARG_BNK],A
	LOADI	A,ERROR1
	
	; PRINT ERROR MESSAGE
	LOADI	B,S_PUTS
	LOADI	C,EXIT
	JUMP	SYSCALL
	
	; RESET FIRST FLAG
2:	LOADI	A,0
	STORE	[FIRST],A
	JUMP	0B

	; EXIT PROGRAM
EXIT:	LOADI	B,0
	JUMP	SYSJUMP


.DATA

	; ERROR MESSAGES
.DEFL BYTE ERROR0	"NO ARGUMENT",0X0A,0X0D,0
.DEFL BYTE ERROR1	"CAN'T REMOVE FILE",0X0A,0X0D,0

.BANK	0
.BSS

	; IS THIS THE FIRST ATTEMPT AT REMOVING?
.DEFL BYTE FIRST	0

	; POINTER TO ARGUMENT
.DEFL BYTE POINTER	0
