; AS2.S
; EXPRESSION HANDLING
; GAVIN TERSTEEG, 2024
; SDMAY24-14

BI	= BI+1
.TEXT
.BANK	BI
EXP0_B	= BI

	; PARSE A NUMERIC VALUE
	; IF THE PARSING IS SUCCESSFUL, THE RESULT WILL
	; BE PLACED IN [VALUE]
	; DEFAULT RADIX IS 10
PARSEN:	LOADI	A,10
	STORE	[RADIX],A
	
	; RESET VALUE AS WELL
	LOADI	A,0
	STORE	[VALUE],A
	STORE	[VALUE+1],A
	
	; RESET BUFFER POINTER
	LOADI	A,TBUF
	
	; DETECT LEADING ZERO
	LOADF	B,[A]
	ADDI	B,0
	BRZ	IRET
	SUBI	B,'0'
	BRNZ	1F
	
	; SET RADIX TO 8
	LOADI	B,8
	STORE	[RADIX],B
	
	; GET NEXT CHARACTER, COMPARE WITH 9
	ADDI	A,1
	LOADF	B,[A]
	LOADI	C,'9'
	CMP	B,C
	BRBE	1F
	
	; RADIX FORMAT IS "0?..."
	ADDI	A,1
	JUMP	4F
	
	; CHECK END OF NUMBER FOR RADIX
1:	MOV	C,A
2:	LOADF	B,[C]
	ADDI	B,0
	BRZ	3F
	ADDI	C,1
	JUMP	2B
	
	; LOOK AT LAST CHARACTER
3:	LOADF	B,[C+0-1]
	SUBI	B,'9'
	BRBE	0F
	ADDI	B,'9'
	STORE	[BUFPNTR],A
	LOADI	A,0
	STOREF	[C+0-1],A
	LOAD	A,[BUFPNTR]

	; B = POTENTIAL RADIX
4:	LOADI	C,16
	SUBI	B,'X'
	BRZ	5F
	SUBI	B,'H'-'X'
	BRZ	5F
	LOADI	C,8
	SUBI	B,'O'-'H'
	BRZ	5F
	LOADI	C,2
	SUBI	B,'B'-'O'
	BRZ	5F
	
	; ISSUE WITH PARSING NUMERIC
9:	LOADI	A,0X81
	LOADI	B,CORE0_B
	LOADI	C,ERROR
	JUMP	INDIR
	
	; C = RADIX
5:	STORE	[RADIX],C
	
	; START PARSING THE NUMERIC
0:	STORE	[BUFPNTR],A
	
	; GRAB A CHARACTER
	LOADF	A,[A]
	ADDI	A,0
	BRZ	IRET
	
	; CHECK BOUNDS
	LOADI	B,'0'
	CMP	A,B
	BRB	9B 
	LOADI	B,'9'
	CMP	A,B
	BRBE	1F
	
	LOADI	B,'A'
	CMP	A,B
	BRB	9B
	LOADI	B,'F'
	CMP	A,B
	BRA	9B
	
	SUBI	A,'A'-('0'+10)
	
	; CONVERT FROM ASCII
1:	SUBI	A,'0'

	; COMPARE WITH RADIX
	LOAD	B,[RADIX]
	CMP	A,B
	BRAE	9B

	

