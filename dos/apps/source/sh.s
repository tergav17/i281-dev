; SH.S
; START SHELL SCRIPT
; GAVIN TERSTEEG, 2024
; SDMAY24-14

; MAXIMUM ARGUMENTS
MAXARGS	= 1

	; START BY PROCESSING THE ARGUMENTS
START:	LOADI	A,0
	STORE	[DBANK],A
	
	; RESET ARG STATE
	STORE	[ARGC],A
	
	; SET UP STACK
	LOADI	D,0X60-2
	
	; SYSCALL RETURN BANK
	LOADI	C,1	
	STOREF	[D+1],C
	
	; SET UP POINTER
	LOADI	C,0
	
	; GO TO ARGUMENT BANK
0:	LOAD	A,[CMDL_B]
	STORE	[ARG_BNK],A
	STORE	[DBANK],A

	; SKIP THE CURRENT ARGUMENT
1:	LOADF	A,[C]
	LOADI	B,0X20
	CMP	A,B
	BRBE	2F
	ADDI	C,1
	JUMP	1B

	; LOOK FOR AN ARGUMENT
2:	LOADF	A,[C]
	ADDI	A,0
	BRZ	ARGDONE
	CMP	A,B
	BRA	3F
	ADDI	C,1
	JUMP	2B

	; IS IT A FLAG?
3:	LOADI	B,'-'
	CMP	A,B
	BRNZ	5F
	
	; HANDLE FLAGS HERE	
4:	ADDI	C,1
	LOADF	A,[C]
	ADDI	A,0
	BRZ	ARGDONE
	LOADI	B,0X20
	CMP	A,B
	BRBE	2B
	
	; REGISTER THE FLAG
	LOADI	B,0
	STORE	[DBANK],B
	
	; IS IT A RECOGNIZED FLAG?
	ADDI	B,0
	BRZ	ARGBAD
	LOADI	A,1
	STOREF	[B],A

	; THERE MAY BE ANOTHER FLAG
	LOAD	B,[CMDL_B]
	STORE	[ARG_BNK],B
	STORE	[DBANK],B
	JUMP	4B

	; SAVE THE ARGUMENT
5:	LOADI	B,0
	STORE	[DBANK],B
	LOAD	B,[ARGC]
	STOREF	[B+ARGV],C
	
	; DID WE EXCEED THE ALLOWED NUMBER OF ARGUMENTS
	SUBI	B,MAXARGS
	BRC	ARGBAD
	ADDI	B,MAXARGS+1
	STORE	[ARGC],B
	JUMP	0B
	
	; BAD ARGUMENT
ARGBAD:	LOADI	A,0
	STORE	[DBANK],A
	
	LOADI	A,STR_B
	STORE	[ARG_BNK],A
	LOADI	A,ERROR0
	
	; PRINT ERROR MESSAGE
	LOADI	B,S_PUTS
	LOADI	C,EXIT
	JUMP	SYSCALL	

	; EXIT PROGRAM
EXIT:	LOADI	B,0
	JUMP	SYSJUMP
	
	; ARGUMENT PROCESSING DONE
ARGDONE:LOADI	A,0
	STORE	[DBANK],A
	
	; CHECK ARG COUNT
	LOAD	A,[ARGC]
	SUBI	A,1
	BRNZ	ARGBAD
	
	; COPY FILE NAME
	LOAD	A,[ARGV]
	LOADI	B,AU_FILE
0:	LOADI	C,AU_MISC
	CMP	B,C
	BRZ	ERROR
	LOAD	C,[CMDL_B]
	STORE	[DBANK],C
	LOADF	D,[A]
	LOADI	C,0
	STORE	[DBANK],C
	LOAD	C,[AUTO_B]
	STORE	[DBANK],C
	STOREF	[B],D
	LOADI	C,0
	STORE	[DBANK],C
	ADDI	A,1
	ADDI	B,1
	SUBI	D,0X20
	BRA	0B
	
	; ENABLE AUTO MODE, SET DEFAULT, AND RESET POINTERS
	LOAD	B,[DFT_USR]
	LOAD	C,[AUTO_B]
	STORE	[DBANK],C
	LOADI	A,0XFF
	STORE	[AU_RUN],A
	STORE	[AU_UA],B
	LOADI	A,0
	STORE	[AU_PNTR],A
	STORE	[AU_BANK],A
	STORE	[AU_BLK],A
	STORE	[DBANK],A

	; EXIT PROGRAM
EXIT:	LOADI	B,0
	JUMP	SYSJUMP
	
	; RE-SET UP STACK
ERROR:	LOADI	D,0X60-2
	LOADI	A,STR_B
	STORE	[ARG_BNK],A
	LOADI	A,ERROR1
	
	; PRINT ERROR MESSAGE
	LOADI	B,S_PUTS
	LOADI	C,EXIT
	JUMP	SYSCALL	

.DATA
STR_B	= 1

	; ERROR MESSAGES
.DEFL BYTE ERROR0	"INVALID ARGUMENTS",0X0A,0X0D,
			"USAGE: SH FILE",0X0A,0X0D,0
.DEFL BYTE ERROR1	"FILE NAME TOO LONG",0X0A,0X0D,0
.BANK	0
.BSS

	; COMMAND LINE ARGUMENTS
.DEFL BYTE ARGC		0
.DEFL BYTE ARGV		0,0

	; IS THIS THE FIRST ATTEMPT AT REMOVING?
.DEFL BYTE FIRST	0
