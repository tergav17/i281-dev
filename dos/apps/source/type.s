; TYPE.S
; TYPE OUT A FILE ONTO THE CONSOLE

	; START BY PROCESSING THE ARGUMENTS
START:	LOADI	A,0
	STORE	[DBANK],A
	
	; SET UP STACK
	LOADI	D,0X60-2
	
	; SYSCALL RETURN BANK
	LOADI	C,1	
	STOREF	[D+1],C
	
	; GO TO ARGUMENT BANK
	LOAD	A,[CMDL_B]
	STORE	[ARG_BNK],A
	STORE	[DBANK],A
	
	; SET UP POINTER
	LOADI	C,0
	
	; SKIP THE INITIAL COMMAND
0:	LOADF	A,[C]
	LOADI	B,0X20
	CMP	A,B
	BRBE	1F
	ADDI	C,1
	JUMP	0B

	; LOOK FOR THE FIRST ARGUMENT
1:	LOADF	A,[C]
	ADDI	A,0
	BRZ	2F
	CMP	A,B
	BRA	OPENARG
	ADDI	C,1
	JUMP	1B
	
	; NO ARGUMENT, ERROR
2:	LOADI	A,0
	STORE	[DBANK],A
	
	LOADI	A,1
	STORE	[ARG_BNK],A
	LOADI	A,ERROR0
	
	; PRINT ERROR MESSAGE
	LOADI	B,S_PUTS
	LOADI	C,EXIT
	JUMP	SYSCALL

	
	; THERE IS AN ARG, TRY AND OPEN IT
OPENARG:LOADI	A,0
	STORE	[DBANK],A
	
	; A = FILE STRING START
	MOV	A,C
	
	; OPEN FILE
	LOADI	B,S_OPEN
	LOADI	C,@+2
	JUMP	SYSCALL
	
	; CHECK FOR ERRORS
	ADDI	A,0
	BRZ	0F
	
	; ERROR MESSAGE ARGS
	LOADI	A,1
	STORE	[ARG_BNK],A
	LOADI	A,ERROR1
	
	; PRINT ERROR MESSAGE
	LOADI	B,S_PUTS
	LOADI	C,EXIT
	JUMP	SYSCALL
	
	; RESET STATE
0:	LOADI	A,2
	STORE	[ARG_BNK],A
	LOADI	A,0
	
	; READ BLOCK
READF:	STORE	[COUNT],A
	LOADI	B,S_READ
	LOADI	C,@+2
	JUMP	SYSCALL
	
	; CHECK FOR ERRORS
	ADDI	A,0
	BRNZ	EXIT
	
	; SET DEFAULT POINTER AND BANK
	LOADI	A,0
	LOADI	B,2
	
	; LOOP TO OUTPUT CHARACTER
OUTPUT:	STORE	[POINTER],A
	STORE	[BANK],B 
	STORE	[DBANK],B
	LOADF	A,[A]
	
	; MAKE SURE IT ISN'T 0
	ADDI	A,0
	BRZ	EXIT
	
	; PRINT CHARACTER MESSAGE
	LOADI	B,0
	STORE	[DBANK],B
	LOADI	B,S_PUTC
	LOADI	C,@+2
	JUMP	SYSCALL
	
	; NEXT CHARACTER
	LOAD	A,[POINTER]
	LOAD	B,[BANK]
	ADDI	A,1
	BRNN	OUTPUT
	LOADI	A,0
	ADDI	B,1
	LOADI	C,6
	CMP	B,C
	BRNZ	OUTPUT
	
	; NEXT BLOCK
	LOAD	A,[COUNT]
	ADDI	A,1
	JUMP	READF

	; EXIT PROGRAM
EXIT:	LOADI	B,0
	JUMP	SYSJUMP


.DATA

	; ERROR MESSAGES
.DEFL BYTE ERROR0	"NO ARGUMENT",0X0A,0X0D,0
.DEFL BYTE ERROR1	"CAN'T OPEN FILE",0X0A,0X0D,0

.BANK	0
.BSS

	; READ BLOCK COUNTER
.DEFL BYTE COUNT	0

	; INFORMATION ON WHAT CHARACTER IS BEING PRINTED
.DEFL BYTE POINTER	0
.DEFL BYTE BANK		0