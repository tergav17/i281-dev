00/00:          ; COMMON.S
01/80:          ; DOS/281 SYSTEM COMMON DEFINITIONS
01/80:          
01/80:          ; DEFINES
01/80:          INDIR	= 0X01
01/80:          IRET	= 0X02
01/80:          SYSCALL	= 0X03
01/80:          PRGM	= 0X04
01/80:          HALT	= 0X7F
01/80:          
01/80:          DBANK	= 0X80		; DATA BANK ADDRESS
01/80:          
01/80:          UART	= 0X90		; UART BASE ADDRESS
01/80:          UART_RH	= UART+0X00	; UART READ HOLDING
01/80:          UART_TH	= UART+0X00	; UART TRANSMIT HOLDING
01/80:          UART_LS	= UART+0X05	; UART LINE STATUS
01/80:          SCRATCH	= UART+0X07	; SCRATCH BYTE ADDRESS
01/80:          
01/80:          CF	= 0XA0		; COMPACT FLASH BASE ADDRESS
01/80:          CF_DATA	= CF+0X00	; CF DATA
01/80:          CF_ERR	= CF+0X01	; CF ERROR
01/80:          CF_FEAT	= CF+0x01	; CF FEATURES
01/80:          CF_CNT	= CF+0X02	; CF SECTOR COUNT
01/80:          CF_LBA0	= CF+0X03	; CF LBA BITS 0-7
01/80:          CF_LBA1	= CF+0X04	; CF LBA BITS 8-15
01/80:          CF_LBA2	= CF+0X05	; CF LBA BITS 16-23
01/80:          CF_LBA3	= CF+0X06	; CF LBA BITS 24-27
01/80:          CF_STAT	= CF+0X07	; CF STATUS
01/80:          CF_CMD	= CF+0X07	; CF COMMAND
01/80:          
01/80:          CF_8BIT	= 0X01		; 8 BIT MODE
01/80:          CF_DCAC	= 0X82		; DISABLE CACHE
01/80:          
01/80:          CF_READ	= 0X20		; READ COMMAND
01/80:          CF_WRIT = 0X30		; WRITE COMMAND
01/80:          CF_SETF	= 0XEF		; SET FEATURE COMMAND
01/80:          
01/80:          ; LOW.S
01/80:          ; LOW MEMORY OF THE DOS/281 SYSTEM
01/80:          ; HANDLES SYSCALLS
01/80:          
01/80:          .BANK 0
00/80:          .TEXT
00/80:          
00/80:          	; JUMP STUB FOR HANDLING SYSCALLS
00/80: FFFE     	JUMP	HALT
00/81: FF00     	JUMP	ENTRY
00/83:          
00/82:          
00/82:          	; ENTRY POINT FOR THE KERENL AFTER THE 2ND STAGE BOOT
00/82:          	; JUST JUMP DIRECTLY TO THE 'INIT' ROUTINE IN CORE 
00/82: 34FF     ENTRY:	LOADI	B,CORE0_B
00/83: 3880     	LOADI	C,INIT
00/84: FF7C     	JUMP	INDIR
00/86:          	
00/85:          
00/85:          
00/85:          .DATA
00/85:          
00/00:          
00/00:          
00/00:          
00/00:          	; SET UP UPPER BANK STUFF
00/00:          	; THE 'B_' SYMBOLS WILL BE USED TO KEEP TRACK OF WHAT BANKS ARE
00/00:          	; IN USE
00/00:          	; 'BI" WILL BE USED FOR INSTRUCTION BANKS
00/00:          	; 'BD' WILL BE USED FOR DATA BANKS
00/00:          	; AT THE END OF ASSEMBLY, THESE VALUES WILL BE USED TO DETERMINE
00/00:          	; HOW MUCH FREE MEMORY USER PROGRAMS HAVE
00/00:          BI	= 255
00/00:          BD	= 255
00/00:          
00/00:          ; CORE.S
00/00:          ; MAIN 'GUTS' OF DOS/281
00/00:          
00/00:          .BANK BI
FF/00:          CORE0_B	= BI
FF/00:          .TEXT
FF/00:          
FF/80:          	; SET UP SYSTEM FOR OPERATION
FF/80:          	; BRING UP THE WORK BANK
FF/80: 30FF     INIT:	LOADI	A,WORK_B
FF/81: A080     	STORE	[DBANK],A
FF/83:          	
FF/82:          	; SET THE STACK POINTER TO THE TOP OF THE STACK
FF/82: 3C7E     	LOADI	D,0X80-2
FF/84:          
FF/83:          	; INITALIZE THE TTY DRIVER
FF/83: 38FF     	LOADI	C,CORE0_B
FF/84: BB01     	STOREF	[D+1],C
FF/85: 388A     	LOADI	C,@+5
FF/86: BB00     	STOREF	[D],C
FF/88:          	
FF/87: 34FE     	LOADI	B,TTY0_B
FF/88: 3880     	LOADI	C,TTYINIT
FF/89: FF77     	JUMP	INDIR
FF/8B:          	
FF/8A:          	; SEND OUT THE 'HELLO' SPLASH
FF/8A: 38FF     	LOADI	C,WORK_B
FF/8B: A804     	STORE	[SRC_BNK],C
FF/8C: 3060     	LOADI	A,S_HELLO
FF/8E:          	
FF/8D: 3892     	LOADI	C,@+5
FF/8E: BB00     	STOREF	[D],C
FF/90:          	
FF/8F: 34FE     	LOADI	B,TTY0_B
FF/90: 3888     	LOADI	C,TTYPUTS
FF/91: FF6F     	JUMP	INDIR
FF/93:          	
FF/92:          	; INITALIZE THE BLOCK DRIVER
FF/92: 3897     	LOADI	C,@+5
FF/93: BB00     	STOREF	[D],C
FF/95:          	
FF/94: 34FD     	LOADI	B,BLK0_B
FF/95: 3880     	LOADI	C,BLKINIT
FF/96: FF6A     	JUMP	INDIR
FF/98:          	
FF/97:          	; DOT PROMPT
FF/97: 38FF     PROMPT: LOADI	C,WORK_B
FF/98: A804     	STORE	[SRC_BNK],C
FF/99: 3015     	LOADI	A,S_PRMT
FF/9B:          
FF/9A: 38FF     	LOADI	C,CORE0_B
FF/9B: BB01     	STOREF	[D+1],C
FF/9C: 38A1     	LOADI	C,@+5
FF/9D: BB00     	STOREF	[D],C
FF/9F:          	
FF/9E: 34FE     	LOADI	B,TTY0_B
FF/9F: 3888     	LOADI	C,TTYPUTS
FF/A0: FF60     	JUMP	INDIR
FF/A2:          	
FF/A1:          	; GET AN INPUT FROM THE TERMINAL
FF/A1: 38FE     	LOADI	C,CMDL_B
FF/A2: A804     	STORE	[SRC_BNK],C
FF/A4:          	
FF/A3: 38A8     	LOADI	C,@+5
FF/A4: BB00     	STOREF	[D],C
FF/A6:          	
FF/A5: 34FE     	LOADI	B,TTY0_B
FF/A6: 389A     	LOADI	C,TTYINPT
FF/A7: FF59     	JUMP	INDIR
FF/A9:          	
FF/A8:          	; ATTEMPT TO SET PARAMETERS FOR SEARCH
FF/A8: 3000     	LOADI	A,0
FF/A9: 38AE     	LOADI	C,@+5
FF/AA: BB00     	STOREF	[D],C
FF/AC:          	
FF/AB: 34FC     	LOADI	B,FS0_B
FF/AC: 3880     	LOADI	C,SSPARAM
FF/AD: FF53     	JUMP	INDIR
FF/AF:          	
FF/AE:          	; AND SEARCH FOR IT
FF/AE: 3000     	LOADI	A,0
FF/AF: 38B4     	LOADI	C,@+5
FF/B0: BB00     	STOREF	[D],C
FF/B2:          	
FF/B1: 34FC     	LOADI	B,FS0_B
FF/B2: 38D4     	LOADI	C,FSEARCH
FF/B3: FF4D     	JUMP	INDIR
FF/B5:          	
FF/B4:          	; AND BREAK
FF/B4: FFFF     BP:	JUMP	BP
FF/B6:          	
FF/B5: FFE1     	JUMP	PROMPT
FF/B7:          
FF/B6:          	; BANK IS DONE, MOVE ON TO THE NEXT
FF/B6:          BI	= BI-1
FF/B6:          
FF/B6:          	; ALLOCATE A BANK FOR THE KERNEL WORK AREA
FF/B6:          	; 0X60-7F = KERNEL STACK SPACE
FF/B6:          .BANK	BD
FF/B6:          WORK_B	= BD
FF/B6:          .DATA
FF/B6:          
FF/00:          	; MISC VALUES
FF/00:          	; USED AS TEMP REGISTERS FOR ALL SORTS OF STUFF
FF/00: 00       .DEFL BYTE M0		0
FF/01: 00       .DEFL BYTE M1		0
FF/02: 00       .DEFL BYTE M2		0
FF/03:          
FF/03:          	; DEVICE SPECIFIC MISC VALUES
FF/03:          	; SAFE TO USE IN DEVICE DRIVERS
FF/03: 00       .DEFL BYTE D0		0
FF/04:          
FF/04:          	; SOURCE BANK OF WHATEVER OPERATION IS BEING DONE
FF/04:          	; MAINLY USED TO KEEP TRACK OF THE BANK ADDRESS
FF/04:          	; OF STUFF BEING WORKED ON IN USER SPACE
FF/04: 00       .DEFL BYTE SRC_BNK	0
FF/05:          
FF/05:          	; ADDRESSES FOR THE BLOCK DEVICE DRIVER
FF/05:          	; USED DURING A 'READ' OR 'WRITE' CALL
FF/05: 00       .DEFL BYTE BLK_HI	0
FF/06: 00       .DEFL BYTE BLK_LO	0
FF/07:          
FF/07:          	; PATTERN FOR SEARCHING FOR FILES
FF/07:          	; AND WORKING USER AREA
FF/07: FF       .DEFL BYTE MATCH	0XFF	; THIS IS ALWAYS 0XFF
FF/08: 30       .DEFL BYTE WRK_USR	'0'
FF/09: 3F3F3F3F
       3F3F3F3F .DEFL BYTE[8] PATTERN	"????????"
FF/11:          
FF/11:          	; STATE INFORMATION FOR SEARCHING 
FF/11: 00       .DEFL BYTE SRCH_LO	0	; LOW BLOCK
FF/12: 00       .DEFL BYTE SRCH_BK	0	; CURRENT BANK
FF/13: 00       .DEFL BYTE SRCH_RP	0	; RECORD POINTER
FF/14: 00       .DEFL BYTE SRCH_LS	0	; LAST ENDING
FF/15:          
FF/15:          	; COMMAND LINE PROMPT
FF/15: 0A0D2E00 .DEFL BYTE S_PRMT	0X0A,0X0D,'.',0
FF/19:          
FF/19:          	; PLACE 'HELLO' AT BOTTOM  OF STACK
FF/19:          	; WE DON'T CARE IF IT GETS OVERWRITTEN LATER
FF/19:          .ORG 0X60
FF/60: 444F532F
       32383120
       56312E30
       0A0D00   .DEFL BYTE S_HELLO	"DOS/281 V1.0",0X0A,0X0D,0
FF/6F:          
FF/6F:          
FF/6F:          	; BANK IS DONE, MOVE ON TO THE NEXT
FF/6F:          BD	= BD-1
FF/6F:          
FF/6F:          	; COMMAND LINE BANK
FF/6F:          CMDL_B	= BD
FF/6F:          
FF/6F:          	; BANK IS DONE, MOVE ON TO THE NEXT
FF/6F:          BD	= BD-4
FF/6F:          	
FF/6F:          	; BLOCK ALLOCATION BITMAP
FF/6F:          ABM_B	= BD
FF/6F:          
FF/6F:          	; BANK IS DONE, MOVE ON TO THE NEXT
FF/6F:          BD	= BD-4
FF/6F:          
FF/6F:          	; FILE BLOCK TABLE
FF/6F:          FBT_B	= BD
FF/6F:          
FF/6F:          	; BANK IS DONE, MOVE ON TO THE NEXT
FF/6F:          BD	= BD-4
FF/6F:          
FF/6F:          	; KERNEL BUFFER
FF/6F:          KBUF_B	= BD
FF/6F:          
FF/6F:          	; BANK IS DONE, MOVE ON TO THE NEXT
FF/6F:          BD	= BD-1
FF/6F:          
FF/6F:          
FF/6F:          ; TTY.S
FF/6F:          ; SERIAL CONSOLE DRIVER
FF/6F:          
FF/6F:          .BANK BI
FE/00:          TTY0_B	= BI
FE/00:          .TEXT
FE/00:          
FE/80:          	; SERIAL INIT
FE/80:          	; SET UP THE 16C550 UART
FE/80:          	; ASSUMES WORK BANK IS SELECTED
FE/80:          	; TODO
FE/80:          	; USES: A, B, C
FE/80: FF51     TTYINIT:JUMP	TTYDONE
FE/82:          
FE/81:          	; PUTS A CHARACTER ONTO THE TERMINAL
FE/81:          	; ASSUMES WORK BANK IS SELECTED
FE/81:          	; A = CHARACTER TO PRINT
FE/81:          	; USES: B
FE/81: 8495     TTYPUTC:LOAD	B,[UART_LS]
FE/83:          
FE/82:          	; READ 5TH BIT TO SEE IF WE CAN TRANSMIT YET
FE/82: 4500     	SHIFTL	B
FE/83: 4500     	SHIFTL	B
FE/84: 4500     	SHIFTL	B
FE/85: F1FB     	BRNC	TTYPUTC
FE/87:          	
FE/86:          	; TRANSMITE BYTE
FE/86: A090     	STORE	[UART_TH],A
FE/87: FF4A     	JUMP	TTYDONE
FE/89:          
FE/88:          	; PUTS A STRING FROM USER SPACE ONTO THE TERMINAL
FE/88:          	; ASSUMES WORK BANK IS SELECTED
FE/88:          	; A = ADDRESS OF STRING
FE/88:          	; [SRC_BNK] = DATA BANK OF STRING
FE/88:          	; USES: A, B, C
FE/88: 8404     TTYPUTS:LOAD	B,[SRC_BNK]
FE/89: A480     	STORE	[DBANK],B
FE/8B:          
FE/8A:          	; READ BYTE FROM STRING
FE/8A: 9800     0:	LOADF	C,[A]
FE/8B: 5800     	ADDI	C,0
FE/8C: F645     	BRZ	TTYDONE
FE/8E:          	
FE/8D:          	; GET THE LINE STATUS REGISTER
FE/8D: 8495     1:	LOAD	B,[UART_LS]
FE/8F:          
FE/8E:          	; READ 5TH BIT TO SEE IF WE CAN TRANSMIT YET
FE/8E: 4500     	SHIFTL	B
FE/8F: 4500     	SHIFTL	B
FE/90: 4500     	SHIFTL	B
FE/91: F1FB     	BRNC	1B
FE/93:          	
FE/92:          	; TRANSMIT BYTE
FE/92: A890     	STORE	[UART_TH],C
FE/94:          	
FE/93:          	; INCREMENT POINTER
FE/93: 5001     	ADDI	A,1
FE/95:          	
FE/94:          	; RETURN TO PRINT LOOP
FE/94: FFF5     	JUMP	0B
FE/96:          	
FE/95:          	; WAIT FOR A CHARACTER TO BE TYPED AND THEN RETURNS IT
FE/95:          	; CHARACTER WILL NOT BE ECHOED
FE/95:          	; IF TTYSTAT RETURN 0XFF, THE RETURN WILL BE INSTANT
FE/95:          	; ASSUMES WORK BANK IS SELECTED
FE/95:          	; CHARACTER RETURNED IN A
FE/95:          	; USES: A, B
FE/95: 8495     TTYGETC:LOAD	B,[UART_LS]
FE/97:          
FE/96:          	; READ 1ST BIT
FE/96: C500     	SHIFTR	B
FE/97: F1FD     	BRNC	TTYGETC
FE/99:          	
FE/98:          	; READ BYTE
FE/98: 8090     	LOAD	A,[UART_RH]
FE/99: FF38     	JUMP	TTYDONE
FE/9B:          	
FE/9A:          	; INPUTS A LINE OF CHARACTER INTO A DATA BANK
FE/9A:          	; RESULTING STRING WILL START AT ADDRESS ZERO, AND BE ZERO
FE/9A:          	; TERMINATED
FE/9A:          	; ASSUMES WORK BANK IS SELECTED
FE/9A:          	; [SRC_BNK] = DATA BANK OF RESULT
FE/9A:          	; USES: A, B, C
FE/9A: 8404     TTYINPT:LOAD	B,[SRC_BNK]
FE/9B: A480     	STORE	[DBANK],B
FE/9D:          	
FE/9C:          	; SET POINTER FOR STORAGE
FE/9C: 3800     	LOADI	C,0
FE/9E:          
FE/9D:          	; READ CHARACTER FROM CONSOLE
FE/9D:          	; GET THE LINE STATUS REGISTER
FE/9D: 8495     0:	LOAD	B,[UART_LS]
FE/9F:          
FE/9E:          	; CHECK BIT 0 TO SEE IF WE HAVE A CHARACTER 
FE/9E: C500     	SHIFTR	B
FE/9F: F1FD     	BRNC	0B
FE/A1:          	
FE/A0:          	; READ THE CHARACTER INTO A
FE/A0: 8090     	LOAD	A,[UART_RH]
FE/A2:          
FE/A1:          	; SEE IF IT IS A 'CR' CHARACTER
FE/A1: 340D     1:	LOADI	B,0X0D
FE/A2: D100     	CMP	A,B
FE/A3: F626     	BRZ	9F
FE/A5:          	
FE/A4:          	; SEE IF IT IS A 'BS' CHARACTER
FE/A4: 3408     	LOADI	B,0X08
FE/A5: D100     	CMP	A,B
FE/A6: F615     	BRZ	4F
FE/A8:          	
FE/A7:          	; IGNORE ALL OTHER WHITESPACE CHARACTERS
FE/A7: 3420     	LOADI	B,0X20
FE/A8: D100     	CMP	A,B
FE/A9: F1F3     	BRB	0B
FE/AB:          	
FE/AA:          	; CHECK IF IT IS A DELETE CHARACTER
FE/AA: 347F     	LOADI	B,0X7F
FE/AB: D100     	CMP	A,B
FE/AC: F702     	BRNZ	2F
FE/AE:          	
FE/AD:          	; YEP, CONVERT IT IN TO A 'BS' CHARACTER
FE/AD: 3008     	LOADI	A,0X08
FE/AE: FFF2     	JUMP	1B
FE/B0:          	
FE/AF:          	; CHECK TO SEE IF THERE IS SPACE TO PLACE THE CHARACTERS
FE/AF:          	; INTO THE BUFFER
FE/AF: D600     2:	CMP	B,C
FE/B0: F6EC     	BRZ	0B
FE/B2:          
FE/B1:          	; CONVERT TO UPPER CASE
FE/B1: 3461     	LOADI	B,0X61
FE/B2: D100     	CMP	A,B
FE/B3: F104     	BRB	3F
FE/B4: 347A     	LOADI	B,0X7A
FE/B5: D100     	CMP	A,B
FE/B6: F801     	BRA	3F
FE/B7: 7020     	SUBI	A,0X20
FE/B9:          	
FE/B8:          	; PRINT THE CHARACTER
FE/B8: A090     3:	STORE	[UART_TH],A
FE/BA:          	
FE/B9:          	; PLACE IT INTO THE BUFFER
FE/B9: B200     	STOREF	[C],A
FE/BA: 5801     	ADDI	C,1
FE/BB: FFE1     	JUMP	0B
FE/BD:          
FE/BC:          	; HANDLE BACKSPACE
FE/BC:          	; SUBTRACT ONE FROM THE BUFFER
FE/BC: 5800     4:	ADDI	C,0
FE/BD: F6DF     	BRZ	0B
FE/BE: 7801     	SUBI	C,1
FE/C0:          
FE/BF:          	; ECHO THE CHARACTER
FE/BF: A090     	STORE	[UART_TH],A
FE/C1:          
FE/C0:          	; PRINT AN ADDITIONAL SPACE AND THEN BACKSPACE
FE/C0: 8495     5:	LOAD	B,[UART_LS]
FE/C1: C500     	SHIFTR	B
FE/C2: F1FD     	BRNC	5B
FE/C4:          	
FE/C3: 3420     	LOADI	B,0X20
FE/C4: A490     	STORE	[UART_TH],B
FE/C6:          	
FE/C5: 8495     6:	LOAD	B,[UART_LS]
FE/C6: C500     	SHIFTR	B
FE/C7: F1FD     	BRNC	6B
FE/C9:          	
FE/C8: A090     	STORE	[UART_TH],A
FE/CA:          	
FE/C9: FFD3     	JUMP	0B
FE/CB:          	
FE/CA:          	; TERMINATE THE STRING AND EXIT
FE/CA: 3000     9:	LOADI	A,0
FE/CB: B200     	STOREF	[C],A
FE/CC: FF05     	JUMP	TTYDONE
FE/CE:          	
FE/CD:          
FE/CD:          	; POLL THE UART TO SEE IF THERE IS A CHARACTER WAITING
FE/CD:          	; ASSUMES WORK BANK IS SELECTED
FE/CD:          	; RETURNS A=0XFF IF THERE IS A CHARACTER, OTHERWISE A=0X00
FE/CD: 3000     TTYSTAT:LOADI	A,0
FE/CF:          
FE/CE:          	; GET THE LINE STATUS REGISTER
FE/CE: 8495     	LOAD	B,[UART_LS]
FE/D0:          
FE/CF:          	; READ 1ST BIT
FE/CF: C500     	SHIFTR	B
FE/D0: F101     	BRNC	TTYDONE
FE/D2:          	
FE/D1:          	; SET A AND FALL TO TTYDONE
FE/D1: 30FF     	LOADI	A,0XFF
FE/D3:          
FE/D2:          	; INTERNAL TTY FUNCTION TO RESET TO WORK BANK AND RETURN
FE/D2: 38FF     TTYDONE:LOADI	C,WORK_B
FE/D3: A880     	STORE	[DBANK],C
FE/D4: FF2D     	JUMP	IRET
FE/D6:          
FE/D5:          	; BANK IS DONE, MOVE ON TO THE NEXT
FE/D5:          BI	= BI-1
FE/D5:          ; BLK.S
FE/D5:          ; BLOCK DEVICE DRIVER
FE/D5:          ; (COMPACT FLASH VERSION)
FE/D5:          
FE/D5:          .BANK BI
FD/80:          BLK0_B	= BI
FD/80:          .TEXT
FD/80:          
FD/80:          	; BLOCK DEVICE INIT
FD/80:          	; SET UP THE COMPACT FLASH CARD
FD/80:          	; ASSUMES WORK BANK IS SELECTED
FD/80:          	; USES: A, B, C
FD/80: 3882     BLKINIT:LOADI	C,@+2
FD/81: FF3F     	JUMP	CFWAIT
FD/83:          	
FD/82:          	; SET 8 BIT MODE
FD/82: 3001     	LOADI	A,CF_8BIT
FD/83: A0A1     	STORE	[CF_FEAT],A
FD/84: 30EF     	LOADI	A,CF_SETF
FD/85: A0A7     	STORE	[CF_CMD],A
FD/87:          	
FD/86:          	; NOW DISABLE THE CACHE
FD/86: 3888     	LOADI	C,@+2
FD/87: FF39     	JUMP	CFWAIT
FD/88: 3082     	LOADI	A,CF_DCAC
FD/89: A0A1     	STORE	[CF_FEAT],A
FD/8A: 30EF     	LOADI	A,CF_SETF
FD/8B: A0A7     	STORE	[CF_CMD],A
FD/8D:          
FD/8C: FF3A     	JUMP	BLKDONE
FD/8E:          
FD/8D:          	; READS A BLOCK (512 BYTES) FROM THE BLOCK DEVICE
FD/8D:          	; CONTENTS WILL BE PLACED IN 4 SEQUENTIAL DATA BANKS
FD/8D:          	; ASSUMES WORK BANK IS SELECTED
FD/8D:          	; A = LOWEST DESTINATION DATA BANK
FD/8D:          	; USES: A, B, C, D0
FD/8D: 388F     BLKREAD:LOADI	C,@+2
FD/8E: FF28     	JUMP	CFADDR
FD/90:          	
FD/8F:          	; EXECUTE THE READ COMMAND AND WAIT
FD/8F: 3420     	LOADI	B,CF_READ
FD/90: A4A7     	STORE	[CF_CMD],B
FD/91: 3893     	LOADI	C,@+2
FD/92: FF2E     	JUMP	CFWAIT
FD/94:          	
FD/93:          	; COPY INTO 4 DATA BANKS
FD/93: 3804     	LOADI	C,4
FD/94: A003     0:	STORE	[D0],A
FD/95: A080     	STORE	[DBANK],A
FD/96: 3000     	LOADI	A,0
FD/98:          	
FD/97:          	; COPY 128 BYTES
FD/97: 84A0     1:	LOAD	B,[CF_DATA]
FD/98: B400     	STOREF	[A],B
FD/99: 5001     	ADDI	A,1
FD/9A: F5FC     	BRNN	1B
FD/9C:          	
FD/9B:          	; NEXT BANK?
FD/9B: 30FF     	LOADI	A,WORK_B
FD/9C: A080     	STORE	[DBANK],A
FD/9D: 8003     	LOAD	A,[D0]
FD/9E: 5001     	ADDI	A,1
FD/9F: 7801     	SUBI	C,1
FD/A0: F7F3     	BRNZ	0B
FD/A2:          	
FD/A1:          	; OPERATION COMPLETE
FD/A1: FF25     	JUMP	BLKDONE
FD/A3:          	
FD/A2:          	; WRITES A BLOCK (512 BYTES) TO THE BLOCK DEVICE
FD/A2:          	; CONTENTS WILL BE TAKEN FROM 4 SEQUENTIAL DATA BANKS
FD/A2:          	; ASSUMES WORK BANK IS SELECTED
FD/A2:          	; A = LOWEST SOURCE DATA BANK
FD/A2:          	; USES: A, B, C, D0
FD/A2: 38A4     BLKWRIT:LOADI	C,@+2
FD/A3: FF13     	JUMP	CFADDR
FD/A5:          	
FD/A4:          	; EXECUTE THE WRITE COMMAND AND WAIT
FD/A4: 3430     	LOADI	B,CF_WRIT
FD/A5: A4A7     	STORE	[CF_CMD],B
FD/A6: 38A8     	LOADI	C,@+2
FD/A7: FF19     	JUMP	CFWAIT
FD/A9:          	
FD/A8:          	; COPY OUT OF 4 DATA BANKS
FD/A8: 3804     	LOADI	C,4
FD/A9: A003     0:	STORE	[D0],A
FD/AA: A080     	STORE	[DBANK],A
FD/AB: 3000     	LOADI	A,0
FD/AD:          	
FD/AC:          	; COPY 128 BYTES
FD/AC: 9400     1:	LOADF	B,[A]
FD/AD: A4A0     	STORE	[CF_DATA],B
FD/AE: 5001     	ADDI	A,1
FD/AF: F5FC     	BRNN	1B
FD/B1:          	
FD/B0:          	; NEXT BANK?
FD/B0: 30FF     	LOADI	A,WORK_B
FD/B1: A080     	STORE	[DBANK],A
FD/B2: 8003     	LOAD	A,[D0]
FD/B3: 5001     	ADDI	A,1
FD/B4: 7801     	SUBI	C,1
FD/B5: F7F3     	BRNZ	0B
FD/B7:          	
FD/B6:          	; OPERATION COMPLETE
FD/B6: FF10     	JUMP	BLKDONE
FD/B8:          
FD/B7:          	; SET THE LBA ADDRESS OF THE CF CARD
FD/B7:          	; USING THE BLOCK ADDRESS ON THE WORK BANK
FD/B7:          	; BLOCK COUNT IS ALSO SET
FD/B7:          	; AFTER THAT, A WAIT IS PERFORMED SO A
FD/B7:          	; COMMAND CAN BE EXECUTED AFTER
FD/B7:          	; USES: B
FD/B7: 8406     CFADDR:	LOAD	B,[BLK_LO]
FD/B8: A4A3     	STORE	[CF_LBA0],B
FD/B9: 8405     	LOAD	B,[BLK_HI]
FD/BA: A4A4     	STORE	[CF_LBA1],B
FD/BB: 3400     	LOADI	B,0
FD/BC: A4A5     	STORE	[CF_LBA2],B
FD/BD: 34E0     	LOADI	B,0XE0
FD/BE: A4A6     	STORE	[CF_LBA3],B
FD/BF: 3401     	LOADI	B,1
FD/C0: A4A2     	STORE	[CF_CNT],B
FD/C2:          
FD/C1:          	; WAIT FOR THE CF CARD TO BECOME READY
FD/C1:          	; FOR THIS TO HAPPEN, THE BUSY FLAG MUST BE 0
FD/C1:          	; AND THE READY FLAG MUST BE 1
FD/C1:          	; IF A CF CARD ISN'T PRESENT, THIS ROUTINE HANGS
FD/C1:          	; BUT WE DON'T CARE
FD/C1:          	; USES: B
FD/C1: 84A7     CFWAIT:	LOAD	B,[CF_STAT]
FD/C3:          
FD/C2:          	; CHECK BIT 7 (BUSY FLAG)
FD/C2: 4500     	SHIFTL	B
FD/C4:          	
FD/C3:          	; GO BACK TO START BUSY
FD/C3: F0FD     	BRC	CFWAIT
FD/C5:          	
FD/C4:          	; CHECK BIT 6 (READY FLAG)
FD/C4: 4500     	SHIFTL	B
FD/C6:          	
FD/C5:          	; GO BACK TO START IF NOT READY
FD/C5: F1FB     	BRNC	CFWAIT
FD/C7:          	
FD/C6:          	; RETURN FROM FUNCTION CALL
FD/C6: FE39     	JUMPR	C
FD/C8:          
FD/C7:          
FD/C7:          	; INTERNAL BLK FUNCTION TO RESET TO WORK BANK AND RETURN
FD/C7: 38FF     BLKDONE:LOADI	C,WORK_B
FD/C8: A880     	STORE	[DBANK],C
FD/C9: FF38     	JUMP	IRET
FD/CB:          
FD/CA:          	; BANK IS DONE, MOVE ON TO THE NEXT
FD/CA:          BI	= BI-1
FD/CA:          ; FS.S
FD/CA:          ; FILE SYSTEM HANDLING ROUTINES
FD/CA:          
FD/CA:          .BANK BI
FC/80:          FS0_B	= BI
FC/80:          .TEXT
FC/80:          	; SET SEARCH PARAMETERS
FC/80:          	; USING A STRING IN USER SPACE, THE FILE SEARCH PATTERN WILL
FC/80:          	; BE UPDATED
FC/80:          	; THE FILE SEARCH POINTER WILL ALSO BE RESET
FC/80:          	; ASSUMES WORK BANK IS SELECTED
FC/80:          	; A = ADDRESS OF STRING
FC/80:          	; [SRC_BNK] = DATA BANK OF STRING
FC/80:          	; RETURNS A=0X00 IF PATTERN IS VALID, OTHERWISE 0XFF
FC/80:          	; 0XFE CAN BE RETURNED IF THERE NEVER IS A '.' IN THE FILE
FC/80:          	; USES: A, B, C, M0, M1, M2
FC/80: A000     SSPARAM:STORE	[M0],A
FC/82:          
FC/81:          	; STORE PATTERN COUNTER
FC/81: 3006     	LOADI	A,6
FC/82: A001     	STORE	[M1],A
FC/84:          	
FC/83:          	; STORE PATTERN POINTER
FC/83: 3009     	LOADI	A,PATTERN
FC/84: A002     	STORE	[M2],A
FC/86:          	
FC/85:          	; RESET THE PATTERN
FC/85: 3009     	LOADI	A,PATTERN
FC/86: 3408     	LOADI	B,8
FC/87: 3820     	LOADI	C,0X20
FC/88: B800     0:	STOREF	[A],C
FC/89: 5001     	ADDI	A,1
FC/8A: 7401     	SUBI	B,1
FC/8B: F7FC     	BRNZ	0B
FC/8D:          	
FC/8C:          	; DO AN INITAL CHECKOUT OF THE PATTERN
FC/8C: 8004     	LOAD	A,[SRC_BNK]
FC/8D: 8400     	LOAD	B,[M0]
FC/8E: A080     	STORE	[DBANK],A
FC/90:          	
FC/8F:          	; MAKE SURE THERE IS ACTUALLY A PATTERN
FC/8F: 9900     	LOADF	C,[B]
FC/90: 3020     	LOADI	A,0X20
FC/91: D200     	CMP	A,C
FC/92: F03F     	BRAE	9F
FC/94:          	
FC/93:          	; SEE IF THERE IS A USER AREA PREFIX
FC/93:          	; AND CHANGE BANK TO KERNEL WORK AREA
FC/93: 9101     	LOADF	A,[B+1]
FC/94: 703A     	SUBI	A,':'
FC/95: 30FF     	LOADI	A,WORK_B
FC/96: A080     	STORE	[DBANK],A
FC/97: F709     	BRNZ	1F
FC/99:          	
FC/98:          	; MAKE SURE THAT THE USER AREA PREFIX IS VALID
FC/98: 3030     	LOADI	A,'0'
FC/99: D800     	CMP	C,A
FC/9A: F137     	BRB	9F
FC/9B: 3039     	LOADI	A,'9'
FC/9C: D800     	CMP	C,A
FC/9D: F834     	BRA	9F
FC/9F:          
FC/9E:          	; SET THE WORKING USER AREA
FC/9E: A808     	STORE	[WRK_USR],C
FC/A0:          	
FC/9F:          	; AND SKIP THE PREFIX
FC/9F: 5402     	ADDI	B,2
FC/A0: A400     	STORE	[M0],B
FC/A2:          
FC/A1:          	; GRAB THE NEXT CHARACTER
FC/A1:          	; C = NEXT CHARACTER
FC/A1: 8400     1:	LOAD	B,[M0]
FC/A2: 8004     	LOAD	A,[SRC_BNK]
FC/A3: A080     	STORE	[DBANK],A
FC/A4: 9900     	LOADF	C,[B]
FC/A5: 30FF     	LOADI	A,WORK_B
FC/A6: A080     	STORE	[DBANK],A
FC/A8:          	
FC/A7:          	; INCREMENT AND SAVE POINTER INTO M0
FC/A7: 5401     	ADDI	B,1
FC/A8: A400     	STORE	[M0],B
FC/AA:          	
FC/A9:          	; GRAB PATTERN POINTER
FC/A9: 8002     	LOAD	A,[M2]
FC/AB:          	
FC/AA:          	; SEE IF THE PATTERN IS COMPLETE
FC/AA:          	; AND GRAB PATTERN COUNTER
FC/AA: 3420     	LOADI	B,0X20
FC/AB: D600     	CMP	B,C
FC/AC: 8401     	LOAD	B,[M1]
FC/AD: F01A     	BRAE	6F
FC/AF:          	
FC/AE:          	
FC/AE:          	
FC/AE:          	; SEE IF IT IS A '.'
FC/AE: 782E     	SUBI	C,'.'
FC/AF: F611     	BRZ	5F
FC/B1:          	
FC/B0:          	; THERE MUST BE SPACE IN THE PATTERN BUFFER FOR ANY
FC/B0:          	; OF THE NEXT VALUES TO BE VALID
FC/B0:          	; LETS CHECK IT
FC/B0: 7401     	SUBI	B,1
FC/B1: F420     	BRN	9F
FC/B3:          	
FC/B2:          	; SEE IF IT IS A '*'
FC/B2: 78FC     	SUBI	C,'*'-'.'
FC/B3: F606     	BRZ	3F
FC/B5:          	
FC/B4:          	; OTHERWISE, IT'S A NORMAL CHARACTER
FC/B4:          	; PLACE IT IN THE BUFFER AND MOVE ON
FC/B4: 582A     	ADDI	C,'*'
FC/B5: B800     	STOREF	[A],C
FC/B6: 5001     	ADDI	A,1
FC/B7: A002     2:	STORE	[M2],A
FC/B8: A401     	STORE	[M1],B
FC/B9: FFE7     	JUMP	1B
FC/BB:          
FC/BA:          	; HANDLE '*'
FC/BA:          	; FILL REST OF PATTERN SECTION WITH '?'
FC/BA: 5401     3:	ADDI	B,1
FC/BB: 383F     	LOADI	C,'?'
FC/BC: B800     4:	STOREF	[A],C
FC/BD: 5001     	ADDI	A,1
FC/BE: 7401     	SUBI	B,1
FC/BF: F7FC     	BRNZ	4B
FC/C0: FFF6     	JUMP	2B
FC/C2:          	
FC/C1:          	; HANDLE '.'
FC/C1:          	; MAKE SURE THE LAST 2 BYTES OF THE PATTERN BUFFER ARE EMPTY
FC/C1:          	; AND THERE HASN'T BEEN A '.' ALREADY
FC/C1: 380F     5:	LOADI	C,PATTERN+6
FC/C2: 4100     	ADD	A,B
FC/C3: D200     	CMP	A,C
FC/C4: F80D     	BRA	9F
FC/C6:          	
FC/C5:          	; SET THE NEW PATTERN POINTER
FC/C5: 300F     	LOADI	A,PATTERN+6
FC/C6: 3402     	LOADI	B,2
FC/C7: FFEF     	JUMP	2B
FC/C9:          
FC/C8:          	; PATTERN IS COMPLETE
FC/C8:          	; MAKE SURE ALL FIELDS HAVE BEEN FILLED IN
FC/C8: 380F     6:	LOADI	C,PATTERN+6
FC/C9: D200     	CMP	A,C
FC/CA: F902     	BRBE	7F
FC/CB: 3000     	LOADI	A,0
FC/CC: FF11     	JUMP	FS0DONE
FC/CE:          	
FC/CD:          	; CHECK TO SEE IF A '.' HAS BEEN PLACED
FC/CD: 4100     7:	ADD	A,B
FC/CE: D200     	CMP	A,C
FC/CF: F702     	BRNZ	9F
FC/D0: 30FE     	LOADI	A,0XFE
FC/D1: FF0C     	JUMP	FS0DONE
FC/D3:          	
FC/D2:          	; PATTERN IS INVALID
FC/D2: 30FF     9:	LOADI	A,0XFF
FC/D3: FF0A     	JUMP	FS0DONE
FC/D5:          	
FC/D4:          		
FC/D4:          	; FILE RECORD FORMAT (16 BYTES)
FC/D4:          	; 0:     FILE ALLOCATED TAG
FC/D4:          	;   0X00 = UNALLOCATED
FC/D4:          	;   0XFF = ALLOCATED
FC/D4:          	; 1:     USER AREA
FC/D4:          	;   '0'-'9' FOR USER AREA
FC/D4:          	; 2-7:   FILE NAME
FC/D4:          	;   'A'-'Z','0'-'9','-','_'
FC/D4:          	; 8-9:   FILE EXTENSION
FC/D4:          	;   'A'-'Z','0'-'9','-','_'
FC/D4:          	; 10-11  FILE SIZE IN BLOCKS
FC/D4:          	; 12:    UNUSED
FC/D4:          	; 13-14: FILE BLOCK TABLE ADDRESS
FC/D4:          	;   16 BIT PHYSICAL ADDRESS OF BLOCK TABLE
FC/D4:          	; 15:    NEXT RECORD / END RECORD
FC/D4:          	;   0X00 = HAS NEXT RECORD
FC/D4:          	;   0X01 = NEXT RECORD ON NEXT BLOCK
FC/D4:          	;   0XFF = END OF RECORD 
FC/D4:          	
FC/D4:          	; SEARCHES THE FILE RECORDS UNTIL ONE MATCHING THE CURRENT PATTERN
FC/D4:          	; IS FOUND OR ALL RECORDS ARE EXHAUSTED
FC/D4:          	; ASSUMES WORK BANK IS SELECTED
FC/D4:          	; A = 0 TO RESET THE SEARCH, 1 TO CONTINUE TO NEXT ENTRY
FC/D4:          	; A RETURNS 0X00 IF FOUND, OTHERWISE 0XFF IS RETURNED
FC/D4:          	; USES: A, B, C
FC/D4: 3407     FSEARCH:LOADI	B,MATCH
FC/D5: A402     	STORE	[M2],B
FC/D6: 5000     	ADDI	A,0
FC/D7: 34FB     	LOADI	B,FS1_B
FC/D8: 38B0     	LOADI	C,FSNEXT
FC/D9: F727     	BRNZ	INDIR
FC/DB:          	
FC/DA:          	; RESET SEARCH STATE
FC/DA: 3011     FSRESET:LOADI	A,17
FC/DB: A011     	STORE	[SRCH_LO],A
FC/DD:          	
FC/DC:          	; CHECK THE RECORD
FC/DC: 3880     	LOADI	C,FSSTART
FC/DD: FF23     	JUMP	INDIR
FC/DF:          	
FC/DE:          
FC/DE:          
FC/DE:          	; INTERNAL FS0 FUNCTION TO RESET TO WORK BANK AND RETURN
FC/DE: 38FF     FS0DONE:LOADI	C,WORK_B
FC/DF: A880     	STORE	[DBANK],C
FC/E0: FF21     	JUMP	IRET
FC/E2:          
FC/E1:          	; BANK IS DONE, MOVE ON TO THE NEXT
FC/E1:          BI	= BI-1
FC/E1:          .BANK BI
FB/80:          FS1_B	= BI
FB/80:          .TEXT
FB/80:          
FB/80:          	; START THE FILE SEARCH ON A BLOCK
FB/80:          	; RESET INTER-BLOCK SEARCH STATE
FB/80:          	; AND READ THE BLOCK
FB/80:          	; THEN EXECUTE FSCHECK
FB/80:          	; ASSUMES WORK BANK IS SELECTED
FB/80:          	; USES: A, B, C
FB/80: 3000     FSSTART:LOADI	A,0
FB/81: A013     	STORE	[SRCH_RP],A
FB/82: 30F2     	LOADI	A,KBUF_B
FB/83: A012     	STORE	[SRCH_BK],A
FB/85:          	
FB/84:          	; SET ADDRESS FOR READ
FB/84: 3400     	LOADI	B,0
FB/85: A405     	STORE	[BLK_HI],B
FB/86: 8411     	LOAD	B,[SRCH_LO]
FB/87: A406     	STORE	[BLK_LO],B
FB/89:          	
FB/88:          	; AND EXECUTE A READ INTO THE KBUF
FB/88: 7C02     	SUBI	D,2
FB/89: 38FB     	LOADI	C,FS1_B
FB/8A: BB01     	STOREF	[D+1],C
FB/8B: 3890     	LOADI	C,@+5
FB/8C: BB00     	STOREF	[D],C
FB/8D: 34FD     	LOADI	B,BLK0_B
FB/8E: 388D     	LOADI	C,BLKREAD
FB/8F: FF71     	JUMP	INDIR
FB/90: 5C02     	ADDI	D,2
FB/92:          
FB/91:          	; FILE SEARCH CHECK
FB/91:          	; USING THE CURRENT SEARCH STATE, THE RECORD WILL BE CHECKED
FB/91:          	; AGAINST THE CURRENT PATTERN
FB/91:          	; ASSUMES WORK BANK IS SELECTED
FB/91:          	; A RETURNS 0X00 IF FOUND, OTHERWISE FSNEXT IS CALLED
FB/91:          	; USES: A, B, C, M0, M1
FB/91: 380A     FSCHECK:LOADI	C,10
FB/92: A801     	STORE	[M1],C
FB/94:          
FB/93:          	; RECORD THE CURRENT RECORD ENDING
FB/93: 8013     	LOAD	A,[SRCH_RP]
FB/94: 8412     	LOAD	B,[SRCH_BK]
FB/95: A480     	STORE	[DBANK],B 
FB/96: 980F     	LOADF	C,[A+15]
FB/97: 34FF     	LOADI	B,WORK_B
FB/98: A480     	STORE	[DBANK],B
FB/99: A814     	STORE	[SRCH_LS],C
FB/9B:          	
FB/9A:          	; GET THE PATTERN TO MATCH AGAINST
FB/9A: 8802     	LOAD	C,[M2]
FB/9C:          	
FB/9B:          	; GET BYTE OF MATCH PATTERN
FB/9B: A800     0:	STORE	[M0],C
FB/9C: 9A00     	LOADF	C,[C]
FB/9D: 783F     	SUBI	C,'?'
FB/9E: F608     	BRZ	1F
FB/9F: 583F     	ADDI	C,'?'
FB/A1:          	
FB/A0:          	; SWITCH TO RECORD BANK
FB/A0: 8412     	LOAD	B,[SRCH_BK]
FB/A1: A480     	STORE	[DBANK],B
FB/A3:          	
FB/A2:          	; CHECK AGAINST THE RECORD POINTER
FB/A2: 9400     	LOADF	B,[A]
FB/A3: D600     	CMP	B,C
FB/A4: 34FF     	LOADI	B,WORK_B
FB/A5: A480     	STORE	[DBANK],B
FB/A6: F709     	BRNZ	FSNEXT
FB/A8:          	
FB/A7:          	; THAT ONE MATCHED, KEEP GOING
FB/A7: 5001     1:	ADDI	A,1
FB/A8: 8800     	LOAD	C,[M0]
FB/A9: 5801     	ADDI	C,1
FB/AA: 8401     	LOAD	B,[M1]
FB/AB: 7401     	SUBI	B,1
FB/AC: A401     	STORE	[M1],B
FB/AD: F7ED     	BRNZ	0B
FB/AF:          	
FB/AE:          	; WE HAVE A MATCH!
FB/AE: 3000     	LOADI	A,0
FB/AF: FF13     	JUMP	FS1DONE
FB/B1:          	
FB/B0:          	; INCREMENT TO THE NEXT RECORD
FB/B0:          	; CHECK TO SEE IF THERE IS ACTUALLY A NEXT RECORD
FB/B0: 30FF     FSNEXT:	LOADI	A,0XFF
FB/B1: 8414     	LOAD	B,[SRCH_LS]
FB/B2: 5400     	ADDI	B,0
FB/B3: F40F     	BRN	FS1DONE
FB/B4: F70A     	BRNZ	0F
FB/B6:          	
FB/B5:          	; INCREMENT TO NEXT RECORD ON BANK
FB/B5: 8013     	LOAD	A,[SRCH_RP]
FB/B6: 5010     	ADDI	A,16
FB/B7: A013     	STORE	[SRCH_RP],A
FB/B8: F5D8     	BRNN	FSCHECK
FB/BA:          	
FB/B9:          	; INCREMENT TO NEXT BANK
FB/B9: 3000     	LOADI	A,0
FB/BA: A013     	STORE	[SRCH_RP],A
FB/BB: 8012     	LOAD	A,[SRCH_BK]
FB/BC: 5001     	ADDI	A,1
FB/BD: A012     	STORE	[SRCH_BK],A
FB/BE: FFD2     	JUMP	FSCHECK
FB/C0:          	
FB/BF:          	; MOVE ON TO NEXT BLOCK
FB/BF: 8011     0:	LOAD	A,[SRCH_LO]
FB/C0: 5001     	ADDI	A,1
FB/C1: A011     	STORE	[SRCH_LO],A
FB/C2: FFBD     	JUMP	FSSTART
FB/C4:          	
FB/C3:          
FB/C3:          
FB/C3:          	; INTERNAL FS1 FUNCTION TO RESET TO WORK BANK AND RETURN
FB/C3: 38FF     FS1DONE:LOADI	C,WORK_B
FB/C4: A880     	STORE	[DBANK],C
FB/C5: FF3C     	JUMP	IRET
FB/C7:          
FB/C6:          	; BANK IS DONE, MOVE ON TO THE NEXT
