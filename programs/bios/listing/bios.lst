00/00:          ; BIOS.S
01/00:          ; STANDARD BIOS FOR COMPACT FLASH BOOT
01/00:          ; PLUS STANDARD CALLS
01/00:          
01/00:          ; DEFINES
01/00:          DBANK	= 0X80		; DATA BANK ADDRESS 
01/00:          CF	= 0XA0		; COMPACT FLASH BASE ADDRESS
01/00:          CF_DATA	= CF+0X00	; CF DATA
01/00:          CF_ERR	= CF+0X01	; CF ERROR
01/00:          CF_FEAT	= CF+0x01	; CF FEATURES
01/00:          CF_CNT	= CF+0X02	; CF SECTOR COUNT
01/00:          CF_LBA0	= CF+0X03	; CF LBA BITS 0-7
01/00:          CF_LBA1	= CF+0X04	; CF LBA BITS 8-15
01/00:          CF_LBA2	= CF+0X05	; CF LBA BITS 16-23
01/00:          CF_LBA3	= CF+0X06	; CF LBA BITS 24-27
01/00:          CF_STAT	= CF+0X07	; CF STATUS
01/00:          CF_CMD	= CF+0X07	; CF COMMAND
01/00:          
01/00:          CF_8BIT	= 0X01		; 8 BIT MODE
01/00:          CF_DCAC	= 0X82		; DISABLE CACHE
01/00:          
01/00:          CF_READ	= 0X20		; READ COMMAND
01/00:          CF_SETF	= 0XEF		; SET FEATURE COMMAND
01/00:          
01/00:          .BANK 0
00/00:          .TEXT
00/00:          
00/00:          	; BIOS CALL JUMP TABLE
00/00: FF06     	JUMP	BOOT	; 0: BOOT OPERATION
00/01: FF35     	JUMP	INDIR	; 1: INDIRECT JUMP
00/02: FF36     	JUMP	IRET	; 2: INDIRECT RETURN
00/03: FF39     	JUMP	SYSCALL	; 3: SYSTEM CALL (JUMP TO BANK 0 ADDRESS 0X80, SAVE REGISTER )
00/04: FF39     	JUMP	SYSJUMP	; 4: SYSTEM JUMP (JUMP TO BANK 0 ADDRESS 0X80, DO NOT SAVE C)
00/05: FF3B     	JUMP	PRGM	; 5: PROGRAM FROM DMEM
00/06: FF55     	JUMP	IWRITE	; 6: INDIRECT WRITE
00/08:          	
00/07:          	
00/07:          	
00/07:          	; KICK OFF THE BOOT SEQUENCE
00/07:          	; START BY SETTING THE CARD TO 8-BIT MODE
00/07:          	; WE MUST WAIT FOR THE CF TO BE READY BEFORE SENDING A COMMAND
00/07: 3809     BOOT:	LOADI	C,@+2
00/08: FF19     	JUMP	CFWAIT
00/09: 3001     	LOADI	A,CF_8BIT
00/0A: A0A1     	STORE	[CF_FEAT],A
00/0B: 30EF     	LOADI	A,CF_SETF
00/0C: A0A7     	STORE	[CF_CMD],A
00/0E:          	
00/0D:          	; NOW DISABLE THE CACHE
00/0D: 380F     	LOADI	C,@+2
00/0E: FF13     	JUMP	CFWAIT
00/0F: 3082     	LOADI	A,CF_DCAC
00/10: A0A1     	STORE	[CF_FEAT],A
00/11: 30EF     	LOADI	A,CF_SETF
00/12: A0A7     	STORE	[CF_CMD],A
00/14:          	
00/13:          	; PUNCH IN THE ADDRESS OF THE MBR
00/13:          	; (MASTER BOOT RECORD)
00/13: 3400     	LOADI	B,0
00/14: A4A3     	STORE	[CF_LBA0],B
00/15: A4A4     	STORE	[CF_LBA1],B
00/16: A4A5     	STORE	[CF_LBA2],B
00/18:          	
00/17:          	; THE TOP 3 BITS OF LBA3 MUST BE '1' TO INDICATE LBA MODE
00/17: 30E0     	LOADI	A,0XE0
00/18: A0A6     	STORE	[CF_LBA3],A
00/1A:          	
00/19:          	; PUT A '1' IN THE SECTOR COUNT REGISTER
00/19: 3001     	LOADI	A,0X01
00/1A: A0A2     	STORE	[CF_CNT],A
00/1C:          	
00/1B:          	; SINCE A = 1, LETS SET THE DESTINATION BANKS TOO
00/1B: 0000     	BANK	A
00/1C: A080     	STORE	[DBANK],A
00/1E:          	
00/1D:          	; ISSUE A READ COMMAND!
00/1D: 381F     	LOADI	C,@+2
00/1E: FF03     	JUMP	CFWAIT
00/1F: 3020     	LOADI	A,CF_READ
00/20: A0A7     	STORE	[CF_CMD],A
00/22:          	
00/21:          	; ...AND WAIT FOR IT TO FINISH
00/21: 3828     	LOADI	C,CFBOOT
00/23:          
00/22:          	; WAIT FOR THE CF CARD TO BECOME READY
00/22:          	; FOR THIS TO HAPPEN, THE BUSY FLAG MUST BE 0
00/22:          	; AND THE READY FLAG MUST BE 1
00/22:          	; IF A CF CARD ISN'T PRESENT, THIS ROUTINE HANGS
00/22:          	; BUT WE DON'T CARE
00/22:          	; USES: A
00/22: 80A7     CFWAIT:	LOAD	A,[CF_STAT]
00/24:          
00/23:          	; CHECK BIT 7 (BUSY FLAG)
00/23: 4000     	SHIFTL	A
00/25:          	
00/24:          	; GO BACK TO START BUSY
00/24: F0FD     	BRC	CFWAIT
00/26:          	
00/25:          	; CHECK BIT 6 (READY FLAG)
00/25: 4000     	SHIFTL	A
00/27:          	
00/26:          	; GO BACK TO START IF NOT READY
00/26: F1FB     	BRNC	CFWAIT
00/28:          	
00/27:          	; RETURN FROM FUNCTION CALL
00/27: FED8     	JUMPR	C
00/29:          	
00/28:          	; NOW THE CARD SHOULD BE READY TO SEND BYTES
00/28:          	; WE ARE JUST GOING TO GO OUT ON A LIMB AND
00/28:          	; ASSUME THAT WHATEVER MASTER BOOT RECORD
00/28:          	; WE HAVE BEEN GIVEN IS VALID
00/28:          	; NO USE IN WASTING PRECIOUS INSTRUCTION MEMORY HERE
00/28:          	; SKIP THE FIRST 128 BYTES OF MEMORY
00/28:          	; B SHOULD STILL BE 0 FROM LOADING THE MBR ADDRESS
00/28: 80A0     CFBOOT:	LOAD	A,[CF_DATA]
00/29: 5401     	ADDI	B,1
00/2A: F5FD     	BRNN	CFBOOT
00/2C:          	
00/2B:          	; NOW WE UNPACK THE DATA SEGMENT
00/2B: 3400     	LOADI	B,0
00/2C: 80A0     0:	LOAD	A,[CF_DATA]
00/2D: B100     	STOREF	[B],A
00/2E: 5401     	ADDI	B,1
00/2F: F5FC     	BRNN	0B
00/31:          	
00/30:          	; FINALLY, WE DO THE INSTRUCTION SEGMENT
00/30: 80A0     1:	LOAD	A,[CF_DATA]
00/31: 1400     	CACHE	A
00/32: 80A0     	LOAD	A,[CF_DATA]
00/33: 1600     	WRITE	[B],A
00/34: 5401     	ADDI	B,1
00/35: F4FA     	BRN	1B
00/37:          	
00/36:          	; NOW WE JUMP TO THE LOADED PROGRAM AND HOPE FOR THE BEST!
00/36: FF49     	JUMP	0X80
00/38:          	
00/37:          	; INDIRECT JUMP
00/37:          	; JUMPS TO A SPECIFIC ADDRESS ON A SPECIFIC BANK
00/37:          	; B = DESTINATION ISR BANK
00/37:          	; C = DESTINATION ADDRESS
00/37:          	; USES: NONE
00/37: 0400     INDIR:	BANK	B
00/38: FEC7     	JUMPR	C
00/3A:          	
00/39:          	; INDIRECT RETURN
00/39:          	; JUMPS TO AN BANK / ADDRESS PAIR FOUND IN THE STACK
00/39:          	; [D] = RETURN ADDRESS
00/39:          	; [D+1] = RETURN ISR BANK
00/39:          	; USES: C
00/39: 9B01     IRET:	LOADF	C,[D+1]
00/3A: 0800     	BANK	C
00/3B: 9B00     	LOADF	C,[D]
00/3C: FEC3     	JUMPR	C
00/3E:          	
00/3D:          	; SYSTEM CALL
00/3D:          	; JUMPS TO ADDRESS 0X80 ON ISR BANK 0
00/3D:          	; USES: C
00/3D: BB00     SYSCALL:STOREF	[D],C
00/3E: 3800     SYSJUMP:LOADI	C,0
00/3F: 0800     	BANK	C
00/40: FF3F     	JUMP	0X80
00/42:          
00/41:          	; THIS ROUTINE WILL TAKE 2 PAGES FROM DATA MEMORY
00/41:          	; AND WRITE THEM INTO A BANK OF INSTRUCTION MEMORY
00/41:          	; RETURNS WITH DATA BANK ON 0
00/41:          	; A = DESTINATION ISR BANK
00/41:          	; B = SOURCE DATA BANK
00/41:          	; C = RETURN ADDRESS
00/41:          	; D = RETURN ISR BANK
00/41:          	; USES: BYTE 0X7E-0X7F OF BANK 0, A, B, C, D
00/41: 0000     PRGM:	BANK	A
00/42: 3000     	LOADI	A,0
00/43: A080     	STORE	[DBANK],A
00/44: A87E     	STORE	[0X7E],C
00/45: AC7F     	STORE	[0X7F],D
00/47:          	
00/46:          	; COPY OVER THE DATA
00/46:          	; CRAZY REGISTER SHIT
00/46: 3C80     	LOADI	D,0X80
00/47: A480     0:	STORE	[DBANK],B
00/48: 3800     	LOADI	C,0
00/49: 9200     1:	LOADF	A,[C]
00/4A: 1400     	CACHE	A
00/4B: 9201     	LOADF	A,[C+1]
00/4C: C900     	SHIFTR	C
00/4D: 4B00     	ADD	C,D
00/4E: 1A00     	WRITE	[C],A
00/4F: 6B00     	SUB	C,D
00/50: 4A00     	SHIFTL	C
00/51: 5802     	ADDI	C,2
00/52: F5F6     	BRNN	1B
00/54:          	
00/53:          	; WE REACHED THE END OF A DATA BANK
00/53:          	; MOVE ON TO THE NEXT?
00/53: 5401     	ADDI	B,1
00/54: 5C40     	ADDI	D,0X40
00/55: F4F1     	BRN	0B
00/57:          	
00/56:          	; NOPE, WE ARE DONE
00/56: 3000     	LOADI	A,0
00/57: A080     	STORE	[DBANK],A
00/58: 887E     	LOAD	C,[0X7E]
00/59: 8C7F     	LOAD	D,[0X7F]
00/5A: 0C00     	BANK	D
00/5B: FEA4     	JUMPR	C
00/5D:          	
00/5C:          	; INDIRECT WRITE
00/5C:          	; PERFORMS A WRITE OPERATION ON A SPECIFIC IBANK
00/5C:          	; AN INDIRECT RETURN WILL BE EXECUTED AFTER
00/5C:          	; A = LOWER VALUE TO WRITE
00/5C:          	; B = DESTINATION ADDRESS
00/5C:          	; C = DESTINATION ISR BANK
00/5C:          	; USES: C
00/5C: 0800     IWRITE:	BANK	C
00/5D: 1600     	WRITE	[B],A
00/5E: FFDA     	JUMP	IRET
00/60:          
00/5F:          	; HALT AT THE END OF THE ROM
00/5F:          .ORG	0x7F
