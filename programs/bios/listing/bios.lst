00/00:          ; BIOS.S
01/00:          ; STANDARD BIOS FOR COMPACT FLASH BOOT
01/00:          ; PLUS STANDARD CALLS
01/00:          
01/00:          ; DEFINES
01/00:          DBANK	= 0X80		; DATA BANK ADDRESS 
01/00:          
01/00:          UART	= 0X90		; UART BASE ADDRESS
01/00:          UART_RH	= UART+0X00	; UART READ HOLDING
01/00:          UART_TH	= UART+0X00	; UART TRANSMIT HOLDING
01/00:          UART_DL = UART+0X00	; UART DIVISOR LOW
01/00:          UART_DH = UART+0X01	; UART DIVISOR HIGH
01/00:          UART_LC = UART+0X03	; UART LINE CONTROL
01/00:          UART_LS	= UART+0X05	; UART LINE STATUS
01/00:          SCRATCH	= UART+0X07	; SCRATCH BYTE ADDRESS
01/00:          
01/00:          CF	= 0XA0		; COMPACT FLASH BASE ADDRESS
01/00:          CF_DATA	= CF+0X00	; CF DATA
01/00:          CF_ERR	= CF+0X01	; CF ERROR
01/00:          CF_FEAT	= CF+0x01	; CF FEATURES
01/00:          CF_CNT	= CF+0X02	; CF SECTOR COUNT
01/00:          CF_LBA0	= CF+0X03	; CF LBA BITS 0-7
01/00:          CF_LBA1	= CF+0X04	; CF LBA BITS 8-15
01/00:          CF_LBA2	= CF+0X05	; CF LBA BITS 16-23
01/00:          CF_LBA3	= CF+0X06	; CF LBA BITS 24-27
01/00:          CF_STAT	= CF+0X07	; CF STATUS
01/00:          CF_CMD	= CF+0X07	; CF COMMAND
01/00:          
01/00:          CF_8BIT	= 0X01		; 8 BIT MODE
01/00:          CF_DCAC	= 0X82		; DISABLE CACHE
01/00:          
01/00:          CF_READ	= 0X20		; READ COMMAND
01/00:          CF_SETF	= 0XEF		; SET FEATURE COMMAND
01/00:          
01/00:          .BANK 0
00/00:          .TEXT
00/00:          
00/00:          	; BIOS CALL JUMP TABLE
00/00: FF0B     	JUMP	BOOT	; 0: BOOT OPERATION
00/01: FF3A     	JUMP	UBOOT	; 1: ALT BOOT OPERATION
00/02: FF55     	JUMP	INDIR	; 2: INDIRECT JUMP
00/03: FF58     	JUMP	IRET	; 3: INDIRECT RETURN
00/04: FF5B     	JUMP	SYSCALL	; 4: SYSTEM CALL (JUMP TO BANK 0 ADDRESS 0X80, SAVE REGISTER C)
00/05: FF5B     	JUMP	SYSJUMP	; 5: SYSTEM JUMP (JUMP TO BANK 0 ADDRESS 0X80, DO NOT SAVE C)
00/06: FF5D     	JUMP	PRGM	; 6: PROGRAM 128 INSTRUCTIONS FROM DMEM
00/07: FF52     	JUMP	IWRITE	; 7: INDIRECT WRITE
00/09:          			; 8: ZSTART
00/08:          			
00/08:          	; PREPARES TO EXECUTE A LEGACY PROGRAM
00/08:          	; SETS BANK TO 1 AND JUMPS TO 0X7F
00/08:          	; ONLY REGISTER A IS USED, AND IT WILL BE SET TO 0 ON JUMP
00/08:          	; IT'S A BIT ODD, BUT IT EXISTS SO WE CAN RESET ALL
00/08:          	; OF THE REGISTERS TO 0, PREPARE TO EXECUTE A PROGRAM, AND
00/08:          	; THEN HALT SO A USER CAN SINGLE STEP THE PROGRAM IF THEY WANT
00/08:          	; USES: A
00/08: 3001     ZSTART:	LOADI	A,1
00/09: 0000     	BANK	A
00/0A: 3000     	LOADI	A,0
00/0B: FF73     	JUMP	HALT
00/0D:          	
00/0C:          	; KICK OFF THE BOOT SEQUENCE
00/0C:          	; START BY SETTING THE CARD TO 8-BIT MODE
00/0C:          	; WE MUST WAIT FOR THE CF TO BE READY BEFORE SENDING A COMMAND
00/0C: 380E     BOOT:	LOADI	C,@+2
00/0D: FF19     	JUMP	CFWAIT
00/0E: 3001     	LOADI	A,CF_8BIT
00/0F: A0A1     	STORE	[CF_FEAT],A
00/10: 30EF     	LOADI	A,CF_SETF
00/11: A0A7     	STORE	[CF_CMD],A
00/13:          	
00/12:          	; NOW DISABLE THE CACHE
00/12: 3814     	LOADI	C,@+2
00/13: FF13     	JUMP	CFWAIT
00/14: 3082     	LOADI	A,CF_DCAC
00/15: A0A1     	STORE	[CF_FEAT],A
00/16: 30EF     	LOADI	A,CF_SETF
00/17: A0A7     	STORE	[CF_CMD],A
00/19:          	
00/18:          	; PUNCH IN THE ADDRESS OF THE MBR
00/18:          	; (MASTER BOOT RECORD)
00/18: 3400     	LOADI	B,0
00/19: A4A3     	STORE	[CF_LBA0],B
00/1A: A4A4     	STORE	[CF_LBA1],B
00/1B: A4A5     	STORE	[CF_LBA2],B
00/1D:          	
00/1C:          	; THE TOP 3 BITS OF LBA3 MUST BE '1' TO INDICATE LBA MODE
00/1C: 30E0     	LOADI	A,0XE0
00/1D: A0A6     	STORE	[CF_LBA3],A
00/1F:          	
00/1E:          	; PUT A '1' IN THE SECTOR COUNT REGISTER
00/1E: 3001     	LOADI	A,0X01
00/1F: A0A2     	STORE	[CF_CNT],A
00/21:          	
00/20:          	; SINCE A = 1, LETS SET THE DESTINATION BANKS TOO
00/20: 0000     	BANK	A
00/21: A080     	STORE	[DBANK],A
00/23:          	
00/22:          	; ISSUE A READ COMMAND!
00/22: 3824     	LOADI	C,@+2
00/23: FF03     	JUMP	CFWAIT
00/24: 3020     	LOADI	A,CF_READ
00/25: A0A7     	STORE	[CF_CMD],A
00/27:          	
00/26:          	; ...AND WAIT FOR IT TO FINISH
00/26: 382D     	LOADI	C,CFBOOT
00/28:          
00/27:          	; WAIT FOR THE CF CARD TO BECOME READY
00/27:          	; FOR THIS TO HAPPEN, THE BUSY FLAG MUST BE 0
00/27:          	; AND THE READY FLAG MUST BE 1
00/27:          	; IF A CF CARD ISN'T PRESENT, THIS ROUTINE HANGS
00/27:          	; BUT WE DON'T CARE
00/27:          	; USES: A
00/27: 80A7     CFWAIT:	LOAD	A,[CF_STAT]
00/29:          
00/28:          	; CHECK BIT 7 (BUSY FLAG)
00/28: 4000     	SHIFTL	A
00/2A:          	
00/29:          	; GO BACK TO START BUSY
00/29: F0FD     	BRC	CFWAIT
00/2B:          	
00/2A:          	; CHECK BIT 6 (READY FLAG)
00/2A: 4000     	SHIFTL	A
00/2C:          	
00/2B:          	; GO BACK TO START IF NOT READY
00/2B: F1FB     	BRNC	CFWAIT
00/2D:          	
00/2C:          	; RETURN FROM FUNCTION CALL
00/2C: FED3     	JUMPR	C
00/2E:          	
00/2D:          	; NOW THE CARD SHOULD BE READY TO SEND BYTES
00/2D:          	; WE ARE JUST GOING TO GO OUT ON A LIMB AND
00/2D:          	; ASSUME THAT WHATEVER MASTER BOOT RECORD
00/2D:          	; WE HAVE BEEN GIVEN IS VALID
00/2D:          	; NO USE IN WASTING PRECIOUS INSTRUCTION MEMORY HERE
00/2D:          	; SKIP THE FIRST 128 BYTES OF MEMORY
00/2D:          	; B SHOULD STILL BE 0 FROM LOADING THE MBR ADDRESS
00/2D: 80A0     CFBOOT:	LOAD	A,[CF_DATA]
00/2E: 5401     	ADDI	B,1
00/2F: F5FD     	BRNN	CFBOOT
00/31:          	
00/30:          	; NOW WE UNPACK THE DATA SEGMENT
00/30: 3400     	LOADI	B,0
00/31: 80A0     0:	LOAD	A,[CF_DATA]
00/32: B100     	STOREF	[B],A
00/33: 5401     	ADDI	B,1
00/34: F5FC     	BRNN	0B
00/36:          	
00/35:          	; FINALLY, WE DO THE INSTRUCTION SEGMENT
00/35: 80A0     1:	LOAD	A,[CF_DATA]
00/36: 1400     	CACHE	A
00/37: 80A0     	LOAD	A,[CF_DATA]
00/38: 1600     	WRITE	[B],A
00/39: 5401     	ADDI	B,1
00/3A: F4FA     	BRN	1B
00/3C:          	
00/3B:          	; NOW WE JUMP TO THE LOADED PROGRAM AND HOPE FOR THE BEST!
00/3B: FF44     	JUMP	0X80
00/3D:          	
00/3C:          	; UART BOOTSTRAP
00/3C:          	; STARTS THE MACHINE UP FROM A "VIRTUAL PAPER TAPE"
00/3C:          	; FIRST 512 BYTES WILL BE READ AS A SAV RECORD
00/3C:          	; AND PLACED INTO MEMORY
00/3C:          	; AFTER THE 512TH BYTE IS READ, THE LOADED PROGRAM
00/3C:          	; WILL BE EXECUTED
00/3C:          	; DUE TO DIFFERENCES IN EXPECTED BOOT PROCESS, THE DESTINATION
00/3C:          	; BANK IS 0 INSTEAD OF 1. ONLY THE TEXT SECTION WILL BE LOADED
00/3C:          	; A MORE INTELLIGENT LOADER SHOULD THEN BE USED
00/3C:          	; START BY CLEANING OUT THE RH REGISTER
00/3C: 8090     UBOOT:	LOAD	A,[UART_RH]
00/3E:          
00/3D:          	; SET DLAB
00/3D: 3080     	LOADI	A,0X80
00/3E: A093     	STORE	[UART_LC],A
00/40:          	
00/3F:          	; DIVISOR = 12
00/3F: 300C     	LOADI	A,12
00/40: A090     	STORE	[UART_DL],A
00/41: 3400     	LOADI	B,0
00/42: A491     	STORE	[UART_DH],B
00/44:          	
00/43:          	; SET 8-BIT, 1 STOP, RESET DLAB
00/43: 3003     	LOADI	A,0X03
00/44: A093     	STORE	[UART_LC],A
00/46:          	
00/45:          	; SET BANK
00/45: 0400     	BANK	B
00/47:          	
00/46:          	; SKIP 256 BYTES
00/46: 3848     0:	LOADI	C,@+2
00/47: FF0B     	JUMP	GETC
00/48: 5401     	ADDI	B,1
00/49: F7FC     	BRNZ	0B
00/4B:          	
00/4A:          	; LOAD TEXT SEGMENT
00/4A: 384C     1:	LOADI	C,@+2
00/4B: FF07     	JUMP	GETC
00/4C: 1400     	CACHE	A
00/4D: 384F     	LOADI	C,@+2
00/4E: FF04     	JUMP	GETC
00/4F: 1680     	WRITE	[B+0X80],A
00/50: 5401     	ADDI	B,1
00/51: F5F8     	BRNN	1B
00/53:          	
00/52:          	; NOW WE JUMP TO THE LOADED PROGRAM AND HOPE FOR THE BEST!
00/52: FF2D     	JUMP	0X80
00/54:          	
00/53:          	
00/53:          	; READ A CHARACTER FROM THE UART
00/53: 8095     GETC:	LOAD	A,[UART_LS]
00/54: C100     	SHIFTR	A
00/55: F1FD     	BRNC	GETC
00/56: 8090     	LOAD	A,[UART_RH]
00/57: FEA8     	JUMPR	C
00/59:          	
00/58:          	; INDIRECT JUMP
00/58:          	; JUMPS TO A SPECIFIC ADDRESS ON A SPECIFIC BANK
00/58:          	; B = DESTINATION ISR BANK
00/58:          	; C = DESTINATION ADDRESS
00/58:          	; USES: NONE
00/58: 0400     INDIR:	BANK	B
00/59: FEA6     	JUMPR	C
00/5B:          	
00/5A:          	; INDIRECT WRITE
00/5A:          	; PERFORMS A WRITE OPERATION ON A SPECIFIC IBANK
00/5A:          	; AN INDIRECT RETURN WILL BE EXECUTED AFTER
00/5A:          	; A = LOWER VALUE TO WRITE
00/5A:          	; B = DESTINATION ADDRESS
00/5A:          	; C = DESTINATION ISR BANK
00/5A:          	; USES: C
00/5A: 0800     IWRITE:	BANK	C
00/5B: 1600     	WRITE	[B],A
00/5D:          	
00/5C:          	; INDIRECT RETURN
00/5C:          	; JUMPS TO AN BANK / ADDRESS PAIR FOUND IN THE STACK
00/5C:          	; [D] = RETURN ADDRESS
00/5C:          	; [D+1] = RETURN ISR BANK
00/5C:          	; USES: C
00/5C: 9B01     IRET:	LOADF	C,[D+1]
00/5D: 0800     	BANK	C
00/5E: 9B00     	LOADF	C,[D]
00/5F: FEA0     	JUMPR	C
00/61:          	
00/60:          	; SYSTEM CALL
00/60:          	; JUMPS TO ADDRESS 0X80 ON ISR BANK 0
00/60:          	; USES: C
00/60: BB00     SYSCALL:STOREF	[D],C
00/61: 3800     SYSJUMP:LOADI	C,0
00/62: 0800     	BANK	C
00/63: FF1C     	JUMP	0X80
00/65:          
00/64:          	; THIS ROUTINE WILL TAKE 2 PAGES FROM DATA MEMORY
00/64:          	; AND WRITE THEM INTO A BANK OF INSTRUCTION MEMORY
00/64:          	; RETURNS WITH DATA BANK ON 0
00/64:          	; A = DESTINATION ISR BANK
00/64:          	; B = SOURCE DATA BANK
00/64:          	; C = RETURN ADDRESS
00/64:          	; D = RETURN ISR BANK
00/64:          	; USES: BYTE 0X7E-0X7F OF BANK 0, A, B, C, D
00/64: 0000     PRGM:	BANK	A
00/65: 3000     	LOADI	A,0
00/66: A080     	STORE	[DBANK],A
00/67: A87E     	STORE	[0X7E],C
00/68: AC7F     	STORE	[0X7F],D
00/6A:          	
00/69:          	; COPY OVER THE DATA
00/69:          	; CRAZY REGISTER SHIT
00/69: 3C80     	LOADI	D,0X80
00/6A: A480     0:	STORE	[DBANK],B
00/6B: 3800     	LOADI	C,0
00/6C: 9200     1:	LOADF	A,[C]
00/6D: 1400     	CACHE	A
00/6E: 9201     	LOADF	A,[C+1]
00/6F: C900     	SHIFTR	C
00/70: 4B00     	ADD	C,D
00/71: 1A00     	WRITE	[C],A
00/72: 6B00     	SUB	C,D
00/73: 4A00     	SHIFTL	C
00/74: 5802     	ADDI	C,2
00/75: F5F6     	BRNN	1B
00/77:          	
00/76:          	; WE REACHED THE END OF A DATA BANK
00/76:          	; MOVE ON TO THE NEXT?
00/76: 5401     	ADDI	B,1
00/77: 5C40     	ADDI	D,0X40
00/78: F4F1     	BRN	0B
00/7A:          	
00/79:          	; NOPE, WE ARE DONE
00/79: 3000     	LOADI	A,0
00/7A: A080     	STORE	[DBANK],A
00/7B: 887E     	LOAD	C,[0X7E]
00/7C: 8C7F     	LOAD	D,[0X7F]
00/7D: 0C00     	BANK	D
00/7E: FE81     	JUMPR	C
00/80:          
00/7F:          	; HALT AT THE END OF THE ROM
00/7F:          .ORG	0x7F
00/7F: FFFF     HALT:	JUMP	HALT
